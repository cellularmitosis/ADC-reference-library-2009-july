<html><head><title>Mac OS X
 Manual Page For lwpcook(3pm)</title>
<style type="text/css"><!-- @import "../../../../adcstyle.css"; .char {background: #ffffff; color:#0000ff;}.comment {background:#ffffff; color:#236e25}.function {background:#ffffff; color:#000000;}.keyword {background:#ffffff; color:#761550;}.number {background: #ffffff; color:#0000ff;}.param {background:#ffffff; color:#000000;}.preprocessor {background:#ffffff; color:#236e25}.string {background: #ffffff; color:#891315;}.template {background:#ffffff; color:#761550;}.type {background:#ffffff; color:#761550;}.var {background:#ffffff; color:#000000;} pre.manpages {font-size: 10pt;} .graybox { border-top: 1px solid #919699; border-left: 1px solid #919699; margin-bottom: 10px; } .graybox th { padding: 4px 8px 4px 8px; background: #E2E2E2; font-size: 12px; font-weight: bold; border-bottom: 1px solid #919699; border-right: 1px solid #919699; } .graybox td { padding: 8px; font-size: 12px; text-align: left; vertical-align: top; border-bottom: 1px solid #919699; border-right: 1px solid #919699; } .whiteout { font-size: 1px; background: #ffffff; color: #ffffff; } --></style>
<style type="text/css" media="print, aural, braille, embossed, projection, tty"><!-- @import "../../../../adcstyle.css"; .char {background: #ffffff; color:#0000ff;}.comment {background:#ffffff; color:#236e25}.function {background:#ffffff; color:#000000;}.keyword {background:#ffffff; color:#761550;}.number {background: #ffffff; color:#0000ff;}.param {background:#ffffff; color:#000000;}.preprocessor {background:#ffffff; color:#236e25}.string {background: #ffffff; color:#891315;}.template {background:#ffffff; color:#761550;}.type {background:#ffffff; color:#761550;}.var {background:#ffffff; color:#000000;} pre.manpages {font-size: 10pt;} .graybox { border-top: 1px solid #919699; border-left: 1px solid #919699; margin-bottom: 10px; } .graybox th { padding: 4px 8px 4px 8px; background: #E2E2E2; font-size: 12px; font-weight: bold; border-bottom: 1px solid #919699; border-right: 1px solid #919699; } .graybox td { padding: 8px; font-size: 12px; text-align: left; vertical-align: top; border-bottom: 1px solid #919699; border-right: 1px solid #919699; } .whiteout { display: none; } --></style>
</head><body bgcolor="white">
<a name="//apple_ref/doc/man/3pm/lwpcook" title="Mac OS X
 Manual Page for lwpcook(3pm)"><a name="//apple_ref/doc/man/3/lwpcook" title="Mac OS X
 Manual Page for lwpcook(3pm)"><!-- headerDoc=man; indexgroup=Section 3pm; uid="//apple_ref/doc/man/3pm/lwpcook"; name=lwpcook(3pm) --></a>
<!--#include virtual="/includes/framesetheader"-->
<table width=600><tr><td scope="row">
<!-- begin breadcrumb insert request --><font face="Geneva,Helvetica,Arial" size="1">
 <a target="_top" href="http://developer.apple.com">ADC Home</a> <b>&gt;</b> <a href="../../../../../referencelibrary/index.html#//apple_ref/doc/uid/TP30000943" target="_top">Reference Library</a> <b>&gt;</b> <a href="../../../../../reference/index.html#//apple_ref/doc/uid/TP30001281" target="_top">Reference</a> <b>&gt;</b> <!-- a href="" target="_top" -->Mac OS X<!-- /a --> <b>&gt;</b> <a href="../index.html#//apple_ref/doc/framework/manpages" target="_top">Mac OS X Man Pages</a></font>
<!-- end breadcrumb insert request -->
<p>&nbsp;</p>
</td></tr></table>
<!-- end of header -->
<table border="0"  cellpadding="2" cellspacing="2" width="630pt"><tr><td valign="top" height="12" colspan="5" scope="row">
<p style='text-align:justify;'>This document is a Mac OS X manual page.  Manual pages are a command-line technology
for providing documentation.  You can view these manual pages locally using the
<a href="../man1/man.1.html#//apple_ref/doc/man/1/man">man(1)</a> command.
These manual pages come from many different sources, and thus, have a variety of writing
styles.</p>
<p style='text-align:justify;'>For more information about the manual page format, see the manual page for 
<a href="../man5/manpages.5.html#//apple_ref/doc/man/5/manpages">manpages(5)</a>.</p>
</td></tr></table>
<hr />
<table border="0"  cellpadding="2" cellspacing="2" width="300"><tr><td valign="top" height="12" colspan="5" scope="row"><pre class="manpages"><tt>
lwpcook(3)                           User Contributed Perl Documentation                          lwpcook(3)



<b>NAME</b>
       lwpcook - The libwww-perl cookbook

<b>DESCRIPTION</b>
       This document contain some examples that show typical usage of the libwww-perl library.  You should
       consult the documentation for the individual modules for more detail.

       All examples should be runnable programs. You can, in most cases, test the code sections by piping
       the program text directly to perl.

<b>GET</b>
       It is very easy to use this library to just fetch documents from the net.  The LWP::Simple module
       provides the <u>get()</u> function that return the document specified by its URL argument:

         use LWP::Simple;
         $doc = get '<a href="http://www.linpro.no/lwp/">http://www.linpro.no/lwp/</a>';

       or, as a perl one-liner using the <u>getprint()</u> function:

         perl -MLWP::Simple -e 'getprint "<a href="http://www.linpro.no/lwp/">http://www.linpro.no/lwp/</a>"'

       or, how about fetching the latest perl by running this command:

         perl -MLWP::Simple -e '
           getstore "<a href="ftp://ftp.sunet.se/pub/lang/perl/CPAN/src/latest.tar.gz">ftp://ftp.sunet.se/pub/lang/perl/CPAN/src/latest.tar.gz</a>",
                    "perl.tar.gz"'

       You will probably first want to find a CPAN site closer to you by running something like the
       following command:

         perl -MLWP::Simple -e 'getprint "<a href="http://www.perl.com/perl/CPAN/CPAN.html">http://www.perl.com/perl/CPAN/CPAN.html</a>"'

       Enough of this simple stuff!  The LWP object oriented interface gives you more control over the
       request sent to the server.  Using this interface you have full control over headers sent and how you
       want to handle the response returned.

         use LWP::UserAgent;
         $ua = LWP::UserAgent-&gt;new;
         $ua-&gt;agent("$0/0.1 " . $ua-&gt;agent);
         # $ua-&gt;agent("Mozilla/8.0") # pretend we are very capable browser

         $req = HTTP::Request-&gt;new(GET =&gt; '<a href="http://www.linpro.no/lwp">http://www.linpro.no/lwp</a>');
         $req-&gt;header('Accept' =&gt; 'text/html');

         # send request
         $res = $ua-&gt;request($req);

         # check the outcome
         if ($res-&gt;is_success) {
            print $res-&gt;decoded_content;
         }
         else {
            print "Error: " . $res-&gt;status_line . "\n";
         }

       The lwp-request program (alias GET) that is distributed with the library can also be used to fetch
       documents from WWW servers.

<b>HEAD</b>
       If you just want to check if a document is present (i.e. the URL is valid) try to run code that looks
       like this:

         use LWP::Simple;

         if (head($url)) {
            # ok document exists
         }

       The <u>head()</u> function really returns a list of meta-information about the document.  The first three
       values of the list returned are the document type, the size of the document, and the age of the
       document.

       More control over the request or access to all header values returned require that you use the object
       oriented interface described for GET above.  Just s/GET/HEAD/g.

<b>POST</b>
       There is no simple procedural interface for posting data to a WWW server.  You must use the object
       oriented interface for this. The most common POST operation is to access a WWW form application:

         use LWP::UserAgent;
         $ua = LWP::UserAgent-&gt;new;

         my $req = HTTP::Request-&gt;new(POST =&gt; '<a href="http://www.perl.com/cgi-bin/BugGlimpse">http://www.perl.com/cgi-bin/BugGlimpse</a>');
         $req-&gt;content_type('application/x-www-form-urlencoded');
         $req-&gt;content('match=www&amp;errors=0');

         my $res = $ua-&gt;request($req);
         print $res-&gt;as_string;

       Lazy people use the HTTP::Request::Common module to set up a suitable POST request message (it
       handles all the escaping issues) and has a suitable default for the content_type:

         use HTTP::Request::Common qw(POST);
         use LWP::UserAgent;
         $ua = LWP::UserAgent-&gt;new;

         my $req = POST '<a href="http://www.perl.com/cgi-bin/BugGlimpse">http://www.perl.com/cgi-bin/BugGlimpse</a>',
                       [ search =&gt; 'www', errors =&gt; 0 ];

         print $ua-&gt;request($req)-&gt;as_string;

       The lwp-request program (alias POST) that is distributed with the library can also be used for
       posting data.

<b>PROXIES</b>
       Some sites use proxies to go through fire wall machines, or just as cache in order to improve
       performance.  Proxies can also be used for accessing resources through protocols not supported
       directly (or supported badly :-) by the libwww-perl library.

       You should initialize your proxy setting before you start sending requests:

         use LWP::UserAgent;
         $ua = LWP::UserAgent-&gt;new;
         $ua-&gt;env_proxy; # initialize from environment variables
         # or
         $ua-&gt;proxy(ftp  =&gt; '<a href="http://proxy.myorg.com">http://proxy.myorg.com</a>');
         $ua-&gt;proxy(wais =&gt; '<a href="http://proxy.myorg.com">http://proxy.myorg.com</a>');
         $ua-&gt;no_proxy(qw(no se fi));

         my $req = HTTP::Request-&gt;new(GET =&gt; 'wais://xxx.com/');
         print $ua-&gt;request($req)-&gt;as_string;

       The LWP::Simple interface will call <u>env</u><b>_</b><u>proxy()</u> for you automatically.  Applications that use the
       $ua-&gt;<u>env</u><b>_</b><u>proxy()</u> method will normally not use the $ua-&gt;<u>proxy()</u> and $ua-&gt;<u>no</u><b>_</b><u>proxy()</u> methods.

       Some proxies also require that you send it a username/password in order to let requests through.  You
       should be able to add the required header, with something like this:

        use LWP::UserAgent;

        $ua = LWP::UserAgent-&gt;new;
        $ua-&gt;proxy(['http', 'ftp'] =&gt; '<a href="http://username:password@proxy.myorg.com">http://username:password@proxy.myorg.com</a>');

        $req = HTTP::Request-&gt;new('GET',"<a href="http://www.perl.com">http://www.perl.com</a>");

        $res = $ua-&gt;request($req);
        print $res-&gt;decoded_content if $res-&gt;is_success;

       Replace "proxy.myorg.com", "username" and "password" with something suitable for your site.

<b>ACCESS</b> <b>TO</b> <b>PROTECTED</b> <b>DOCUMENTS</b>
       Documents protected by basic authorization can easily be accessed like this:

         use LWP::UserAgent;
         $ua = LWP::UserAgent-&gt;new;
         $req = HTTP::Request-&gt;new(GET =&gt; '<a href="http://www.linpro.no/secret/">http://www.linpro.no/secret/</a>');
         $req-&gt;authorization_basic('aas', 'mypassword');
         print $ua-&gt;request($req)-&gt;as_string;

       The other alternative is to provide a subclass of <u>LWP::UserAgent</u> that overrides the
       <u>get</u><b>_</b><u>basic</u><b>_</b><u>credentials()</u> method. Study the <u>lwp-request</u> program for an example of this.

<b>COOKIES</b>
       Some sites like to play games with cookies.  By default LWP ignores cookies provided by the servers
       it visits.  LWP will collect cookies and respond to cookie requests if you set up a cookie jar.

         use LWP::UserAgent;
         use HTTP::Cookies;

         $ua = LWP::UserAgent-&gt;new;
         $ua-&gt;cookie_jar(HTTP::Cookies-&gt;new(file =&gt; "lwpcookies.txt",
                                            autosave =&gt; 1));

         # and then send requests just as you used to do
         $res = $ua-&gt;request(HTTP::Request-&gt;new(GET =&gt; "<a href="http://www.yahoo.no">http://www.yahoo.no</a>"));
         print $res-&gt;status_line, "\n";

       As you visit sites that send you cookies to keep, then the file <u>lwpcookies.txt"</u> will grow.

<b>HTTPS</b>
       URLs with https scheme are accessed in exactly the same way as with http scheme, provided that an SSL
       interface module for LWP has been properly installed (see the <u>README.SSL</u> file found in the libwww-
       perl distribution for more details).  If no SSL interface is installed for LWP to use, then you will
       get "501 Protocol scheme 'https' is not supported" errors when accessing such URLs.

       Here's an example of fetching and printing a WWW page using SSL:

         use LWP::UserAgent;

         my $ua = LWP::UserAgent-&gt;new;
         my $req = HTTP::Request-&gt;new(GET =&gt; 'https://www.helsinki.fi/');
         my $res = $ua-&gt;request($req);
         if ($res-&gt;is_success) {
             print $res-&gt;as_string;
         }
         else {
             print "Failed: ", $res-&gt;status_line, "\n";
         }

<b>MIRRORING</b>
       If you want to mirror documents from a WWW server, then try to run code similar to this at regular
       intervals:

         use LWP::Simple;

         %mirrors = (
            '<a href="http://www.sn.no/">http://www.sn.no/</a>'             =&gt; 'sn.html',
            '<a href="http://www.perl.com/">http://www.perl.com/</a>'          =&gt; 'perl.html',
            '<a href="http://www.sn.no/libwww-perl/">http://www.sn.no/libwww-perl/</a>' =&gt; 'lwp.html',
            'gopher://gopher.sn.no/'        =&gt; 'gopher.html',
         );

         while (($url, $localfile) = each(%mirrors)) {
            mirror($url, $localfile);
         }

       Or, as a perl one-liner:

         perl -MLWP::Simple -e 'mirror("<a href="http://www.perl.com/">http://www.perl.com/</a>", "perl.html")';

       The document will not be transfered unless it has been updated.

<b>LARGE</b> <b>DOCUMENTS</b>
       If the document you want to fetch is too large to be kept in memory, then you have two alternatives.
       You can instruct the library to write the document content to a file (second $ua-&gt;<u>request()</u> argument
       is a file name):

         use LWP::UserAgent;
         $ua = LWP::UserAgent-&gt;new;

         my $req = HTTP::Request-&gt;new(GET =&gt;
                       '<a href="http://www.linpro.no/lwp/libwww-perl-5.46.tar.gz">http://www.linpro.no/lwp/libwww-perl-5.46.tar.gz</a>');
         $res = $ua-&gt;request($req, "libwww-perl.tar.gz");
         if ($res-&gt;is_success) {
            print "ok\n";
         }
         else {
            print $res-&gt;status_line, "\n";
         }

       Or you can process the document as it arrives (second $ua-&gt;<u>request()</u> argument is a code reference):

         use LWP::UserAgent;
         $ua = LWP::UserAgent-&gt;new;
         $URL = '<a href="ftp://ftp.unit.no/pub/rfc/rfc-index.txt">ftp://ftp.unit.no/pub/rfc/rfc-index.txt</a>';

         my $expected_length;
         my $bytes_received = 0;
         my $res =
            $ua-&gt;request(HTTP::Request-&gt;new(GET =&gt; $URL),
                      sub {
                          my($chunk, $res) = @_;
                          $bytes_received += length($chunk);
                          unless (defined $expected_length) {
                             $expected_length = $res-&gt;content_length || 0;
                          }
                          if ($expected_length) {
                               printf STDERR "%d%% - ",
                                         100 * $bytes_received / $expected_length;
                          }
                          print STDERR "$bytes_received bytes received\n";

                          # XXX Should really do something with the chunk itself
                          # print $chunk;
                      });
          print $res-&gt;status_line, "\n";

<b>COPYRIGHT</b>
       Copyright 1996-2001, Gisle Aas

       This library is free software; you can redistribute it and/or modify it under the same terms as Perl
       itself.



perl v5.8.8                                      2004-11-30                                       lwpcook(3)
</tt></pre>
</td></tr></table>
	<table width="100%" border="0" cellpadding="0" cellspacing="0">
		<tr>
			<td><div style="width: 100%; height: 1px; background-color: #919699; margin-top: 5px; margin-bottom: 15px"></div></td>
		</tr>
		<tr>
			<td align="center"><br/>
				<table border="0" cellpadding="0" cellspacing="0" class="graybox">
					<tr>
						<th>Did this document help you?</th>
					</tr>
					<tr>
						<td>
						    <div style="margin-bottom: 8px"><a href="http://developer.apple.com/feedback/?v=1&url=/documentation/Darwin/Reference/ManPages/man3/lwpcook.3pm.html%3Fid%3DTP40000894-7.0&media=dvd" target=_new>Yes</a>:  Tell us what works for you.</div>
							<div style="margin-bottom: 8px"><a href="http://developer.apple.com/feedback/?v=2&url=/documentation/Darwin/Reference/ManPages/man3/lwpcook.3pm.html%3Fid%3DTP40000894-7.0&media=dvd" target=_new>It&#8217;s good, but:</a> Report typos, inaccuracies, and so forth.</div>
							<div><a href="http://developer.apple.com/feedback/?v=3&url=/documentation/Darwin/Reference/ManPages/man3/lwpcook.3pm.html%3Fid%3DTP40000894-7.0&media=dvd" target=_new>It wasn&#8217;t helpful</a>: Tell us what would have helped.</div>
						</td>
					</tr>
				</table>
			</td>
		</tr>
	</table>

</body></html>
