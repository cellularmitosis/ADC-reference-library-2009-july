<html><head><title>Mac OS X
 Manual Page For perlbot(1)</title>
<style type="text/css"><!-- @import "../../../../adcstyle.css"; .char {background: #ffffff; color:#0000ff;}.comment {background:#ffffff; color:#236e25}.function {background:#ffffff; color:#000000;}.keyword {background:#ffffff; color:#761550;}.number {background: #ffffff; color:#0000ff;}.param {background:#ffffff; color:#000000;}.preprocessor {background:#ffffff; color:#236e25}.string {background: #ffffff; color:#891315;}.template {background:#ffffff; color:#761550;}.type {background:#ffffff; color:#761550;}.var {background:#ffffff; color:#000000;} pre.manpages {font-size: 10pt;} .graybox { border-top: 1px solid #919699; border-left: 1px solid #919699; margin-bottom: 10px; } .graybox th { padding: 4px 8px 4px 8px; background: #E2E2E2; font-size: 12px; font-weight: bold; border-bottom: 1px solid #919699; border-right: 1px solid #919699; } .graybox td { padding: 8px; font-size: 12px; text-align: left; vertical-align: top; border-bottom: 1px solid #919699; border-right: 1px solid #919699; } .whiteout { font-size: 1px; background: #ffffff; color: #ffffff; } --></style>
<style type="text/css" media="print, aural, braille, embossed, projection, tty"><!-- @import "../../../../adcstyle.css"; .char {background: #ffffff; color:#0000ff;}.comment {background:#ffffff; color:#236e25}.function {background:#ffffff; color:#000000;}.keyword {background:#ffffff; color:#761550;}.number {background: #ffffff; color:#0000ff;}.param {background:#ffffff; color:#000000;}.preprocessor {background:#ffffff; color:#236e25}.string {background: #ffffff; color:#891315;}.template {background:#ffffff; color:#761550;}.type {background:#ffffff; color:#761550;}.var {background:#ffffff; color:#000000;} pre.manpages {font-size: 10pt;} .graybox { border-top: 1px solid #919699; border-left: 1px solid #919699; margin-bottom: 10px; } .graybox th { padding: 4px 8px 4px 8px; background: #E2E2E2; font-size: 12px; font-weight: bold; border-bottom: 1px solid #919699; border-right: 1px solid #919699; } .graybox td { padding: 8px; font-size: 12px; text-align: left; vertical-align: top; border-bottom: 1px solid #919699; border-right: 1px solid #919699; } .whiteout { display: none; } --></style>
</head><body bgcolor="white">
<a name="//apple_ref/doc/man/1/perlbot" title="Mac OS X
 Manual Page for perlbot(1)"><!-- headerDoc=man; indexgroup=Section 1; uid="//apple_ref/doc/man/1/perlbot"; name=perlbot(1) --></a>
<!--#include virtual="/includes/framesetheader"-->
<table width=600><tr><td scope="row">
<!-- begin breadcrumb insert request --><font face="Geneva,Helvetica,Arial" size="1">
 <a target="_top" href="http://developer.apple.com">ADC Home</a> <b>&gt;</b> <a href="../../../../../referencelibrary/index.html#//apple_ref/doc/uid/TP30000943" target="_top">Reference Library</a> <b>&gt;</b> <a href="../../../../../reference/index.html#//apple_ref/doc/uid/TP30001281" target="_top">Reference</a> <b>&gt;</b> <!-- a href="" target="_top" -->Mac OS X<!-- /a --> <b>&gt;</b> <a href="../index.html#//apple_ref/doc/framework/manpages" target="_top">Mac OS X Man Pages</a></font>
<!-- end breadcrumb insert request -->
<p>&nbsp;</p>
</td></tr></table>
<!-- end of header -->
<table border="0"  cellpadding="2" cellspacing="2" width="630pt"><tr><td valign="top" height="12" colspan="5" scope="row">
<p style='text-align:justify;'>This document is a Mac OS X manual page.  Manual pages are a command-line technology
for providing documentation.  You can view these manual pages locally using the
<a href="man.1.html#//apple_ref/doc/man/1/man">man(1)</a> command.
These manual pages come from many different sources, and thus, have a variety of writing
styles.</p>
<p style='text-align:justify;'>For more information about the manual page format, see the manual page for 
<a href="../man5/manpages.5.html#//apple_ref/doc/man/5/manpages">manpages(5)</a>.</p>
</td></tr></table>
<hr />
<table border="0"  cellpadding="2" cellspacing="2" width="300"><tr><td valign="top" height="12" colspan="5" scope="row"><pre class="manpages"><tt>
PERLBOT(1)                            Perl Programmers Reference Guide                            PERLBOT(1)



<b>NAME</b>
       perlbot - Bag'o Object Tricks (the BOT)

<b>DESCRIPTION</b>
       The following collection of tricks and hints is intended to whet curious appetites about such things
       as the use of instance variables and the mechanics of object and class relationships.  The reader is
       encouraged to consult relevant textbooks for discussion of Object Oriented definitions and methodol-<font color="#ffffff" class="whiteout">ogy.&nbsp;methodology.</font>
       ogy.  This is not intended as a tutorial for object-oriented programming or as a comprehensive guide
       to Perl's object oriented features, nor should it be construed as a style guide.  If you're looking
       for tutorials, be sure to read perlboot, perltoot, and perltooc.

       The Perl motto still holds:  There's more than one way to do it.

<b>OO</b> <b>SCALING</b> <b>TIPS</b>
       1    Do not attempt to verify the type of $self.  That'll break if the class is inherited, when the
            type of $self is valid but its package isn't what you expect.  See rule 5.

       2    If an object-oriented (OO) or indirect-object (IO) syntax was used, then the object is probably
            the correct type and there's no need to become paranoid about it.  Perl isn't a paranoid lan-<font color="#ffffff" class="whiteout">guage&nbsp;language</font>
            guage anyway.  If people subvert the OO or IO syntax then they probably know what they're doing
            and you should let them do it.  See rule 1.

       3    Use the two-argument form of <u>bless()</u>.  Let a subclass use your constructor.  See "INHERITING A
            CONSTRUCTOR".

       4    The subclass is allowed to know things about its immediate superclass, the superclass is allowed
            to know nothing about a subclass.

       5    Don't be trigger happy with inheritance.  A "using", "containing", or "delegation" relationship
            (some sort of aggregation, at least) is often more appropriate.  See "OBJECT RELATIONSHIPS",
            "USING RELATIONSHIP WITH SDBM", and "DELEGATION".

       6    The object is the namespace.  Make package globals accessible via the object.  This will remove
            the guess work about the symbol's home package.  See "CLASS CONTEXT AND THE OBJECT".

       7    IO syntax is certainly less noisy, but it is also prone to ambiguities that can cause difficult-<font color="#ffffff" class="whiteout">to-find&nbsp;difficultto-find</font>
            to-find bugs.  Allow people to use the sure-thing OO syntax, even if you don't like it.

       8    Do not use function-call syntax on a method.  You're going to be bitten someday.  Someone might
            move that method into a superclass and your code will be broken.  On top of that you're feeding
            the paranoia in rule 2.

       9    Don't assume you know the home package of a method.  You're making it difficult for someone to
            override that method.  See "THINKING OF CODE REUSE".

<b>INSTANCE</b> <b>VARIABLES</b>
       An anonymous array or anonymous hash can be used to hold instance variables.  Named parameters are
       also demonstrated.

               package Foo;

               sub new {
                       my $type = shift;
                       my %params = @_;
                       my $self = {};
                       $self-&gt;{'High'} = $params{'High'};
                       $self-&gt;{'Low'}  = $params{'Low'};
                       bless $self, $type;
               }

               package Bar;

               sub new {
                       my $type = shift;
                       my %params = @_;
                       my $self = [];
                       $self-&gt;[0] = $params{'Left'};
                       $self-&gt;[1] = $params{'Right'};
                       bless $self, $type;
               }

               package main;

               $a = Foo-&gt;new( 'High' =&gt; 42, 'Low' =&gt; 11 );
               print "High=$a-&gt;{'High'}\n";
               print "Low=$a-&gt;{'Low'}\n";

               $b = Bar-&gt;new( 'Left' =&gt; 78, 'Right' =&gt; 40 );
               print "Left=$b-&gt;[0]\n";
               print "Right=$b-&gt;[1]\n";

<b>SCALAR</b> <b>INSTANCE</b> <b>VARIABLES</b>
       An anonymous scalar can be used when only one instance variable is needed.

               package Foo;

               sub new {
                       my $type = shift;
                       my $self;
                       $self = shift;
                       bless \$self, $type;
               }

               package main;

               $a = Foo-&gt;new( 42 );
               print "a=$$a\n";

<b>INSTANCE</b> <b>VARIABLE</b> <b>INHERITANCE</b>
       This example demonstrates how one might inherit instance variables from a superclass for inclusion in
       the new class.  This requires calling the superclass's constructor and adding one's own instance
       variables to the new object.

               package Bar;

               sub new {
                       my $type = shift;
                       my $self = {};
                       $self-&gt;{'buz'} = 42;
                       bless $self, $type;
               }

               package Foo;
               @ISA = qw( Bar );

               sub new {
                       my $type = shift;
                       my $self = Bar-&gt;new;
                       $self-&gt;{'biz'} = 11;
                       bless $self, $type;
               }

               package main;

               $a = Foo-&gt;new;
               print "buz = ", $a-&gt;{'buz'}, "\n";
               print "biz = ", $a-&gt;{'biz'}, "\n";

<b>OBJECT</b> <b>RELATIONSHIPS</b>
       The following demonstrates how one might implement "containing" and "using" relationships between
       objects.

               package Bar;

               sub new {
                       my $type = shift;
                       my $self = {};
                       $self-&gt;{'buz'} = 42;
                       bless $self, $type;
               }

               package Foo;

               sub new {
                       my $type = shift;
                       my $self = {};
                       $self-&gt;{'Bar'} = Bar-&gt;new;
                       $self-&gt;{'biz'} = 11;
                       bless $self, $type;
               }

               package main;

               $a = Foo-&gt;new;
               print "buz = ", $a-&gt;{'Bar'}-&gt;{'buz'}, "\n";
               print "biz = ", $a-&gt;{'biz'}, "\n";

<b>OVERRIDING</b> <b>SUPERCLASS</b> <b>METHODS</b>
       The following example demonstrates how to override a superclass method and then call the overridden
       method.  The <b>SUPER</b> pseudo-class allows the programmer to call an overridden superclass method without
       actually knowing where that method is defined.

               package Buz;
               sub goo { print "here's the goo\n" }

               package Bar; @ISA = qw( Buz );
               sub google { print "google here\n" }

               package Baz;
               sub mumble { print "mumbling\n" }

               package Foo;
               @ISA = qw( Bar Baz );

               sub new {
                       my $type = shift;
                       bless [], $type;
               }
               sub grr { print "grumble\n" }
               sub goo {
                       my $self = shift;
                       $self-&gt;SUPER::goo();
               }
               sub mumble {
                       my $self = shift;
                       $self-&gt;SUPER::mumble();
               }
               sub google {
                       my $self = shift;
                       $self-&gt;SUPER::google();
               }

               package main;

               $foo = Foo-&gt;new;
               $foo-&gt;mumble;
               $foo-&gt;grr;
               $foo-&gt;goo;
               $foo-&gt;google;

       Note that "SUPER" refers to the superclasses of the current package ("Foo"), not to the superclasses
       of $self.

<b>USING</b> <b>RELATIONSHIP</b> <b>WITH</b> <b>SDBM</b>
       This example demonstrates an interface for the SDBM class.  This creates a "using" relationship
       between the SDBM class and the new class Mydbm.

               package Mydbm;

               require SDBM_File;
               require Tie::Hash;
               @ISA = qw( Tie::Hash );

               sub TIEHASH {
                   my $type = shift;
                   my $ref  = SDBM_File-&gt;new(@_);
                   bless {'dbm' =&gt; $ref}, $type;
               }
               sub FETCH {
                   my $self = shift;
                   my $ref  = $self-&gt;{'dbm'};
                   $ref-&gt;FETCH(@_);
               }
               sub STORE {
                   my $self = shift;
                   if (defined $_[0]){
                       my $ref = $self-&gt;{'dbm'};
                       $ref-&gt;STORE(@_);
                   } else {
                       die "Cannot STORE an undefined key in Mydbm\n";
                   }
               }

               package main;
               use Fcntl qw( O_RDWR O_CREAT );

               tie %foo, "Mydbm", "Sdbm", O_RDWR|O_CREAT, 0640;
               $foo{'bar'} = 123;
               print "foo-bar = $foo{'bar'}\n";

               tie %bar, "Mydbm", "Sdbm2", O_RDWR|O_CREAT, 0640;
               $bar{'Cathy'} = 456;
               print "bar-Cathy = $bar{'Cathy'}\n";

<b>THINKING</b> <b>OF</b> <b>CODE</b> <b>REUSE</b>
       One strength of Object-Oriented languages is the ease with which old code can use new code.  The fol-<font color="#ffffff" class="whiteout">lowing&nbsp;following</font>
       lowing examples will demonstrate first how one can hinder code reuse and then how one can promote
       code reuse.

       This first example illustrates a class which uses a fully-qualified method call to access the "pri-<font color="#ffffff" class="whiteout">vate"&nbsp;"private"</font>
       vate" method <u>BAZ()</u>.  The second example will show that it is impossible to override the <u>BAZ()</u> method.

               package FOO;

               sub new {
                       my $type = shift;
                       bless {}, $type;
               }
               sub bar {
                       my $self = shift;
                       $self-&gt;FOO::private::BAZ;
               }

               package FOO::private;

               sub BAZ {
                       print "in BAZ\n";
               }

               package main;

               $a = FOO-&gt;new;
               $a-&gt;bar;

       Now we try to override the <u>BAZ()</u> method.  We would like <u>FOO::bar()</u> to call <u>GOOP::BAZ()</u>, but this can-<font color="#ffffff" class="whiteout">not&nbsp;cannot</font>
       not happen because <u>FOO::bar()</u> explicitly calls <u>FOO::private::BAZ()</u>.

               package FOO;

               sub new {
                       my $type = shift;
                       bless {}, $type;
               }
               sub bar {
                       my $self = shift;
                       $self-&gt;FOO::private::BAZ;
               }

               package FOO::private;

               sub BAZ {
                       print "in BAZ\n";
               }

               package GOOP;
               @ISA = qw( FOO );
               sub new {
                       my $type = shift;
                       bless {}, $type;
               }

               sub BAZ {
                       print "in GOOP::BAZ\n";
               }

               package main;

               $a = GOOP-&gt;new;
               $a-&gt;bar;

       To create reusable code we must modify class FOO, flattening class FOO::private.  The next example
       shows a reusable class FOO which allows the method <u>GOOP::BAZ()</u> to be used in place of <u>FOO::BAZ()</u>.

               package FOO;

               sub new {
                       my $type = shift;
                       bless {}, $type;
               }
               sub bar {
                       my $self = shift;
                       $self-&gt;BAZ;
               }

               sub BAZ {
                       print "in BAZ\n";
               }

               package GOOP;
               @ISA = qw( FOO );

               sub new {
                       my $type = shift;
                       bless {}, $type;
               }
               sub BAZ {
                       print "in GOOP::BAZ\n";
               }

               package main;

               $a = GOOP-&gt;new;
               $a-&gt;bar;

<b>CLASS</b> <b>CONTEXT</b> <b>AND</b> <b>THE</b> <b>OBJECT</b>
       Use the object to solve package and class context problems.  Everything a method needs should be
       available via the object or should be passed as a parameter to the method.

       A class will sometimes have static or global data to be used by the methods.  A subclass may want to
       override that data and replace it with new data.  When this happens the superclass may not know how
       to find the new copy of the data.

       This problem can be solved by using the object to define the context of the method.  Let the method
       look in the object for a reference to the data.  The alternative is to force the method to go hunting
       for the data ("Is it in my class, or in a subclass?  Which subclass?"), and this can be inconvenient
       and will lead to hackery.  It is better just to let the object tell the method where that data is
       located.

               package Bar;

               %fizzle = ( 'Password' =&gt; 'XYZZY' );

               sub new {
                       my $type = shift;
                       my $self = {};
                       $self-&gt;{'fizzle'} = \%fizzle;
                       bless $self, $type;
               }

               sub enter {
                       my $self = shift;

                       # Don't try to guess if we should use %Bar::fizzle
                       # or %Foo::fizzle.  The object already knows which
                       # we should use, so just ask it.
                       #
                       my $fizzle = $self-&gt;{'fizzle'};

                       print "The word is ", $fizzle-&gt;{'Password'}, "\n";
               }

               package Foo;
               @ISA = qw( Bar );

               %fizzle = ( 'Password' =&gt; 'Rumple' );

               sub new {
                       my $type = shift;
                       my $self = Bar-&gt;new;
                       $self-&gt;{'fizzle'} = \%fizzle;
                       bless $self, $type;
               }

               package main;

               $a = Bar-&gt;new;
               $b = Foo-&gt;new;
               $a-&gt;enter;
               $b-&gt;enter;

<b>INHERITING</b> <b>A</b> <b>CONSTRUCTOR</b>
       An inheritable constructor should use the second form of <u>bless()</u> which allows blessing directly into
       a specified class.  Notice in this example that the object will be a BAR not a FOO, even though the
       constructor is in class FOO.

               package FOO;

               sub new {
                       my $type = shift;
                       my $self = {};
                       bless $self, $type;
               }

               sub baz {
                       print "in FOO::baz()\n";
               }

               package BAR;
               @ISA = qw(FOO);

               sub baz {
                       print "in BAR::baz()\n";
               }

               package main;

               $a = BAR-&gt;new;
               $a-&gt;baz;

<b>DELEGATION</b>
       Some classes, such as SDBM_File, cannot be effectively subclassed because they create foreign
       objects.  Such a class can be extended with some sort of aggregation technique such as the "using"
       relationship mentioned earlier or by delegation.

       The following example demonstrates delegation using an <u>AUTOLOAD()</u> function to perform message-for-<font color="#ffffff" class="whiteout">warding.&nbsp;message-forwarding.</font>
       warding.  This will allow the Mydbm object to behave exactly like an SDBM_File object.  The Mydbm
       class could now extend the behavior by adding custom <u>FETCH()</u> and <u>STORE()</u> methods, if this is desired.

               package Mydbm;

               require SDBM_File;
               require Tie::Hash;
               @ISA = qw(Tie::Hash);

               sub TIEHASH {
                       my $type = shift;
                       my $ref = SDBM_File-&gt;new(@_);
                       bless {'delegate' =&gt; $ref};
               }

               sub AUTOLOAD {
                       my $self = shift;

                       # The Perl interpreter places the name of the
                       # message in a variable called $AUTOLOAD.

                       # DESTROY messages should never be propagated.
                       return if $AUTOLOAD =~ /::DESTROY$/;

                       # Remove the package name.
                       $AUTOLOAD =~ s/^Mydbm:://;

                       # Pass the message to the delegate.
                       $self-&gt;{'delegate'}-&gt;$AUTOLOAD(@_);
               }

               package main;
               use Fcntl qw( O_RDWR O_CREAT );

               tie %foo, "Mydbm", "adbm", O_RDWR|O_CREAT, 0640;
               $foo{'bar'} = 123;
               print "foo-bar = $foo{'bar'}\n";

<b>SEE</b> <b>ALSO</b>
       perlboot, perltoot, perltooc.



perl v5.8.8                                      2006-01-07                                       PERLBOT(1)
</tt></pre>
</td></tr></table>
	<table width="100%" border="0" cellpadding="0" cellspacing="0">
		<tr>
			<td><div style="width: 100%; height: 1px; background-color: #919699; margin-top: 5px; margin-bottom: 15px"></div></td>
		</tr>
		<tr>
			<td align="center"><br/>
				<table border="0" cellpadding="0" cellspacing="0" class="graybox">
					<tr>
						<th>Did this document help you?</th>
					</tr>
					<tr>
						<td>
						    <div style="margin-bottom: 8px"><a href="http://developer.apple.com/feedback/?v=1&url=/documentation/Darwin/Reference/ManPages/man1/perlbot.1.html%3Fid%3DTP40000894-7.0&media=dvd" target=_new>Yes</a>:  Tell us what works for you.</div>
							<div style="margin-bottom: 8px"><a href="http://developer.apple.com/feedback/?v=2&url=/documentation/Darwin/Reference/ManPages/man1/perlbot.1.html%3Fid%3DTP40000894-7.0&media=dvd" target=_new>It&#8217;s good, but:</a> Report typos, inaccuracies, and so forth.</div>
							<div><a href="http://developer.apple.com/feedback/?v=3&url=/documentation/Darwin/Reference/ManPages/man1/perlbot.1.html%3Fid%3DTP40000894-7.0&media=dvd" target=_new>It wasn&#8217;t helpful</a>: Tell us what would have helped.</div>
						</td>
					</tr>
				</table>
			</td>
		</tr>
	</table>

</body></html>
