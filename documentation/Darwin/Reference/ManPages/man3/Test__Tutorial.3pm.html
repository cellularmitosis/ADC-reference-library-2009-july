<html><head><title>Mac OS X
 Manual Page For Test::Tutorial(3pm)</title>
<style type="text/css"><!-- @import "../../../../adcstyle.css"; .char {background: #ffffff; color:#0000ff;}.comment {background:#ffffff; color:#236e25}.function {background:#ffffff; color:#000000;}.keyword {background:#ffffff; color:#761550;}.number {background: #ffffff; color:#0000ff;}.param {background:#ffffff; color:#000000;}.preprocessor {background:#ffffff; color:#236e25}.string {background: #ffffff; color:#891315;}.template {background:#ffffff; color:#761550;}.type {background:#ffffff; color:#761550;}.var {background:#ffffff; color:#000000;} pre.manpages {font-size: 10pt;} .graybox { border-top: 1px solid #919699; border-left: 1px solid #919699; margin-bottom: 10px; } .graybox th { padding: 4px 8px 4px 8px; background: #E2E2E2; font-size: 12px; font-weight: bold; border-bottom: 1px solid #919699; border-right: 1px solid #919699; } .graybox td { padding: 8px; font-size: 12px; text-align: left; vertical-align: top; border-bottom: 1px solid #919699; border-right: 1px solid #919699; } .whiteout { font-size: 1px; background: #ffffff; color: #ffffff; } --></style>
<style type="text/css" media="print, aural, braille, embossed, projection, tty"><!-- @import "../../../../adcstyle.css"; .char {background: #ffffff; color:#0000ff;}.comment {background:#ffffff; color:#236e25}.function {background:#ffffff; color:#000000;}.keyword {background:#ffffff; color:#761550;}.number {background: #ffffff; color:#0000ff;}.param {background:#ffffff; color:#000000;}.preprocessor {background:#ffffff; color:#236e25}.string {background: #ffffff; color:#891315;}.template {background:#ffffff; color:#761550;}.type {background:#ffffff; color:#761550;}.var {background:#ffffff; color:#000000;} pre.manpages {font-size: 10pt;} .graybox { border-top: 1px solid #919699; border-left: 1px solid #919699; margin-bottom: 10px; } .graybox th { padding: 4px 8px 4px 8px; background: #E2E2E2; font-size: 12px; font-weight: bold; border-bottom: 1px solid #919699; border-right: 1px solid #919699; } .graybox td { padding: 8px; font-size: 12px; text-align: left; vertical-align: top; border-bottom: 1px solid #919699; border-right: 1px solid #919699; } .whiteout { display: none; } --></style>
</head><body bgcolor="white">
<a name="//apple_ref/doc/man/3pm/Test::Tutorial" title="Mac OS X
 Manual Page for Test::Tutorial(3pm)"><a name="//apple_ref/doc/man/3/Test::Tutorial" title="Mac OS X
 Manual Page for Test::Tutorial(3pm)"><!-- headerDoc=man; indexgroup=Section 3pm; uid="//apple_ref/doc/man/3pm/Test::Tutorial"; name=Test::Tutorial(3pm) --></a>
<!--#include virtual="/includes/framesetheader"-->
<table width=600><tr><td scope="row">
<!-- begin breadcrumb insert request --><font face="Geneva,Helvetica,Arial" size="1">
 <a target="_top" href="http://developer.apple.com">ADC Home</a> <b>&gt;</b> <a href="../../../../../referencelibrary/index.html#//apple_ref/doc/uid/TP30000943" target="_top">Reference Library</a> <b>&gt;</b> <a href="../../../../../reference/index.html#//apple_ref/doc/uid/TP30001281" target="_top">Reference</a> <b>&gt;</b> <!-- a href="" target="_top" -->Mac OS X<!-- /a --> <b>&gt;</b> <a href="../index.html#//apple_ref/doc/framework/manpages" target="_top">Mac OS X Man Pages</a></font>
<!-- end breadcrumb insert request -->
<p>&nbsp;</p>
</td></tr></table>
<!-- end of header -->
<table border="0"  cellpadding="2" cellspacing="2" width="630pt"><tr><td valign="top" height="12" colspan="5" scope="row">
<p style='text-align:justify;'>This document is a Mac OS X manual page.  Manual pages are a command-line technology
for providing documentation.  You can view these manual pages locally using the
<a href="../man1/man.1.html#//apple_ref/doc/man/1/man">man(1)</a> command.
These manual pages come from many different sources, and thus, have a variety of writing
styles.</p>
<p style='text-align:justify;'>For more information about the manual page format, see the manual page for 
<a href="../man5/manpages.5.html#//apple_ref/doc/man/5/manpages">manpages(5)</a>.</p>
</td></tr></table>
<hr />
<table border="0"  cellpadding="2" cellspacing="2" width="300"><tr><td valign="top" height="12" colspan="5" scope="row"><pre class="manpages"><tt>
Test::Tutorial(3pm)                   Perl Programmers Reference Guide                   Test::Tutorial(3pm)



<b>NAME</b>
       Test::Tutorial - A tutorial about writing really basic tests

<b>DESCRIPTION</b>
       <u>AHHHHHHH!!!!</u>  <u>NOT</u> <u>TESTING!</u>  <u>Anything</u> <u>but</u> <u>testing!</u>  <u>Beat</u> <u>me,</u> <u>whip</u> <u>me,</u> <u>send</u> <u>me</u> <u>to</u> <u>Detroit,</u> <u>but</u> <u>don't</u>
       <u>make</u> <u>me</u> <u>write</u> <u>tests!</u>

       <u>*sob*</u>

       <u>Besides,</u> <u>I</u> <u>don't</u> <u>know</u> <u>how</u> <u>to</u> <u>write</u> <u>the</u> <u>damned</u> <u>things.</u>

       Is this you?  Is writing tests right up there with writing documentation and having your fingernails
       pulled out?  Did you open up a test and read

           ######## We start with some black magic

       and decide that's quite enough for you?

       It's ok.  That's all gone now.  We've done all the black magic for you.  And here are the tricks...

       <b>Nuts</b> <b>and</b> <b>bolts</b> <b>of</b> <b>testing.</b>

       Here's the most basic test program.

           #!/usr/bin/perl -w

           print "1..1\n";

           print 1 + 1 == 2 ? "ok 1\n" : "not ok 1\n";

       since 1 + 1 is 2, it prints:

           1..1
           ok 1

       What this says is: 1..1 "I'm going to run one test." [1] "ok 1" "The first test passed".  And that's
       about all magic there is to testing.  Your basic unit of testing is the <u>ok</u>.  For each thing you test,
       an "ok" is printed.  Simple.  <b>Test::Harness</b> interprets your test results to determine if you suc-<font color="#ffffff" class="whiteout">ceeded&nbsp;succeeded</font>
       ceeded or failed (more on that later).

       Writing all these print statements rapidly gets tedious.  Fortunately, there's <b>Test::Simple</b>.  It has
       one function, "ok()".

           #!/usr/bin/perl -w

           use Test::Simple tests =&gt; 1;

           ok( 1 + 1 == 2 );

       and that does the same thing as the code above.  "ok()" is the backbone of Perl testing, and we'll be
       using it instead of roll-your-own from here on.  If "ok()" gets a true value, the test passes.
       False, it fails.

           #!/usr/bin/perl -w

           use Test::Simple tests =&gt; 2;
           ok( 1 + 1 == 2 );
           ok( 2 + 2 == 5 );

       from that comes

           1..2
           ok 1
           not ok 2
           #     Failed test (test.pl at line 5)
           # Looks like you failed 1 tests of 2.

       1..2 "I'm going to run two tests."  This number is used to ensure your test program ran all the way
       through and didn't die or skip some tests.  "ok 1" "The first test passed."  "not ok 2" "The second
       test failed".  Test::Simple helpfully prints out some extra commentary about your tests.

       It's not scary.  Come, hold my hand.  We're going to give an example of testing a module.  For our
       example, we'll be testing a date library, <b>Date::ICal</b>.  It's on CPAN, so download a copy and follow
       along. [2]

       <b>Where</b> <b>to</b> <b>start?</b>

       This is the hardest part of testing, where do you start?  People often get overwhelmed at the appar-<font color="#ffffff" class="whiteout">ent&nbsp;apparent</font>
       ent enormity of the task of testing a whole module.  Best place to start is at the beginning.
       Date::ICal is an object-oriented module, and that means you start by making an object.  So we test
       "new()".

           #!/usr/bin/perl -w

           use Test::Simple tests =&gt; 2;

           use Date::ICal;

           my $ical = Date::ICal-&gt;new;         # create an object
           ok( defined $ical );                # check that we got something
           ok( $ical-&gt;isa('Date::ICal') );     # and it's the right class

       run that and you should get:

           1..2
           ok 1
           ok 2

       congratulations, you've written your first useful test.

       <b>Names</b>

       That output isn't terribly descriptive, is it?  When you have two tests you can figure out which one
       is #2, but what if you have 102?

       Each test can be given a little descriptive name as the second argument to "ok()".

           use Test::Simple tests =&gt; 2;

           ok( defined $ical,              'new() returned something' );
           ok( $ical-&gt;isa('Date::ICal'),   "  and it's the right class" );

       So now you'd see...

           1..2
           ok 1 - new() returned something
           ok 2 -   and it's the right class

       <b>Test</b> <b>the</b> <b>manual</b>

       Simplest way to build up a decent testing suite is to just test what the manual says it does. [3]
       Let's pull something out of the "SYNOPSIS" in Date::ICal and test that all its bits work.

           #!/usr/bin/perl -w

           use Test::Simple tests =&gt; 8;

           use Date::ICal;

           $ical = Date::ICal-&gt;new( year =&gt; 1964, month =&gt; 10, day =&gt; 16,
                                    hour =&gt; 16, min =&gt; 12, sec =&gt; 47,
                                    tz =&gt; '0530' );

           ok( defined $ical,            'new() returned something' );
           ok( $ical-&gt;isa('Date::ICal'), "  and it's the right class" );
           ok( $ical-&gt;sec   == 47,       '  sec()'   );
           ok( $ical-&gt;min   == 12,       '  min()'   );
           ok( $ical-&gt;hour  == 16,       '  hour()'  );
           ok( $ical-&gt;day   == 17,       '  day()'   );
           ok( $ical-&gt;month == 10,       '  month()' );
           ok( $ical-&gt;year  == 1964,     '  year()'  );

       run that and you get:

           1..8
           ok 1 - new() returned something
           ok 2 -   and it's the right class
           ok 3 -   sec()
           ok 4 -   min()
           ok 5 -   hour()
           not ok 6 -   day()
           #     Failed test (- at line 16)
           ok 7 -   month()
           ok 8 -   year()
           # Looks like you failed 1 tests of 8.

       Whoops, a failure! [4] Test::Simple helpfully lets us know on what line the failure occurred, but not
       much else.  We were supposed to get 17, but we didn't.  What did we get??  Dunno.  We'll have to re-<font color="#ffffff" class="whiteout">run&nbsp;rerun</font>
       run the test in the debugger or throw in some print statements to find out.

       Instead, we'll switch from <b>Test::Simple</b> to <b>Test::More</b>.  <b>Test::More</b> does everything <b>Test::Simple</b> does,
       and more!  In fact, Test::More does things <u>exactly</u> the way Test::Simple does.  You can literally swap
       Test::Simple out and put Test::More in its place.  That's just what we're going to do.

       Test::More does more than Test::Simple.  The most important difference at this point is it provides
       more informative ways to say "ok".  Although you can write almost any test with a generic "ok()", it
       can't tell you what went wrong.  Instead, we'll use the "is()" function, which lets us declare that
       something is supposed to be the same as something else:

           #!/usr/bin/perl -w

           use Test::More tests =&gt; 8;

           use Date::ICal;

           $ical = Date::ICal-&gt;new( year =&gt; 1964, month =&gt; 10, day =&gt; 16,
                                    hour =&gt; 16, min =&gt; 12, sec =&gt; 47,
                                    tz =&gt; '0530' );

           ok( defined $ical,            'new() returned something' );
           ok( $ical-&gt;isa('Date::ICal'), "  and it's the right class" );
           is( $ical-&gt;sec,     47,       '  sec()'   );
           is( $ical-&gt;min,     12,       '  min()'   );
           is( $ical-&gt;hour,    16,       '  hour()'  );
           is( $ical-&gt;day,     17,       '  day()'   );
           is( $ical-&gt;month,   10,       '  month()' );
           is( $ical-&gt;year,    1964,     '  year()'  );

       "Is "$ical-&gt;sec" 47?"  "Is "$ical-&gt;min" 12?"  With "is()" in place, you get some more information

           1..8
           ok 1 - new() returned something
           ok 2 -   and it's the right class
           ok 3 -   sec()
           ok 4 -   min()
           ok 5 -   hour()
           not ok 6 -   day()
           #     Failed test (- at line 16)
           #          got: '16'
           #     expected: '17'
           ok 7 -   month()
           ok 8 -   year()
           # Looks like you failed 1 tests of 8.

       letting us know that "$ical-&gt;day" returned 16, but we expected 17.  A quick check shows that the code
       is working fine, we made a mistake when writing up the tests.  Just change it to:

           is( $ical-&gt;day,     16,       '  day()'   );

       and everything works.

       So any time you're doing a "this equals that" sort of test, use "is()".  It even works on arrays.
       The test is always in scalar context, so you can test how many elements are in a list this way. [5]

           is( @foo, 5, 'foo has 5 elements' );

       <b>Sometimes</b> <b>the</b> <b>tests</b> <b>are</b> <b>wrong</b>

       Which brings us to a very important lesson.  Code has bugs.  Tests are code.  Ergo, tests have bugs.
       A failing test could mean a bug in the code, but don't discount the possibility that the test is
       wrong.

       On the flip side, don't be tempted to prematurely declare a test incorrect just because you're having
       trouble finding the bug.  Invalidating a test isn't something to be taken lightly, and don't use it
       as a cop out to avoid work.

       <b>Testing</b> <b>lots</b> <b>of</b> <b>values</b>

       We're going to be wanting to test a lot of dates here, trying to trick the code with lots of differ-<font color="#ffffff" class="whiteout">ent&nbsp;different</font>
       ent edge cases.  Does it work before 1970?  After 2038?  Before 1904?  Do years after 10,000 give it
       trouble?  Does it get leap years right?  We could keep repeating the code above, or we could set up a
       little try/expect loop.

           use Test::More tests =&gt; 32;
           use Date::ICal;

           my %ICal_Dates = (
                   # An ICal string     And the year, month, date
                   #                    hour, minute and second we expect.
                   '19971024T120000' =&gt;    # from the docs.
                                       [ 1997, 10, 24, 12,  0,  0 ],
                   '20390123T232832' =&gt;    # after the Unix epoch
                                       [ 2039,  1, 23, 23, 28, 32 ],
                   '19671225T000000' =&gt;    # before the Unix epoch
                                       [ 1967, 12, 25,  0,  0,  0 ],
                   '18990505T232323' =&gt;    # before the MacOS epoch
                                       [ 1899,  5,  5, 23, 23, 23 ],
           );

           while( my($ical_str, $expect) = each %ICal_Dates ) {
               my $ical = Date::ICal-&gt;new( ical =&gt; $ical_str );

               ok( defined $ical,            "new(ical =&gt; '$ical_str')" );
               ok( $ical-&gt;isa('Date::ICal'), "  and it's the right class" );

               is( $ical-&gt;year,    $expect-&gt;[0],     '  year()'  );
               is( $ical-&gt;month,   $expect-&gt;[1],     '  month()' );
               is( $ical-&gt;day,     $expect-&gt;[2],     '  day()'   );
               is( $ical-&gt;hour,    $expect-&gt;[3],     '  hour()'  );
               is( $ical-&gt;min,     $expect-&gt;[4],     '  min()'   );
               is( $ical-&gt;sec,     $expect-&gt;[5],     '  sec()'   );
           }

       So now we can test bunches of dates by just adding them to %ICal_Dates.  Now that it's less work to
       test with more dates, you'll be inclined to just throw more in as you think of them.  Only problem
       is, every time we add to that we have to keep adjusting the "use Test::More tests =&gt; ##" line.  That
       can rapidly get annoying.  There's two ways to make this work better.

       First, we can calculate the plan dynamically using the "plan()" function.

           use Test::More;
           use Date::ICal;

           my %ICal_Dates = (
               ...same as before...
           );

           # For each key in the hash we're running 8 tests.
           plan tests =&gt; keys %ICal_Dates * 8;

       Or to be even more flexible, we use "no_plan".  This means we're just running some tests, don't know
       how many. [6]

           use Test::More 'no_plan';   # instead of tests =&gt; 32

       now we can just add tests and not have to do all sorts of math to figure out how many we're running.

       <b>Informative</b> <b>names</b>

       Take a look at this line here

           ok( defined $ical,            "new(ical =&gt; '$ical_str')" );

       we've added more detail about what we're testing and the ICal string itself we're trying out to the
       name.  So you get results like:

           ok 25 - new(ical =&gt; '19971024T120000')
           ok 26 -   and it's the right class
           ok 27 -   year()
           ok 28 -   month()
           ok 29 -   day()
           ok 30 -   hour()
           ok 31 -   min()
           ok 32 -   sec()

       if something in there fails, you'll know which one it was and that will make tracking down the prob-<font color="#ffffff" class="whiteout">lem&nbsp;problem</font>
       lem easier.  So try to put a bit of debugging information into the test names.

       Describe what the tests test, to make debugging a failed test easier for you or for the next person
       who runs your test.

       <b>Skipping</b> <b>tests</b>

       Poking around in the existing Date::ICal tests, I found this in <u>t/01sanity.t</u> [7]

           #!/usr/bin/perl -w

           use Test::More tests =&gt; 7;
           use Date::ICal;

           # Make sure epoch time is being handled sanely.
           my $t1 = Date::ICal-&gt;new( epoch =&gt; 0 );
           is( $t1-&gt;epoch, 0,          "Epoch time of 0" );

           # XXX This will only work on unix systems.
           is( $t1-&gt;ical, '19700101Z', "  epoch to ical" );

           is( $t1-&gt;year,  1970,       "  year()"  );
           is( $t1-&gt;month, 1,          "  month()" );
           is( $t1-&gt;day,   1,          "  day()"   );

           # like the tests above, but starting with ical instead of epoch
           my $t2 = Date::ICal-&gt;new( ical =&gt; '19700101Z' );
           is( $t2-&gt;ical, '19700101Z', "Start of epoch in ICal notation" );

           is( $t2-&gt;epoch, 0,          "  and back to ICal" );

       The beginning of the epoch is different on most non-Unix operating systems [8].  Even though Perl
       smooths out the differences for the most part, certain ports do it differently.  MacPerl is one off
       the top of my head. [9] We <u>know</u> this will never work on MacOS.  So rather than just putting a comment
       in the test, we can explicitly say it's never going to work and skip the test.

           use Test::More tests =&gt; 7;
           use Date::ICal;

           # Make sure epoch time is being handled sanely.
           my $t1 = Date::ICal-&gt;new( epoch =&gt; 0 );
           is( $t1-&gt;epoch, 0,          "Epoch time of 0" );

           SKIP: {
               skip('epoch to ICal not working on MacOS', 6)
                   if $^O eq 'MacOS';

               is( $t1-&gt;ical, '19700101Z', "  epoch to ical" );

               is( $t1-&gt;year,  1970,       "  year()"  );
               is( $t1-&gt;month, 1,          "  month()" );
               is( $t1-&gt;day,   1,          "  day()"   );

               # like the tests above, but starting with ical instead of epoch
               my $t2 = Date::ICal-&gt;new( ical =&gt; '19700101Z' );
               is( $t2-&gt;ical, '19700101Z', "Start of epoch in ICal notation" );

               is( $t2-&gt;epoch, 0,          "  and back to ICal" );
           }

       A little bit of magic happens here.  When running on anything but MacOS, all the tests run normally.
       But when on MacOS, "skip()" causes the entire contents of the SKIP block to be jumped over.  It's
       never run.  Instead, it prints special output that tells Test::Harness that the tests have been
       skipped.

           1..7
           ok 1 - Epoch time of 0
           ok 2 # skip epoch to ICal not working on MacOS
           ok 3 # skip epoch to ICal not working on MacOS
           ok 4 # skip epoch to ICal not working on MacOS
           ok 5 # skip epoch to ICal not working on MacOS
           ok 6 # skip epoch to ICal not working on MacOS
           ok 7 # skip epoch to ICal not working on MacOS

       This means your tests won't fail on MacOS.  This means less emails from MacPerl users telling you
       about failing tests that you know will never work.  You've got to be careful with skip tests.  These
       are for tests which don't work and <u>never</u> <u>will</u>.  It is not for skipping genuine bugs (we'll get to
       that in a moment).

       The tests are wholly and completely skipped. [10]  This will work.

           SKIP: {
               skip("I don't wanna die!");

               die, die, die, die, die;
           }

       <b>Todo</b> <b>tests</b>

       Thumbing through the Date::ICal man page, I came across this:

          ical

              $ical_string = $ical-&gt;ical;

          Retrieves, or sets, the date on the object, using any
          valid ICal date/time string.

       "Retrieves or sets".  Hmmm, didn't see a test for using "ical()" to set the date in the Date::ICal
       test suite.  So I'll write one.

           use Test::More tests =&gt; 1;
           use Date::ICal;

           my $ical = Date::ICal-&gt;new;
           $ical-&gt;ical('20201231Z');
           is( $ical-&gt;ical, '20201231Z',   'Setting via ical()' );

       run that and I get

           1..1
           not ok 1 - Setting via ical()
           #     Failed test (- at line 6)
           #          got: '20010814T233649Z'
           #     expected: '20201231Z'
           # Looks like you failed 1 tests of 1.

       Whoops!  Looks like it's unimplemented.  Let's assume we don't have the time to fix this. [11] Nor-<font color="#ffffff" class="whiteout">mally,&nbsp;Normally,</font>
       mally, you'd just comment out the test and put a note in a todo list somewhere.  Instead, we're going
       to explicitly state "this test will fail" by wrapping it in a "TODO" block.

           use Test::More tests =&gt; 1;

           TODO: {
               local $TODO = 'ical($ical) not yet implemented';

               my $ical = Date::ICal-&gt;new;
               $ical-&gt;ical('20201231Z');

               is( $ical-&gt;ical, '20201231Z',   'Setting via ical()' );
           }

       Now when you run, it's a little different:

           1..1
           not ok 1 - Setting via ical() # TODO ical($ical) not yet implemented
           #          got: '20010822T201551Z'
           #     expected: '20201231Z'

       Test::More doesn't say "Looks like you failed 1 tests of 1".  That '# TODO' tells Test::Harness "this
       is supposed to fail" and it treats a failure as a successful test.  So you can write tests even
       before you've fixed the underlying code.

       If a TODO test passes, Test::Harness will report it "UNEXPECTEDLY SUCCEEDED".  When that happens, you
       simply remove the TODO block with "local $TODO" and turn it into a real test.

       <b>Testing</b> <b>with</b> <b>taint</b> <b>mode.</b>

       Taint mode is a funny thing.  It's the globalest of all global features.  Once you turn it on, it
       affects <u>all</u> code in your program and <u>all</u> modules used (and all the modules they use).  If a single
       piece of code isn't taint clean, the whole thing explodes.  With that in mind, it's very important to
       ensure your module works under taint mode.

       It's very simple to have your tests run under taint mode.  Just throw a "-T" into the "#!" line.
       Test::Harness will read the switches in "#!" and use them to run your tests.

           #!/usr/bin/perl -Tw

           ...test normally here...

       So when you say "make test" it will be run with taint mode and warnings on.

<b>FOOTNOTES</b>
       1   The first number doesn't really mean anything, but it has to be 1.  It's the second number that's
           important.

       2   For those following along at home, I'm using version 1.31.  It has some bugs, which is good --<font color="#ffffff" class="whiteout">we'll&nbsp;-we'll</font>
           we'll uncover them with our tests.

       3   You can actually take this one step further and test the manual itself.  Have a look at
           <b>Test::Inline</b> (formerly <b>Pod::Tests</b>).

       4   Yes, there's a mistake in the test suite.  What!  Me, contrived?

       5   We'll get to testing the contents of lists later.

       6   But what happens if your test program dies halfway through?!  Since we didn't say how many tests
           we're going to run, how can we know it failed?  No problem, Test::More employs some magic to
           catch that death and turn the test into a failure, even if every test passed up to that point.

       7   I cleaned it up a little.

       8   Most Operating Systems record time as the number of seconds since a certain date.  This date is
           the beginning of the epoch.  Unix's starts at midnight January 1st, 1970 GMT.

       9   MacOS's epoch is midnight January 1st, 1904.  VMS's is midnight, November 17th, 1858, but vmsperl
           emulates the Unix epoch so it's not a problem.

       10  As long as the code inside the SKIP block at least compiles.  Please don't ask how.  No, it's not
           a filter.

       11  Do NOT be tempted to use TODO tests as a way to avoid fixing simple bugs!

<b>AUTHORS</b>
       Michael G Schwern &lt;schwern@pobox.com&gt; and the perl-qa dancers!

<b>COPYRIGHT</b>
       Copyright 2001 by Michael G Schwern &lt;schwern@pobox.com&gt;.

       This documentation is free; you can redistribute it and/or modify it under the same terms as Perl
       itself.

       Irrespective of its distribution, all code examples in these files are hereby placed into the public
       domain.  You are permitted and encouraged to use this code in your own programs for fun or for profit
       as you see fit.  A simple comment in the code giving credit would be courteous but is not required.



perl v5.8.8                                      2001-09-21                              Test::Tutorial(3pm)
</tt></pre>
</td></tr></table>
	<table width="100%" border="0" cellpadding="0" cellspacing="0">
		<tr>
			<td><div style="width: 100%; height: 1px; background-color: #919699; margin-top: 5px; margin-bottom: 15px"></div></td>
		</tr>
		<tr>
			<td align="center"><br/>
				<table border="0" cellpadding="0" cellspacing="0" class="graybox">
					<tr>
						<th>Did this document help you?</th>
					</tr>
					<tr>
						<td>
						    <div style="margin-bottom: 8px"><a href="http://developer.apple.com/feedback/?v=1&url=/documentation/Darwin/Reference/ManPages/man3/Test__Tutorial.3pm.html%3Fid%3DTP40000894-7.0&media=dvd" target=_new>Yes</a>:  Tell us what works for you.</div>
							<div style="margin-bottom: 8px"><a href="http://developer.apple.com/feedback/?v=2&url=/documentation/Darwin/Reference/ManPages/man3/Test__Tutorial.3pm.html%3Fid%3DTP40000894-7.0&media=dvd" target=_new>It&#8217;s good, but:</a> Report typos, inaccuracies, and so forth.</div>
							<div><a href="http://developer.apple.com/feedback/?v=3&url=/documentation/Darwin/Reference/ManPages/man3/Test__Tutorial.3pm.html%3Fid%3DTP40000894-7.0&media=dvd" target=_new>It wasn&#8217;t helpful</a>: Tell us what would have helped.</div>
						</td>
					</tr>
				</table>
			</td>
		</tr>
	</table>

</body></html>
