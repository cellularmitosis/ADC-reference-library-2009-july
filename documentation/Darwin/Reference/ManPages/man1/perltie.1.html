<html><head><title>Mac OS X
 Manual Page For perltie(1)</title>
<style type="text/css"><!-- @import "../../../../adcstyle.css"; .char {background: #ffffff; color:#0000ff;}.comment {background:#ffffff; color:#236e25}.function {background:#ffffff; color:#000000;}.keyword {background:#ffffff; color:#761550;}.number {background: #ffffff; color:#0000ff;}.param {background:#ffffff; color:#000000;}.preprocessor {background:#ffffff; color:#236e25}.string {background: #ffffff; color:#891315;}.template {background:#ffffff; color:#761550;}.type {background:#ffffff; color:#761550;}.var {background:#ffffff; color:#000000;} pre.manpages {font-size: 10pt;} .graybox { border-top: 1px solid #919699; border-left: 1px solid #919699; margin-bottom: 10px; } .graybox th { padding: 4px 8px 4px 8px; background: #E2E2E2; font-size: 12px; font-weight: bold; border-bottom: 1px solid #919699; border-right: 1px solid #919699; } .graybox td { padding: 8px; font-size: 12px; text-align: left; vertical-align: top; border-bottom: 1px solid #919699; border-right: 1px solid #919699; } .whiteout { font-size: 1px; background: #ffffff; color: #ffffff; } --></style>
<style type="text/css" media="print, aural, braille, embossed, projection, tty"><!-- @import "../../../../adcstyle.css"; .char {background: #ffffff; color:#0000ff;}.comment {background:#ffffff; color:#236e25}.function {background:#ffffff; color:#000000;}.keyword {background:#ffffff; color:#761550;}.number {background: #ffffff; color:#0000ff;}.param {background:#ffffff; color:#000000;}.preprocessor {background:#ffffff; color:#236e25}.string {background: #ffffff; color:#891315;}.template {background:#ffffff; color:#761550;}.type {background:#ffffff; color:#761550;}.var {background:#ffffff; color:#000000;} pre.manpages {font-size: 10pt;} .graybox { border-top: 1px solid #919699; border-left: 1px solid #919699; margin-bottom: 10px; } .graybox th { padding: 4px 8px 4px 8px; background: #E2E2E2; font-size: 12px; font-weight: bold; border-bottom: 1px solid #919699; border-right: 1px solid #919699; } .graybox td { padding: 8px; font-size: 12px; text-align: left; vertical-align: top; border-bottom: 1px solid #919699; border-right: 1px solid #919699; } .whiteout { display: none; } --></style>
</head><body bgcolor="white">
<a name="//apple_ref/doc/man/1/perltie" title="Mac OS X
 Manual Page for perltie(1)"><!-- headerDoc=man; indexgroup=Section 1; uid="//apple_ref/doc/man/1/perltie"; name=perltie(1) --></a>
<!--#include virtual="/includes/framesetheader"-->
<table width=600><tr><td scope="row">
<!-- begin breadcrumb insert request --><font face="Geneva,Helvetica,Arial" size="1">
 <a target="_top" href="http://developer.apple.com">ADC Home</a> <b>&gt;</b> <a href="../../../../../referencelibrary/index.html#//apple_ref/doc/uid/TP30000943" target="_top">Reference Library</a> <b>&gt;</b> <a href="../../../../../reference/index.html#//apple_ref/doc/uid/TP30001281" target="_top">Reference</a> <b>&gt;</b> <!-- a href="" target="_top" -->Mac OS X<!-- /a --> <b>&gt;</b> <a href="../index.html#//apple_ref/doc/framework/manpages" target="_top">Mac OS X Man Pages</a></font>
<!-- end breadcrumb insert request -->
<p>&nbsp;</p>
</td></tr></table>
<!-- end of header -->
<table border="0"  cellpadding="2" cellspacing="2" width="630pt"><tr><td valign="top" height="12" colspan="5" scope="row">
<p style='text-align:justify;'>This document is a Mac OS X manual page.  Manual pages are a command-line technology
for providing documentation.  You can view these manual pages locally using the
<a href="man.1.html#//apple_ref/doc/man/1/man">man(1)</a> command.
These manual pages come from many different sources, and thus, have a variety of writing
styles.</p>
<p style='text-align:justify;'>For more information about the manual page format, see the manual page for 
<a href="../man5/manpages.5.html#//apple_ref/doc/man/5/manpages">manpages(5)</a>.</p>
</td></tr></table>
<hr />
<table border="0"  cellpadding="2" cellspacing="2" width="300"><tr><td valign="top" height="12" colspan="5" scope="row"><pre class="manpages"><tt>
PERLTIE(1)                            Perl Programmers Reference Guide                            PERLTIE(1)



<b>NAME</b>
       perltie - how to hide an object class in a simple variable

<b>SYNOPSIS</b>
        tie VARIABLE, CLASSNAME, LIST

        $object = tied VARIABLE

        untie VARIABLE

<b>DESCRIPTION</b>
       Prior to release 5.0 of Perl, a programmer could use <u>dbmopen()</u> to connect an on-disk database in the
       standard Unix <a href="../man3/dbm.3.html#//apple_ref/doc/man/3/dbm"><u>dbm</u>(3x)</a> format magically to a %HASH in their program.  However, their Perl was either
       built with one particular dbm library or another, but not both, and you couldn't extend this mecha-<font color="#ffffff" class="whiteout">nism&nbsp;mechanism</font>
       nism to other packages or types of variables.

       Now you can.

       The <u>tie()</u> function binds a variable to a class (package) that will provide the implementation for
       access methods for that variable.  Once this magic has been performed, accessing a tied variable
       automatically triggers method calls in the proper class.  The complexity of the class is hidden
       behind magic methods calls.  The method names are in ALL CAPS, which is a convention that Perl uses
       to indicate that they're called implicitly rather than explicitly--just like the <u>BEGIN()</u> and <u>END()</u>
       functions.

       In the <u>tie()</u> call, "VARIABLE" is the name of the variable to be enchanted.  "CLASSNAME" is the name
       of a class implementing objects of the correct type.  Any additional arguments in the "LIST" are
       passed to the appropriate constructor method for that class--meaning <u>TIESCALAR()</u>, <u>TIEARRAY()</u>,
       <u>TIEHASH()</u>, or <u>TIEHANDLE()</u>.  (Typically these are arguments such as might be passed to the <u>dbminit()</u>
       function of C.) The object returned by the "new" method is also returned by the <u>tie()</u> function, which
       would be useful if you wanted to access other methods in "CLASSNAME". (You don't actually have to
       return a reference to a right "type" (e.g., HASH or "CLASSNAME") so long as it's a properly blessed
       object.)  You can also retrieve a reference to the underlying object using the <u>tied()</u> function.

       Unlike <u>dbmopen()</u>, the <u>tie()</u> function will not "use" or "require" a module for you--you need to do
       that explicitly yourself.

       <b>Tying</b> <b>Scalars</b>

       A class implementing a tied scalar should define the following methods: TIESCALAR, FETCH, STORE, and
       possibly UNTIE and/or DESTROY.

       Let's look at each in turn, using as an example a tie class for scalars that allows the user to do
       something like:

           tie $his_speed, 'Nice', getppid();
           tie $my_speed,  'Nice', $$;

       And now whenever either of those variables is accessed, its current system priority is retrieved and
       returned.  If those variables are set, then the process's priority is changed!

       We'll use Jarkko Hietaniemi &lt;<u>jhi@iki.fi&gt;</u>'s BSD::Resource class (not included) to access the
       PRIO_PROCESS, PRIO_MIN, and PRIO_MAX constants from your system, as well as the <u>getpriority()</u> and
       <u>setpriority()</u> system calls.  Here's the preamble of the class.

           package Nice;
           use Carp;
           use BSD::Resource;
           use strict;
           $Nice::DEBUG = 0 unless defined $Nice::DEBUG;

       TIESCALAR classname, LIST
           This is the constructor for the class.  That means it is expected to return a blessed reference
           to a new scalar (probably anonymous) that it's creating.  For example:

               sub TIESCALAR {
                   my $class = shift;
                   my $pid = shift || $$; # 0 means me

                   if ($pid !~ /^\d+$/) {
                       carp "Nice::Tie::Scalar got non-numeric pid $pid" if $^W;
                       return undef;
                   }

                   unless (kill 0, $pid) { # EPERM or ERSCH, no doubt
                       carp "Nice::Tie::Scalar got bad pid $pid: $!" if $^W;
                       return undef;
                   }

                   return bless \$pid, $class;
               }

           This tie class has chosen to return an error rather than raising an exception if its constructor
           should fail.  While this is how <u>dbmopen()</u> works, other classes may well not wish to be so forgiv-<font color="#ffffff" class="whiteout">ing.&nbsp;forgiving.</font>
           ing.  It checks the global variable $^W to see whether to emit a bit of noise anyway.

       FETCH this
           This method will be triggered every time the tied variable is accessed (read).  It takes no argu-<font color="#ffffff" class="whiteout">ments&nbsp;arguments</font>
           ments beyond its self reference, which is the object representing the scalar we're dealing with.
           Because in this case we're using just a SCALAR ref for the tied scalar object, a simple $$self
           allows the method to get at the real value stored there.  In our example below, that real value
           is the process ID to which we've tied our variable.

               sub FETCH {
                   my $self = shift;
                   confess "wrong type" unless ref $self;
                   croak "usage error" if @_;
                   my $nicety;
                   local($!) = 0;
                   $nicety = getpriority(PRIO_PROCESS, $$self);
                   if ($!) { croak "getpriority failed: $!" }
                   return $nicety;
               }

           This time we've decided to blow up (raise an exception) if the renice fails--there's no place for
           us to return an error otherwise, and it's probably the right thing to do.

       STORE this, value
           This method will be triggered every time the tied variable is set (assigned).  Beyond its self
           reference, it also expects one (and only one) argument--the new value the user is trying to
           assign. Don't worry about returning a value from STORE -- the semantic of assignment returning
           the assigned value is implemented with FETCH.

               sub STORE {
                   my $self = shift;
                   confess "wrong type" unless ref $self;
                   my $new_nicety = shift;
                   croak "usage error" if @_;

                   if ($new_nicety &lt; PRIO_MIN) {
                       carp sprintf
                         "WARNING: priority %d less than minimum system priority %d",
                             $new_nicety, PRIO_MIN if $^W;
                       $new_nicety = PRIO_MIN;
                   }

                   if ($new_nicety &gt; PRIO_MAX) {
                       carp sprintf
                         "WARNING: priority %d greater than maximum system priority %d",
                             $new_nicety, PRIO_MAX if $^W;
                       $new_nicety = PRIO_MAX;
                   }

                   unless (defined setpriority(PRIO_PROCESS, $$self, $new_nicety)) {
                       confess "setpriority failed: $!";
                   }
               }

       UNTIE this
           This method will be triggered when the "untie" occurs. This can be useful if the class needs to
           know when no further calls will be made. (Except DESTROY of course.) See "The "untie" Gotcha"
           below for more details.

       DESTROY this
           This method will be triggered when the tied variable needs to be destructed.  As with other
           object classes, such a method is seldom necessary, because Perl deallocates its moribund object's
           memory for you automatically--this isn't C++, you know.  We'll use a DESTROY method here for
           debugging purposes only.

               sub DESTROY {
                   my $self = shift;
                   confess "wrong type" unless ref $self;
                   carp "[ Nice::DESTROY pid $$self ]" if $Nice::DEBUG;
               }

       That's about all there is to it.  Actually, it's more than all there is to it, because we've done a
       few nice things here for the sake of completeness, robustness, and general aesthetics.  Simpler
       TIESCALAR classes are certainly possible.

       <b>Tying</b> <b>Arrays</b>

       A class implementing a tied ordinary array should define the following methods: TIEARRAY, FETCH,
       STORE, FETCHSIZE, STORESIZE and perhaps UNTIE and/or DESTROY.

       FETCHSIZE and STORESIZE are used to provide $#array and equivalent "scalar(@array)" access.

       The methods POP, PUSH, SHIFT, UNSHIFT, SPLICE, DELETE, and EXISTS are required if the perl operator
       with the corresponding (but lowercase) name is to operate on the tied array. The <b>Tie::Array</b> class can
       be used as a base class to implement the first five of these in terms of the basic methods above.
       The default implementations of DELETE and EXISTS in <b>Tie::Array</b> simply "croak".

       In addition EXTEND will be called when perl would have pre-extended allocation in a real array.

       For this discussion, we'll implement an array whose elements are a fixed size at creation.  If you
       try to create an element larger than the fixed size, you'll take an exception.  For example:

           use FixedElem_Array;
           tie @array, 'FixedElem_Array', 3;
           $array[0] = 'cat';  # ok.
           $array[1] = 'dogs'; # exception, length('dogs') &gt; 3.

       The preamble code for the class is as follows:

           package FixedElem_Array;
           use Carp;
           use strict;

       TIEARRAY classname, LIST
           This is the constructor for the class.  That means it is expected to return a blessed reference
           through which the new array (probably an anonymous ARRAY ref) will be accessed.

           In our example, just to show you that you don't <u>really</u> have to return an ARRAY reference, we'll
           choose a HASH reference to represent our object.  A HASH works out well as a generic record type:
           the "{ELEMSIZE}" field will store the maximum element size allowed, and the "{ARRAY}" field will
           hold the true ARRAY ref.  If someone outside the class tries to dereference the object returned
           (doubtless thinking it an ARRAY ref), they'll blow up.  This just goes to show you that you
           should respect an object's privacy.

               sub TIEARRAY {
                 my $class    = shift;
                 my $elemsize = shift;
                 if ( @_ || $elemsize =~ /\D/ ) {
                   croak "usage: tie ARRAY, '" . __PACKAGE__ . "', elem_size";
                 }
                 return bless {
                   ELEMSIZE =&gt; $elemsize,
                   ARRAY    =&gt; [],
                 }, $class;
               }

       FETCH this, index
           This method will be triggered every time an individual element the tied array is accessed (read).
           It takes one argument beyond its self reference: the index whose value we're trying to fetch.

               sub FETCH {
                 my $self  = shift;
                 my $index = shift;
                 return $self-&gt;{ARRAY}-&gt;[$index];
               }

           If a negative array index is used to read from an array, the index will be translated to a posi-<font color="#ffffff" class="whiteout">tive&nbsp;positive</font>
           tive one internally by calling FETCHSIZE before being passed to FETCH.  You may disable this fea-<font color="#ffffff" class="whiteout">ture&nbsp;feature</font>
           ture by assigning a true value to the variable $NEGATIVE_INDICES in the tied array class.

           As you may have noticed, the name of the FETCH method (et al.) is the same for all accesses, even
           though the constructors differ in names (TIESCALAR vs TIEARRAY).  While in theory you could have
           the same class servicing several tied types, in practice this becomes cumbersome, and it's easi-<font color="#ffffff" class="whiteout">est&nbsp;easiest</font>
           est to keep them at simply one tie type per class.

       STORE this, index, value
           This method will be triggered every time an element in the tied array is set (written).  It takes
           two arguments beyond its self reference: the index at which we're trying to store something and
           the value we're trying to put there.

           In our example, "undef" is really "$self-&gt;{ELEMSIZE}" number of spaces so we have a little more
           work to do here:

               sub STORE {
                 my $self = shift;
                 my( $index, $value ) = @_;
                 if ( length $value &gt; $self-&gt;{ELEMSIZE} ) {
                   croak "length of $value is greater than $self-&gt;{ELEMSIZE}";
                 }
                 # fill in the blanks
                 $self-&gt;EXTEND( $index ) if $index &gt; $self-&gt;FETCHSIZE();
                 # right justify to keep element size for smaller elements
                 $self-&gt;{ARRAY}-&gt;[$index] = sprintf "%$self-&gt;{ELEMSIZE}s", $value;
               }

           Negative indexes are treated the same as with FETCH.

       FETCHSIZE this
           Returns the total number of items in the tied array associated with object <u>this</u>. (Equivalent to
           "scalar(@array)").  For example:

               sub FETCHSIZE {
                 my $self = shift;
                 return scalar @{$self-&gt;{ARRAY}};
               }

       STORESIZE this, count
           Sets the total number of items in the tied array associated with object <u>this</u> to be <u>count</u>. If this
           makes the array larger then class's mapping of "undef" should be returned for new positions.  If
           the array becomes smaller then entries beyond count should be deleted.

           In our example, 'undef' is really an element containing "$self-&gt;{ELEMSIZE}" number of spaces.
           Observe:

               sub STORESIZE {
                 my $self  = shift;
                 my $count = shift;
                 if ( $count &gt; $self-&gt;FETCHSIZE() ) {
                   foreach ( $count - $self-&gt;FETCHSIZE() .. $count ) {
                     $self-&gt;STORE( $_, '' );
                   }
                 } elsif ( $count &lt; $self-&gt;FETCHSIZE() ) {
                   foreach ( 0 .. $self-&gt;FETCHSIZE() - $count - 2 ) {
                     $self-&gt;POP();
                   }
                 }
               }

       EXTEND this, count
           Informative call that array is likely to grow to have <u>count</u> entries.  Can be used to optimize
           allocation. This method need do nothing.

           In our example, we want to make sure there are no blank ("undef") entries, so "EXTEND" will make
           use of "STORESIZE" to fill elements as needed:

               sub EXTEND {
                 my $self  = shift;
                 my $count = shift;
                 $self-&gt;STORESIZE( $count );
               }

       EXISTS this, key
           Verify that the element at index <u>key</u> exists in the tied array <u>this</u>.

           In our example, we will determine that if an element consists of "$self-&gt;{ELEMSIZE}" spaces only,
           it does not exist:

               sub EXISTS {
                 my $self  = shift;
                 my $index = shift;
                 return 0 if ! defined $self-&gt;{ARRAY}-&gt;[$index] ||
                             $self-&gt;{ARRAY}-&gt;[$index] eq ' ' x $self-&gt;{ELEMSIZE};
                 return 1;
               }

       DELETE this, key
           Delete the element at index <u>key</u> from the tied array <u>this</u>.

           In our example, a deleted item is "$self-&gt;{ELEMSIZE}" spaces:

               sub DELETE {
                 my $self  = shift;
                 my $index = shift;
                 return $self-&gt;STORE( $index, '' );
               }

       CLEAR this
           Clear (remove, delete, ...) all values from the tied array associated with object <u>this</u>.  For
           example:

               sub CLEAR {
                 my $self = shift;
                 return $self-&gt;{ARRAY} = [];
               }

       PUSH this, LIST
           Append elements of <u>LIST</u> to the array.  For example:

               sub PUSH {
                 my $self = shift;
                 my @list = @_;
                 my $last = $self-&gt;FETCHSIZE();
                 $self-&gt;STORE( $last + $_, $list[$_] ) foreach 0 .. $#list;
                 return $self-&gt;FETCHSIZE();
               }

       POP this
           Remove last element of the array and return it.  For example:

               sub POP {
                 my $self = shift;
                 return pop @{$self-&gt;{ARRAY}};
               }

       SHIFT this
           Remove the first element of the array (shifting other elements down) and return it.  For example:

               sub SHIFT {
                 my $self = shift;
                 return shift @{$self-&gt;{ARRAY}};
               }

       UNSHIFT this, LIST
           Insert LIST elements at the beginning of the array, moving existing elements up to make room.
           For example:

               sub UNSHIFT {
                 my $self = shift;
                 my @list = @_;
                 my $size = scalar( @list );
                 # make room for our list
                 @{$self-&gt;{ARRAY}}[ $size .. $#{$self-&gt;{ARRAY}} + $size ]
                  = @{$self-&gt;{ARRAY}};
                 $self-&gt;STORE( $_, $list[$_] ) foreach 0 .. $#list;
               }

       SPLICE this, offset, length, LIST
           Perform the equivalent of "splice" on the array.

           <u>offset</u> is optional and defaults to zero, negative values count back from the end of the array.

           <u>length</u> is optional and defaults to rest of the array.

           <u>LIST</u> may be empty.

           Returns a list of the original <u>length</u> elements at <u>offset</u>.

           In our example, we'll use a little shortcut if there is a <u>LIST</u>:

               sub SPLICE {
                 my $self   = shift;
                 my $offset = shift || 0;
                 my $length = shift || $self-&gt;FETCHSIZE() - $offset;
                 my @list   = ();
                 if ( @_ ) {
                   tie @list, __PACKAGE__, $self-&gt;{ELEMSIZE};
                   @list   = @_;
                 }
                 return splice @{$self-&gt;{ARRAY}}, $offset, $length, @list;
               }

       UNTIE this
           Will be called when "untie" happens. (See "The "untie" Gotcha" below.)

       DESTROY this
           This method will be triggered when the tied variable needs to be destructed.  As with the scalar
           tie class, this is almost never needed in a language that does its own garbage collection, so
           this time we'll just leave it out.

       <b>Tying</b> <b>Hashes</b>

       Hashes were the first Perl data type to be tied (see <u>dbmopen()</u>).  A class implementing a tied hash
       should define the following methods: TIEHASH is the constructor.  FETCH and STORE access the key and
       value pairs.  EXISTS reports whether a key is present in the hash, and DELETE deletes one.  CLEAR
       empties the hash by deleting all the key and value pairs.  FIRSTKEY and NEXTKEY implement the <u>keys()</u>
       and <u>each()</u> functions to iterate over all the keys. SCALAR is triggered when the tied hash is evalu-<font color="#ffffff" class="whiteout">ated&nbsp;evaluated</font>
       ated in scalar context. UNTIE is called when "untie" happens, and DESTROY is called when the tied
       variable is garbage collected.

       If this seems like a lot, then feel free to inherit from merely the standard Tie::StdHash module for
       most of your methods, redefining only the interesting ones.  See Tie::Hash for details.

       Remember that Perl distinguishes between a key not existing in the hash, and the key existing in the
       hash but having a corresponding value of "undef".  The two possibilities can be tested with the
       "exists()" and "defined()" functions.

       Here's an example of a somewhat interesting tied hash class:  it gives you a hash representing a par-<font color="#ffffff" class="whiteout">ticular&nbsp;particular</font>
       ticular user's dot files.  You index into the hash with the name of the file (minus the dot) and you
       get back that dot file's contents.  For example:

           use DotFiles;
           tie %dot, 'DotFiles';
           if ( $dot{profile} =~ /MANPATH/ ||
                $dot{login}   =~ /MANPATH/ ||
                $dot{cshrc}   =~ /MANPATH/    )
           {
               print "you seem to set your MANPATH\n";
           }

       Or here's another sample of using our tied class:

           tie %him, 'DotFiles', 'daemon';
           foreach $f ( keys %him ) {
               printf "daemon dot file %s is size %d\n",
                   $f, length $him{$f};
           }

       In our tied hash DotFiles example, we use a regular hash for the object containing several important
       fields, of which only the "{LIST}" field will be what the user thinks of as the real hash.

       USER whose dot files this object represents

       HOME where those dot files live

       CLOBBER
            whether we should try to change or remove those dot files

       LIST the hash of dot file names and content mappings

       Here's the start of <u>Dotfiles.pm</u>:

           package DotFiles;
           use Carp;
           sub whowasi { <!-- a -->(caller(1)<!-- /a -->)[3] . '()' }
           my $DEBUG = 0;
           sub debug { $DEBUG = @_ ? shift : 1 }

       For our example, we want to be able to emit debugging info to help in tracing during development.  We
       keep also one convenience function around internally to help print out warnings; <u>whowasi()</u> returns
       the function name that calls it.

       Here are the methods for the DotFiles tied hash.

       TIEHASH classname, LIST
           This is the constructor for the class.  That means it is expected to return a blessed reference
           through which the new object (probably but not necessarily an anonymous hash) will be accessed.

           Here's the constructor:

               sub TIEHASH {
                   my $self = shift;
                   my $user = shift || $&gt;;
                   my $dotdir = shift || '';
                   croak "usage: @{[&amp;whowasi]} [USER [DOTDIR]]" if @_;
                   $user = getpwuid($user) if $user =~ /^\d+$/;
                   my $dir = (getpwnam($user))[7]
                           || croak "@{[&amp;whowasi]}: no user $user";
                   $dir .= "/$dotdir" if $dotdir;

                   my $node = {
                       USER    =&gt; $user,
                       HOME    =&gt; $dir,
                       LIST    =&gt; {},
                       CLOBBER =&gt; 0,
                   };

                   opendir(DIR, $dir)
                           || croak "@{[&amp;whowasi]}: can't opendir $dir: $!";
                   foreach $dot ( grep /^\./ &amp;&amp; -f "$dir/$_", readdir(DIR)) {
                       $dot =~ s/^\.//;
                       $node-&gt;{LIST}{$dot} = undef;
                   }
                   closedir DIR;
                   return bless $node, $self;
               }

           It's probably worth mentioning that if you're going to filetest the return values out of a read-<font color="#ffffff" class="whiteout">dir,&nbsp;readdir,</font>
           dir, you'd better prepend the directory in question.  Otherwise, because we didn't <u>chdir()</u> there,
           it would have been testing the wrong file.

       FETCH this, key
           This method will be triggered every time an element in the tied hash is accessed (read).  It
           takes one argument beyond its self reference: the key whose value we're trying to fetch.

           Here's the fetch for our DotFiles example.

               sub FETCH {
                   carp &amp;whowasi if $DEBUG;
                   my $self = shift;
                   my $dot = shift;
                   my $dir = $self-&gt;{HOME};
                   my $file = "$dir/.$dot";

                   unless (exists $self-&gt;{LIST}-&gt;{$dot} || -f $file) {
                       carp "@{[&amp;whowasi]}: no $dot file" if $DEBUG;
                       return undef;
                   }

                   if (defined $self-&gt;{LIST}-&gt;{$dot}) {
                       return $self-&gt;{LIST}-&gt;{$dot};
                   } else {
                       return $self-&gt;{LIST}-&gt;{$dot} = `cat $dir/.$dot`;
                   }
               }

           It was easy to write by having it call the Unix <a href="cat.1.html#//apple_ref/doc/man/1/cat"><u>cat</u>(1)</a> command, but it would probably be more
           portable to open the file manually (and somewhat more efficient).  Of course, because dot files
           are a Unixy concept, we're not that concerned.

       STORE this, key, value
           This method will be triggered every time an element in the tied hash is set (written).  It takes
           two arguments beyond its self reference: the index at which we're trying to store something, and
           the value we're trying to put there.

           Here in our DotFiles example, we'll be careful not to let them try to overwrite the file unless
           they've called the <u>clobber()</u> method on the original object reference returned by <u>tie()</u>.

               sub STORE {
                   carp &amp;whowasi if $DEBUG;
                   my $self = shift;
                   my $dot = shift;
                   my $value = shift;
                   my $file = $self-&gt;{HOME} . "/.$dot";
                   my $user = $self-&gt;{USER};

                   croak "@{[&amp;whowasi]}: $file not clobberable"
                       unless $self-&gt;{CLOBBER};

                   open(F, "&gt; $file") || croak "can't open $file: $!";
                   print F $value;
                   <!-- a -->close(F)<!-- /a -->;
               }

           If they wanted to clobber something, they might say:

               $ob = tie %daemon_dots, 'daemon';
               $ob-&gt;clobber(1);
               $daemon_dots{signature} = "A true daemon\n";

           Another way to lay hands on a reference to the underlying object is to use the <u>tied()</u> function,
           so they might alternately have set clobber using:

               tie %daemon_dots, 'daemon';
               tied(%daemon_dots)-&gt;clobber(1);

           The clobber method is simply:

               sub clobber {
                   my $self = shift;
                   $self-&gt;{CLOBBER} = @_ ? shift : 1;
               }

       DELETE this, key
           This method is triggered when we remove an element from the hash, typically by using the <u>delete()</u>
           function.  Again, we'll be careful to check whether they really want to clobber files.

               sub DELETE   {
                   carp &amp;whowasi if $DEBUG;

                   my $self = shift;
                   my $dot = shift;
                   my $file = $self-&gt;{HOME} . "/.$dot";
                   croak "@{[&amp;whowasi]}: won't remove file $file"
                       unless $self-&gt;{CLOBBER};
                   delete $self-&gt;{LIST}-&gt;{$dot};
                   my $success = unlink($file);
                   carp "@{[&amp;whowasi]}: can't unlink $file: $!" unless $success;
                   $success;
               }

           The value returned by DELETE becomes the return value of the call to <u>delete()</u>.  If you want to
           emulate the normal behavior of <u>delete()</u>, you should return whatever FETCH would have returned for
           this key.  In this example, we have chosen instead to return a value which tells the caller
           whether the file was successfully deleted.

       CLEAR this
           This method is triggered when the whole hash is to be cleared, usually by assigning the empty
           list to it.

           In our example, that would remove all the user's dot files!  It's such a dangerous thing that
           they'll have to set CLOBBER to something higher than 1 to make it happen.

               sub CLEAR    {
                   carp &amp;whowasi if $DEBUG;
                   my $self = shift;
                   croak "@{[&amp;whowasi]}: won't remove all dot files for $self-&gt;{USER}"
                       unless $self-&gt;{CLOBBER} &gt; 1;
                   my $dot;
                   foreach $dot ( keys %{$self-&gt;{LIST}}) {
                       $self-&gt;DELETE($dot);
                   }
               }

       EXISTS this, key
           This method is triggered when the user uses the <u>exists()</u> function on a particular hash.  In our
           example, we'll look at the "{LIST}" hash element for this:

               sub EXISTS   {
                   carp &amp;whowasi if $DEBUG;
                   my $self = shift;
                   my $dot = shift;
                   return exists $self-&gt;{LIST}-&gt;{$dot};
               }

       FIRSTKEY this
           This method will be triggered when the user is going to iterate through the hash, such as via a
           <u>keys()</u> or <u>each()</u> call.

               sub FIRSTKEY {
                   carp &amp;whowasi if $DEBUG;
                   my $self = shift;
                   my $a = keys %{$self-&gt;{LIST}};          # reset each() iterator
                   each %{$self-&gt;{LIST}}
               }

       NEXTKEY this, lastkey
           This method gets triggered during a <u>keys()</u> or <u>each()</u> iteration.  It has a second argument which
           is the last key that had been accessed.  This is useful if you're carrying about ordering or
           calling the iterator from more than one sequence, or not really storing things in a hash any-<font color="#ffffff" class="whiteout">where.&nbsp;anywhere.</font>
           where.

           For our example, we're using a real hash so we'll do just the simple thing, but we'll have to go
           through the LIST field indirectly.

               sub NEXTKEY  {
                   carp &amp;whowasi if $DEBUG;
                   my $self = shift;
                   return each %{ $self-&gt;{LIST} }
               }

       SCALAR this
           This is called when the hash is evaluated in scalar context. In order to mimic the behaviour of
           untied hashes, this method should return a false value when the tied hash is considered empty. If
           this method does not exist, perl will make some educated guesses and return true when the hash is
           inside an iteration. If this isn't the case, FIRSTKEY is called, and the result will be a false
           value if FIRSTKEY returns the empty list, true otherwise.

           However, you should <b>not</b> blindly rely on perl always doing the right thing. Particularly, perl
           will mistakenly return true when you clear the hash by repeatedly calling DELETE until it is
           empty. You are therefore advised to supply your own SCALAR method when you want to be absolutely
           sure that your hash behaves nicely in scalar context.

           In our example we can just call "scalar" on the underlying hash referenced by "$self-&gt;{LIST}":

               sub SCALAR {
                   carp &amp;whowasi if $DEBUG;
                   my $self = shift;
                   return scalar %{ $self-&gt;{LIST} }
               }

       UNTIE this
           This is called when "untie" occurs.  See "The "untie" Gotcha" below.

       DESTROY this
           This method is triggered when a tied hash is about to go out of scope.  You don't really need it
           unless you're trying to add debugging or have auxiliary state to clean up.  Here's a very simple
           function:

               sub DESTROY  {
                   carp &amp;whowasi if $DEBUG;
               }

       Note that functions such as <u>keys()</u> and <u>values()</u> may return huge lists when used on large objects,
       like DBM files.  You may prefer to use the <u>each()</u> function to iterate over such.  Example:

           # print out history file offsets
           use NDBM_File;
           tie(%HIST, 'NDBM_File', '/usr/lib/news/history', 1, 0);
           while (($key,$val) = each %HIST) {
               print $key, ' = ', unpack('L',$val), "\n";
           }
           untie(%HIST);

       <b>Tying</b> <b>FileHandles</b>

       This is partially implemented now.

       A class implementing a tied filehandle should define the following methods: TIEHANDLE, at least one
       of PRINT, PRINTF, WRITE, READLINE, GETC, READ, and possibly CLOSE, UNTIE and DESTROY.  The class can
       also provide: BINMODE, OPEN, EOF, FILENO, SEEK, TELL - if the corresponding perl operators are used
       on the handle.

       When STDERR is tied, its PRINT method will be called to issue warnings and error messages.  This fea-<font color="#ffffff" class="whiteout">ture&nbsp;feature</font>
       ture is temporarily disabled during the call, which means you can use "warn()" inside PRINT without
       starting a recursive loop.  And just like "__WARN__" and "__DIE__" handlers, STDERR's PRINT method
       may be called to report parser errors, so the caveats mentioned under "%SIG" in perlvar apply.

       All of this is especially useful when perl is embedded in some other program, where output to STDOUT
       and STDERR may have to be redirected in some special way.  See nvi and the Apache module for exam-<font color="#ffffff" class="whiteout">ples.&nbsp;examples.</font>
       ples.

       In our example we're going to create a shouting handle.

           package Shout;

       TIEHANDLE classname, LIST
           This is the constructor for the class.  That means it is expected to return a blessed reference
           of some sort. The reference can be used to hold some internal information.

               sub TIEHANDLE { print "&lt;shout&gt;\n"; my $i; bless \$i, shift }

       WRITE this, LIST
           This method will be called when the handle is written to via the "syswrite" function.

               sub WRITE {
                   $r = shift;
                   my($buf,$len,$offset) = @_;
                   print "WRITE called, \$buf=$buf, \$len=$len, \$offset=$offset";
               }

       PRINT this, LIST
           This method will be triggered every time the tied handle is printed to with the "print()" func-<font color="#ffffff" class="whiteout">tion.&nbsp;function.</font>
           tion.  Beyond its self reference it also expects the list that was passed to the print function.

               sub PRINT { $r = shift; $$r++; print join($,,map(uc($_),@_)),$\ }

       PRINTF this, LIST
           This method will be triggered every time the tied handle is printed to with the "printf()" func-<font color="#ffffff" class="whiteout">tion.&nbsp;function.</font>
           tion.  Beyond its self reference it also expects the format and list that was passed to the
           printf function.

               sub PRINTF {
                   shift;
                   my $fmt = shift;
                   print sprintf($fmt, @_);
               }

       READ this, LIST
           This method will be called when the handle is read from via the "read" or "sysread" functions.

               sub READ {
                   my $self = shift;
                   my $bufref = \$_[0];
                   my(undef,$len,$offset) = @_;
                   print "READ called, \$buf=$bufref, \$len=$len, \$offset=$offset";
                   # add to $$bufref, set $len to number of characters read
                   $len;
               }

       READLINE this
           This method will be called when the handle is read from via &lt;HANDLE&gt;.  The method should return
           undef when there is no more data.

               sub READLINE { $r = shift; "READLINE called $$r times\n"; }

       GETC this
           This method will be called when the "getc" function is called.

               sub GETC { print "Don't GETC, Get Perl"; return "a"; }

       CLOSE this
           This method will be called when the handle is closed via the "close" function.

               sub CLOSE { print "CLOSE called.\n" }

       UNTIE this
           As with the other types of ties, this method will be called when "untie" happens.  It may be
           appropriate to "auto CLOSE" when this occurs.  See "The "untie" Gotcha" below.

       DESTROY this
           As with the other types of ties, this method will be called when the tied handle is about to be
           destroyed. This is useful for debugging and possibly cleaning up.

               sub DESTROY { print "&lt;/shout&gt;\n" }

       Here's how to use our little example:

           tie(*FOO,'Shout');
           print FOO "hello\n";
           $a = 4; $b = 6;
           print FOO $a, " plus ", $b, " equals ", $a + $b, "\n";
           print &lt;FOO&gt;;

       <b>UNTIE</b> <b>this</b>

       You can define for all tie types an UNTIE method that will be called at <u>untie()</u>.  See "The "untie"
       Gotcha" below.

       <b>The</b> <b>"untie"</b> <b>Gotcha</b>

       If you intend making use of the object returned from either <u>tie()</u> or <u>tied()</u>, and if the tie's target
       class defines a destructor, there is a subtle gotcha you <u>must</u> guard against.

       As setup, consider this (admittedly rather contrived) example of a tie; all it does is use a file to
       keep a log of the values assigned to a scalar.

           package Remember;

           use strict;
           use warnings;
           use IO::File;

           sub TIESCALAR {
               my $class = shift;
               my $filename = shift;
               my $handle = new IO::File "&gt; $filename"
                                or die "Cannot open $filename: $!\n";

               print $handle "The Start\n";
               bless {FH =&gt; $handle, Value =&gt; 0}, $class;
           }

           sub FETCH {
               my $self = shift;
               return $self-&gt;{Value};
           }

           sub STORE {
               my $self = shift;
               my $value = shift;
               my $handle = $self-&gt;{FH};
               print $handle "$value\n";
               $self-&gt;{Value} = $value;
           }

           sub DESTROY {
               my $self = shift;
               my $handle = $self-&gt;{FH};
               print $handle "The End\n";
               close $handle;
           }

           1;

       Here is an example that makes use of this tie:

           use strict;
           use Remember;

           my $fred;
           tie $fred, 'Remember', 'myfile.txt';
           $fred = 1;
           $fred = 4;
           $fred = 5;
           untie $fred;
           system "cat myfile.txt";

       This is the output when it is executed:

           The Start
           1
           4
           5
           The End

       So far so good.  Those of you who have been paying attention will have spotted that the tied object
       hasn't been used so far.  So lets add an extra method to the Remember class to allow comments to be
       included in the file -- say, something like this:

           sub comment {
               my $self = shift;
               my $text = shift;
               my $handle = $self-&gt;{FH};
               print $handle $text, "\n";
           }

       And here is the previous example modified to use the "comment" method (which requires the tied
       object):

           use strict;
           use Remember;

           my ($fred, $x);
           $x = tie $fred, 'Remember', 'myfile.txt';
           $fred = 1;
           $fred = 4;
           comment $x "changing...";
           $fred = 5;
           untie $fred;
           system "cat myfile.txt";

       When this code is executed there is no output.  Here's why:

       When a variable is tied, it is associated with the object which is the return value of the TIESCALAR,
       TIEARRAY, or TIEHASH function.  This object normally has only one reference, namely, the implicit
       reference from the tied variable.  When <u>untie()</u> is called, that reference is destroyed.  Then, as in
       the first example above, the object's destructor (DESTROY) is called, which is normal for objects
       that have no more valid references; and thus the file is closed.

       In the second example, however, we have stored another reference to the tied object in $x.  That
       means that when <u>untie()</u> gets called there will still be a valid reference to the object in existence,
       so the destructor is not called at that time, and thus the file is not closed.  The reason there is
       no output is because the file buffers have not been flushed to disk.

       Now that you know what the problem is, what can you do to avoid it?  Prior to the introduction of the
       optional UNTIE method the only way was the good old "-w" flag. Which will spot any instances where
       you call <u>untie()</u> and there are still valid references to the tied object.  If the second script above
       this near the top "use warnings 'untie'" or was run with the "-w" flag, Perl prints this warning mes-<font color="#ffffff" class="whiteout">sage:&nbsp;message:</font>
       sage:

           untie attempted while 1 inner references still exist

       To get the script to work properly and silence the warning make sure there are no valid references to
       the tied object <u>before</u> <u>untie()</u> is called:

           undef $x;
           untie $fred;

       Now that UNTIE exists the class designer can decide which parts of the class functionality are really
       associated with "untie" and which with the object being destroyed. What makes sense for a given class
       depends on whether the inner references are being kept so that non-tie-related methods can be called
       on the object. But in most cases it probably makes sense to move the functionality that would have
       been in DESTROY to the UNTIE method.

       If the UNTIE method exists then the warning above does not occur. Instead the UNTIE method is passed
       the count of "extra" references and can issue its own warning if appropriate. e.g. to replicate the
       no UNTIE case this method can be used:

           sub UNTIE
           {
            my ($obj,$count) = @_;
            carp "untie attempted while $count inner references still exist" if $count;
           }

<b>SEE</b> <b>ALSO</b>
       See DB_File or Config for some interesting <u>tie()</u> implementations.  A good starting point for many
       <u>tie()</u> implementations is with one of the modules Tie::Scalar, Tie::Array, Tie::Hash, or Tie::Handle.

<b>BUGS</b>
       The bucket usage information provided by "scalar(%hash)" is not available.  What this means is that
       using %tied_hash in boolean context doesn't work right (currently this always tests false, regardless
       of whether the hash is empty or hash elements).

       Localizing tied arrays or hashes does not work.  After exiting the scope the arrays or the hashes are
       not restored.

       Counting the number of entries in a hash via "scalar(keys(%hash))" or "scalar(values(%hash)") is
       inefficient since it needs to iterate through all the entries with FIRSTKEY/NEXTKEY.

       Tied hash/array slices cause multiple FETCH/STORE pairs, there are no tie methods for slice opera-<font color="#ffffff" class="whiteout">tions.&nbsp;operations.</font>
       tions.

       You cannot easily tie a multilevel data structure (such as a hash of hashes) to a dbm file.  The
       first problem is that all but GDBM and Berkeley DB have size limitations, but beyond that, you also
       have problems with how references are to be represented on disk.  One experimental module that does
       attempt to address this need is DBM::Deep.  Check your nearest CPAN site as described in perlmodlib
       for source code.  Note that despite its name, DBM::Deep does not use dbm.  Another earlier attempt at
       solving the problem is MLDBM, which is also available on the CPAN, but which has some fairly serious
       limitations.

       Tied filehandles are still incomplete.  <u>sysopen()</u>, <u>truncate()</u>, <u>flock()</u>, <u>fcntl()</u>, <u>stat()</u> and -X can't
       currently be trapped.

<b>AUTHOR</b>
       Tom Christiansen

       TIEHANDLE by Sven Verdoolaege &lt;<u>skimo@dns.ufsia.ac.be&gt;</u> and Doug MacEachern &lt;<u>dougm@osf.org&gt;</u>

       UNTIE by Nick Ing-Simmons &lt;<u>nick@ing-simmons.net&gt;</u>

       SCALAR by Tassilo von Parseval &lt;<u>tassilo.von.parseval@rwth-aachen.de&gt;</u>

       Tying Arrays by Casey West &lt;<u>casey@geeknest.com&gt;</u>



perl v5.8.8                                      2006-01-07                                       PERLTIE(1)
</tt></pre>
</td></tr></table>
	<table width="100%" border="0" cellpadding="0" cellspacing="0">
		<tr>
			<td><div style="width: 100%; height: 1px; background-color: #919699; margin-top: 5px; margin-bottom: 15px"></div></td>
		</tr>
		<tr>
			<td align="center"><br/>
				<table border="0" cellpadding="0" cellspacing="0" class="graybox">
					<tr>
						<th>Did this document help you?</th>
					</tr>
					<tr>
						<td>
						    <div style="margin-bottom: 8px"><a href="http://developer.apple.com/feedback/?v=1&url=/documentation/Darwin/Reference/ManPages/man1/perltie.1.html%3Fid%3DTP40000894-7.0&media=dvd" target=_new>Yes</a>:  Tell us what works for you.</div>
							<div style="margin-bottom: 8px"><a href="http://developer.apple.com/feedback/?v=2&url=/documentation/Darwin/Reference/ManPages/man1/perltie.1.html%3Fid%3DTP40000894-7.0&media=dvd" target=_new>It&#8217;s good, but:</a> Report typos, inaccuracies, and so forth.</div>
							<div><a href="http://developer.apple.com/feedback/?v=3&url=/documentation/Darwin/Reference/ManPages/man1/perltie.1.html%3Fid%3DTP40000894-7.0&media=dvd" target=_new>It wasn&#8217;t helpful</a>: Tell us what would have helped.</div>
						</td>
					</tr>
				</table>
			</td>
		</tr>
	</table>

</body></html>
