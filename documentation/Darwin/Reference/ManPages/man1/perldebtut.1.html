<html><head><title>Mac OS X
 Manual Page For perldebtut(1)</title>
<style type="text/css"><!-- @import "../../../../adcstyle.css"; .char {background: #ffffff; color:#0000ff;}.comment {background:#ffffff; color:#236e25}.function {background:#ffffff; color:#000000;}.keyword {background:#ffffff; color:#761550;}.number {background: #ffffff; color:#0000ff;}.param {background:#ffffff; color:#000000;}.preprocessor {background:#ffffff; color:#236e25}.string {background: #ffffff; color:#891315;}.template {background:#ffffff; color:#761550;}.type {background:#ffffff; color:#761550;}.var {background:#ffffff; color:#000000;} pre.manpages {font-size: 10pt;} .graybox { border-top: 1px solid #919699; border-left: 1px solid #919699; margin-bottom: 10px; } .graybox th { padding: 4px 8px 4px 8px; background: #E2E2E2; font-size: 12px; font-weight: bold; border-bottom: 1px solid #919699; border-right: 1px solid #919699; } .graybox td { padding: 8px; font-size: 12px; text-align: left; vertical-align: top; border-bottom: 1px solid #919699; border-right: 1px solid #919699; } .whiteout { font-size: 1px; background: #ffffff; color: #ffffff; } --></style>
<style type="text/css" media="print, aural, braille, embossed, projection, tty"><!-- @import "../../../../adcstyle.css"; .char {background: #ffffff; color:#0000ff;}.comment {background:#ffffff; color:#236e25}.function {background:#ffffff; color:#000000;}.keyword {background:#ffffff; color:#761550;}.number {background: #ffffff; color:#0000ff;}.param {background:#ffffff; color:#000000;}.preprocessor {background:#ffffff; color:#236e25}.string {background: #ffffff; color:#891315;}.template {background:#ffffff; color:#761550;}.type {background:#ffffff; color:#761550;}.var {background:#ffffff; color:#000000;} pre.manpages {font-size: 10pt;} .graybox { border-top: 1px solid #919699; border-left: 1px solid #919699; margin-bottom: 10px; } .graybox th { padding: 4px 8px 4px 8px; background: #E2E2E2; font-size: 12px; font-weight: bold; border-bottom: 1px solid #919699; border-right: 1px solid #919699; } .graybox td { padding: 8px; font-size: 12px; text-align: left; vertical-align: top; border-bottom: 1px solid #919699; border-right: 1px solid #919699; } .whiteout { display: none; } --></style>
</head><body bgcolor="white">
<a name="//apple_ref/doc/man/1/perldebtut" title="Mac OS X
 Manual Page for perldebtut(1)"><!-- headerDoc=man; indexgroup=Section 1; uid="//apple_ref/doc/man/1/perldebtut"; name=perldebtut(1) --></a>
<!--#include virtual="/includes/framesetheader"-->
<table width=600><tr><td scope="row">
<!-- begin breadcrumb insert request --><font face="Geneva,Helvetica,Arial" size="1">
 <a target="_top" href="http://developer.apple.com">ADC Home</a> <b>&gt;</b> <a href="../../../../../referencelibrary/index.html#//apple_ref/doc/uid/TP30000943" target="_top">Reference Library</a> <b>&gt;</b> <a href="../../../../../reference/index.html#//apple_ref/doc/uid/TP30001281" target="_top">Reference</a> <b>&gt;</b> <!-- a href="" target="_top" -->Mac OS X<!-- /a --> <b>&gt;</b> <a href="../index.html#//apple_ref/doc/framework/manpages" target="_top">Mac OS X Man Pages</a></font>
<!-- end breadcrumb insert request -->
<p>&nbsp;</p>
</td></tr></table>
<!-- end of header -->
<table border="0"  cellpadding="2" cellspacing="2" width="630pt"><tr><td valign="top" height="12" colspan="5" scope="row">
<p style='text-align:justify;'>This document is a Mac OS X manual page.  Manual pages are a command-line technology
for providing documentation.  You can view these manual pages locally using the
<a href="man.1.html#//apple_ref/doc/man/1/man">man(1)</a> command.
These manual pages come from many different sources, and thus, have a variety of writing
styles.</p>
<p style='text-align:justify;'>For more information about the manual page format, see the manual page for 
<a href="../man5/manpages.5.html#//apple_ref/doc/man/5/manpages">manpages(5)</a>.</p>
</td></tr></table>
<hr />
<table border="0"  cellpadding="2" cellspacing="2" width="300"><tr><td valign="top" height="12" colspan="5" scope="row"><pre class="manpages"><tt>
PERLDEBTUT(1)                         Perl Programmers Reference Guide                         PERLDEBTUT(1)



<b>NAME</b>
       perldebtut - Perl debugging tutorial

<b>DESCRIPTION</b>
       A (very) lightweight introduction in the use of the perl debugger, and a pointer to existing, deeper
       sources of information on the subject of debugging perl programs.

       There's an extraordinary number of people out there who don't appear to know anything about using the
       perl debugger, though they use the language every day.  This is for them.

<b>use</b> <b>strict</b>
       First of all, there's a few things you can do to make your life a lot more straightforward when it
       comes to debugging perl programs, without using the debugger at all.  To demonstrate, here's a simple
       script, named "hello", with a problem:

               #!/usr/bin/perl

               $var1 = 'Hello World'; # always wanted to do that :-)
               $var2 = "$varl\n";

               print $var2;
               exit;

       While this compiles and runs happily, it probably won't do what's expected, namely it doesn't print
       "Hello World\n" at all;  It will on the other hand do exactly what it was told to do, computers being
       a bit that way inclined.  That is, it will print out a newline character, and you'll get what looks
       like a blank line.  It looks like there's 2 variables when (because of the typo) there's really 3:

               $var1 = 'Hello World';
               $varl = undef;
               $var2 = "\n";

       To catch this kind of problem, we can force each variable to be declared before use by pulling in the
       strict module, by putting 'use strict;' after the first line of the script.

       Now when you run it, perl complains about the 3 undeclared variables and we get four error messages
       because one variable is referenced twice:

        Global symbol "$var1" requires explicit package name at ./t1 line 4.
        Global symbol "$var2" requires explicit package name at ./t1 line 5.
        Global symbol "$varl" requires explicit package name at ./t1 line 5.
        Global symbol "$var2" requires explicit package name at ./t1 line 7.
        Execution of ./hello aborted due to compilation errors.

       Luvverly! and to fix this we declare all variables explicitly and now our script looks like this:

               #!/usr/bin/perl
               use strict;

               my $var1 = 'Hello World';
               my $varl = undef;
               my $var2 = "$varl\n";

               print $var2;
               exit;

       We then do (always a good idea) a syntax check before we try to run it again:

               &gt; perl -c hello
               hello syntax OK

       And now when we run it, we get "\n" still, but at least we know why.  Just getting this script to
       compile has exposed the '$varl' (with the letter 'l') variable, and simply changing $varl to $var1
       solves the problem.

<b>Looking</b> <b>at</b> <b>data</b> <b>and</b> <b>-w</b> <b>and</b> <b>v</b>
       Ok, but how about when you want to really see your data, what's in that dynamic variable, just before
       using it?

               #!/usr/bin/perl
               use strict;

               my $key = 'welcome';
               my %data = (
                       'this' =&gt; qw(that),
                       'tom' =&gt; qw(and jerry),
                       'welcome' =&gt; q(Hello World),
                       'zip' =&gt; q(welcome),
               );
               my @data = keys %data;

               print "$data{$key}\n";
               exit;

       Looks OK, after it's been through the syntax check (perl -c scriptname), we run it and all we get is
       a blank line again!  Hmmmm.

       One common debugging approach here, would be to liberally sprinkle a few print statements, to add a
       check just before we print out our data, and another just after:

               print "All OK\n" if grep($key, keys %data);
               print "$data{$key}\n";
               print "done: '$data{$key}'\n";

       And try again:

               &gt; perl data
               All OK

               done: ''

       After much staring at the same piece of code and not seeing the wood for the trees for some time, we
       get a cup of coffee and try another approach.  That is, we bring in the cavalry by giving perl the
       '<b>-d</b>' switch on the command line:

               &gt; perl -d data
               Default die handler restored.

               Loading DB routines from perl5db.pl version 1.07
               Editor support available.

               Enter h or `h h' for help, or `man perldebug' for more help.

               main::(./data:4):     my $key = 'welcome';

       Now, what we've done here is to launch the built-in perl debugger on our script.  It's stopped at the
       first line of executable code and is waiting for input.

       Before we go any further, you'll want to know how to quit the debugger: use just the letter '<b>q</b>', not
       the words 'quit' or 'exit':

               DB&lt;1&gt; q
               &gt;

       That's it, you're back on home turf again.

<b>help</b>
       Fire the debugger up again on your script and we'll look at the help menu.  There's a couple of ways
       of calling help: a simple '<b>h</b>' will get the summary help list, '<b>|h</b>' (pipe-h) will pipe the help
       through your pager (which is (probably 'more' or 'less'), and finally, '<b>h</b> <b>h</b>' (h-space-h) will give
       you the entire help screen.  Here is the summary page:

       D<b>1</b>h

        List/search source lines:               Control script execution:
         l [ln|sub]  List source code            T           Stack trace
         - or .      List previous/current line  s [expr]    Single step [in expr]
         v [line]    View around line            n [expr]    Next, steps over subs
         f filename  View source in file         &lt;CR/Enter&gt;  Repeat last n or s
         /pattern/ ?patt?   Search forw/backw    r           Return from subroutine
         M           Show module versions        c [ln|sub]  Continue until position
        Debugger controls:                       L           List break/watch/actions
         o [...]     Set debugger options        t [expr]    Toggle trace [trace expr]
         &lt;[&lt;]|{[{]|&gt;[&gt;] [cmd] Do pre/post-prompt b [ln|event|sub] [cnd] Set breakpoint
         ! [N|pat]   Redo a previous command     B ln|*      Delete a/all breakpoints
         H [-num]    Display last num commands   a [ln] cmd  Do cmd before line
         = [a val]   Define/list an alias        A ln|*      Delete a/all actions
         h [db_cmd]  Get help on command         w expr      Add a watch expression
         h h         Complete help page          W expr|*    Delete a/all watch exprs
         |[|]db_cmd  Send output to pager        ![!] syscmd Run cmd in a subprocess
         q or ^D     Quit                        R           Attempt a restart
        Data Examination:     expr     Execute perl code, also see: s,n,t expr
         x|m expr       Evals expr in list context, dumps the result or lists methods.
         p expr         Print expression (uses script's current package).
         S [[!]pat]     List subroutine names [not] matching pattern
         V [Pk [Vars]]  List Variables in Package.  Vars can be ~pattern or !pattern.
         X [Vars]       Same as "V current_package [Vars]".
         y [n [Vars]]   List lexicals in higher scope &lt;n&gt;.  Vars same as V.
        For more help, type h cmd_letter, or run man perldebug for all docs.

       More confusing options than you can shake a big stick at!  It's not as bad as it looks and it's very
       useful to know more about all of it, and fun too!

       There's a couple of useful ones to know about straight away.  You wouldn't think we're using any
       libraries at all at the moment, but '<b>M</b>' will show which modules are currently loaded, and their ver-<font color="#ffffff" class="whiteout">sion&nbsp;version</font>
       sion number, while '<b>m</b>' will show the methods, and '<b>S</b>' shows all subroutines (by pattern) as shown
       below.  '<b>V</b>' and '<b>X</b>' show variables in the program by package scope and can be constrained by pattern.

               DB&lt;2&gt;S str
               dumpvar::stringify
               strict::bits
               strict::import
               strict::unimport

       Using 'X' and cousins requires you not to use the type identifiers ($@%), just the 'name':

               DM&lt;3&gt;X ~err
               FileHandle(stderr) =&gt; <!-- a -->fileno(2)<!-- /a -->

       Remember we're in our tiny program with a problem, we should have a look at where we are, and what
       our data looks like. First of all let's view some code at our present position (the first line of
       code in this case), via '<b>v</b>':

               DB&lt;4&gt; v
               1       #!/usr/bin/perl
               2:      use strict;
               3
               4==&gt;    my $key = 'welcome';
               5:      my %data = (
               6               'this' =&gt; qw(that),
               7               'tom' =&gt; qw(and jerry),
               8               'welcome' =&gt; q(Hello World),
               9               'zip' =&gt; q(welcome),
               10      );

       At line number 4 is a helpful pointer, that tells you where you are now.  To see more code, type 'v'
       again:

               DB&lt;4&gt; v
               8               'welcome' =&gt; q(Hello World),
               9               'zip' =&gt; q(welcome),
               10      );
               11:     my @data = keys %data;
               12:     print "All OK\n" if grep($key, keys %data);
               13:     print "$data{$key}\n";
               14:     print "done: '$data{$key}'\n";
               15:     exit;

       And if you wanted to list line 5 again, type 'l 5', (note the space):

               DB&lt;4&gt; l 5
               5:      my %data = (

       In this case, there's not much to see, but of course normally there's pages of stuff to wade through,
       and 'l' can be very useful.  To reset your view to the line we're about to execute, type a lone
       period '.':

               DB&lt;5&gt; .
               main::(./data_a:4):     my $key = 'welcome';

       The line shown is the one that is about to be executed <b>next</b>, it hasn't happened yet.  So while we can
       print a variable with the letter '<b>p</b>', at this point all we'd get is an empty (undefined) value back.
       What we need to do is to step through the next executable statement with an '<b>s</b>':

               DB&lt;6&gt; s
               main::(./data_a:5):     my %data = (
               main::(./data_a:6):             'this' =&gt; qw(that),
               main::(./data_a:7):             'tom' =&gt; qw(and jerry),
               main::(./data_a:8):             'welcome' =&gt; q(Hello World),
               main::(./data_a:9):             'zip' =&gt; q(welcome),
               main::(./data_a:10):    );

       Now we can have a look at that first ($key) variable:

               DB&lt;7&gt; p $key
               welcome

       line 13 is where the action is, so let's continue down to there via the letter '<b>c</b>', which by the way,
       inserts a 'one-time-only' breakpoint at the given line or sub routine:

               DB&lt;8&gt; c 13
               All OK
               main::(./data_a:13):    print "$data{$key}\n";

       We've gone past our check (where 'All OK' was printed) and have stopped just before the meat of our
       task.  We could try to print out a couple of variables to see what is happening:

               DB&lt;9&gt; p $data{$key}

       Not much in there, lets have a look at our hash:

               DB&lt;10&gt; p %data
               Hello Worldziptomandwelcomejerrywelcomethisthat

               DB&lt;11&gt; p keys %data
               Hello Worldtomwelcomejerrythis

       Well, this isn't very easy to read, and using the helpful manual (<b>h</b> <b>h</b>), the '<b>x</b>' command looks promis-<font color="#ffffff" class="whiteout">ing:&nbsp;promising:</font>
       ing:

               DB&lt;12&gt; x %data
               0  'Hello World'
               1  'zip'
               2  'tom'
               3  'and'
               4  'welcome'
               5  undef
               6  'jerry'
               7  'welcome'
               8  'this'
               9  'that'

       That's not much help, a couple of welcomes in there, but no indication of which are keys, and which
       are values, it's just a listed array dump and, in this case, not particularly helpful.  The trick
       here, is to use a <b>reference</b> to the data structure:

               DB&lt;13&gt; x \%data
               0  HASH(0x8194bc4)
                  'Hello World' =&gt; 'zip'
                  'jerry' =&gt; 'welcome'
                  'this' =&gt; 'that'
                  'tom' =&gt; 'and'
                  'welcome' =&gt; undef

       The reference is truly dumped and we can finally see what we're dealing with.  Our quoting was per-<font color="#ffffff" class="whiteout">fectly&nbsp;perfectly</font>
       fectly valid but wrong for our purposes, with 'and jerry' being treated as 2 separate words rather
       than a phrase, thus throwing the evenly paired hash structure out of alignment.

       The '<b>-w</b>' switch would have told us about this, had we used it at the start, and saved us a lot of
       trouble:

               &gt; perl -w data
               Odd number of elements in hash assignment at ./data line 5.

       We fix our quoting: 'tom' =&gt; q(and jerry), and run it again, this time we get our expected output:

               &gt; perl -w data
               Hello World

       While we're here, take a closer look at the '<b>x</b>' command, it's really useful and will merrily dump out
       nested references, complete objects, partial objects - just about whatever you throw at it:

       Let's make a quick object and x-plode it, first we'll start the debugger: it wants some form of input
       from STDIN, so we give it something non-committal, a zero:

               &gt; perl -de 0
               Default die handler restored.

               Loading DB routines from perl5db.pl version 1.07
               Editor support available.

               Enter h or `h h' for help, or `man perldebug' for more help.

               main::(-e:1):   0

       Now build an on-the-fly object over a couple of lines (note the backslash):

               DB&lt;1&gt; $obj = bless({'unique_id'=&gt;'123', 'attr'=&gt; \
               cont:   {'col' =&gt; 'black', 'things' =&gt; [qw(this that etc)]}}, 'MY_class')

       And let's have a look at it:

               DB&lt;2&gt; x $obj
               0  MY_class=HASH(0x828ad98)
                       'attr' =&gt; HASH(0x828ad68)
               'col' =&gt; 'black'
               'things' =&gt; ARRAY(0x828abb8)
                       0  'this'
                       1  'that'
                       2  'etc'
                       'unique_id' =&gt; 123
               DB&lt;3&gt;

       Useful, huh?  You can eval nearly anything in there, and experiment with bits of code or regexes
       until the cows come home:

               DB&lt;3&gt; @data = qw(this that the other atheism leather theory scythe)

               DB&lt;4&gt; p 'saw -&gt; '.($cnt += map { print "\t:\t$_\n" } grep(/the/, sort @data))
               atheism
               leather
               other
               scythe
               the
               theory
               saw -&gt; 6

       If you want to see the command History, type an '<b>H</b>':

               DB&lt;5&gt; H
               4: p 'saw -&gt; '.($cnt += map { print "\t:\t$_\n" } grep(/the/, sort @data))
               3: @data = qw(this that the other atheism leather theory scythe)
               2: x $obj
               1: $obj = bless({'unique_id'=&gt;'123', 'attr'=&gt;
               {'col' =&gt; 'black', 'things' =&gt; [qw(this that etc)]}}, 'MY_class')
               DB&lt;5&gt;

       And if you want to repeat any previous command, use the exclamation: '<b>!</b>':

               DB&lt;5&gt; !4
               p 'saw -&gt; '.($cnt += map { print "$_\n" } grep(/the/, sort @data))
               atheism
               leather
               other
               scythe
               the
               theory
               saw -&gt; 12

       For more on references see perlref and perlreftut

<b>Stepping</b> <b>through</b> <b>code</b>
       Here's a simple program which converts between Celsius and Fahrenheit, it too has a problem:

               #!/usr/bin/perl -w
               use strict;

               my $arg = $ARGV[0] || '-c20';

               if ($arg =~ /^\-(c|f)((\-|\+)*\d+(\.\d+)*)$/) {
                       my ($deg, $num) = ($1, $2);
                       my ($in, $out) = ($num, $num);
                       if ($deg eq 'c') {
                               $deg = 'f';
                               $out = &amp;c2f($num);
                       } else {
                               $deg = 'c';
                               $out = &amp;f2c($num);
                       }
                       $out = sprintf('%0.2f', $out);
                       $out =~ s/^((\-|\+)*\d+)\.0+$/$1/;
                       print "$out $deg\n";
               } else {
                       print "Usage: $0 -[c|f] num\n";
               }
               exit;

               sub f2c {
                       my $f = shift;
                       my $c = 5 * $f - 32 / 9;
                       return $c;
               }

               sub c2f {
                       my $c = shift;
                       my $f = 9 * $c / 5 + 32;
                       return $f;
               }

       For some reason, the Fahrenheit to Celsius conversion fails to return the expected output.  This is
       what it does:

               &gt; temp -c0.72
               33.30 f

               &gt; temp -f33.3
               162.94 c

       Not very consistent!  We'll set a breakpoint in the code manually and run it under the debugger to
       see what's going on.  A breakpoint is a flag, to which the debugger will run without interruption,
       when it reaches the breakpoint, it will stop execution and offer a prompt for further interaction.
       In normal use, these debugger commands are completely ignored, and they are safe - if a little messy,
       to leave in production code.

               my ($in, $out) = ($num, $num);
               $DB::single=2; # insert at line 9!
               if ($deg eq 'c')
                       ...

               &gt; perl -d temp -f33.3
               Default die handler restored.

               Loading DB routines from perl5db.pl version 1.07
               Editor support available.

               Enter h or `h h' for help, or `man perldebug' for more help.

               main::(temp:4): my $arg = $ARGV[0] || '-c100';

       We'll simply continue down to our pre-set breakpoint with a '<b>c</b>':

               DB&lt;1&gt; c
               main::(temp:10):                if ($deg eq 'c') {

       Followed by a view command to see where we are:

               DB&lt;1&gt; v
               7:              my ($deg, $num) = ($1, $2);
               8:              my ($in, $out) = ($num, $num);
               9:              $DB::single=2;
               10==&gt;           if ($deg eq 'c') {
               11:                     $deg = 'f';
               12:                     $out = &amp;c2f($num);
               13              } else {
               14:                     $deg = 'c';
               15:                     $out = &amp;f2c($num);
               16              }

       And a print to show what values we're currently using:

               DB&lt;1&gt; p $deg, $num
               f33.3

       We can put another break point on any line beginning with a colon, we'll use line 17 as that's just
       as we come out of the subroutine, and we'd like to pause there later on:

               DB&lt;2&gt; b 17

       There's no feedback from this, but you can see what breakpoints are set by using the list 'L' com-<font color="#ffffff" class="whiteout">mand:&nbsp;command:</font>
       mand:

               DB&lt;3&gt; L
               temp:
                       17:            print "$out $deg\n";
                       break if (1)

       Note that to delete a breakpoint you use 'd' or 'D'.

       Now we'll continue down into our subroutine, this time rather than by line number, we'll use the sub-<font color="#ffffff" class="whiteout">routine&nbsp;subroutine</font>
       routine name, followed by the now familiar 'v':

               DB&lt;3&gt; c f2c
               main::f2c(temp:30):             my $f = shift;

               DB&lt;4&gt; v
               24:     exit;
               25
               26      sub f2c {
               27==&gt;           my $f = shift;
               28:             my $c = 5 * $f - 32 / 9;
               29:             return $c;
               30      }
               31
               32      sub c2f {
               33:             my $c = shift;

       Note that if there was a subroutine call between us and line 29, and we wanted to <b>single-step</b> through
       it, we could use the '<b>s</b>' command, and to step over it we would use '<b>n</b>' which would execute the sub,
       but not descend into it for inspection.  In this case though, we simply continue down to line 29:

               DB&lt;4&gt; c 29
               main::f2c(temp:29):             return $c;

       And have a look at the return value:

               DB&lt;5&gt; p $c
               162.944444444444

       This is not the right answer at all, but the sum looks correct.  I wonder if it's anything to do with
       operator precedence?  We'll try a couple of other possibilities with our sum:

               DB&lt;6&gt; p (5 * $f - 32 / 9)
               162.944444444444

               DB&lt;7&gt; p 5 * $f - (32 / 9)
               162.944444444444

               DB&lt;8&gt; p (5 * $f) - 32 / 9
               162.944444444444

               DB&lt;9&gt; p 5 * ($f - 32) / 9
               0.722222222222221

       :-) that's more like it!  Ok, now we can set our return variable and we'll return out of the sub with
       an 'r':

               DB&lt;10&gt; $c = 5 * ($f - 32) / 9

               DB&lt;11&gt; r
               scalar context return from main::f2c: 0.722222222222221

       Looks good, let's just continue off the end of the script:

               DB&lt;12&gt; c
               0.72 c
               Debugged program terminated.  Use q to quit or R to restart,
               use O inhibit_exit to avoid stopping after program termination,
               h q, h R or h O to get additional info.

       A quick fix to the offending line (insert the missing parentheses) in the actual program and we're
       finished.

<b>Placeholder</b> <b>for</b> <b>a,</b> <b>w,</b> <b>t,</b> <b>T</b>
       Actions, watch variables, stack traces etc.: on the TODO list.

               a

               w

               t

               T

<b>REGULAR</b> <b>EXPRESSIONS</b>
       Ever wanted to know what a regex looked like?  You'll need perl compiled with the DEBUGGING flag for
       this one:

               &gt; perl -Dr -e '/^pe(a)*rl$/i'
               Compiling REx `^pe(a)*rl$'
               size 17 first at 2
               rarest char
                at 0
                  1: <!-- a -->BOL(2)<!-- /a -->
                  2: EXACTF &lt;pe&gt;(4)
                  4: CURLYN[1] {0,32767}(14)
                  6:   <!-- a -->NOTHING(8)<!-- /a -->
                  8:   EXACTF &lt;a&gt;(0)
                 12:   WHILEM(0)
                 13: <!-- a -->NOTHING(14)<!-- /a -->
                 14: EXACTF &lt;rl&gt;(16)
                 16: <!-- a -->EOL(17)<!-- /a -->
                 17: END(0)
               floating `'$ at 4..2147483647 (checking floating) stclass `EXACTF &lt;pe&gt;'
       anchored(BOL) minlen 4
               Omitting $` $&amp; $' support.

               EXECUTING...

               Freeing REx: `^pe(a)*rl$'

       Did you really want to know? :-) For more gory details on getting regular expressions to work, have a
       look at perlre, perlretut, and to decode the mysterious labels (BOL and CURLYN, etc. above), see
       perldebguts.

<b>OUTPUT</b> <b>TIPS</b>
       To get all the output from your error log, and not miss any messages via helpful operating system
       buffering, insert a line like this, at the start of your script:

               $|=1;

       To watch the tail of a dynamically growing logfile, (from the command line):

               tail -f $error_log

       Wrapping all die calls in a handler routine can be useful to see how, and from where, they're being
       called, perlvar has more information:

               BEGIN { $SIG{__DIE__} = sub { require Carp; Carp::confess(@_) } }

       Various useful techniques for the redirection of STDOUT and STDERR filehandles are explained in per-<font color="#ffffff" class="whiteout">lopentut&nbsp;perlopentut</font>
       lopentut and perlfaq8.

<b>CGI</b>
       Just a quick hint here for all those CGI programmers who can't figure out how on earth to get past
       that 'waiting for input' prompt, when running their CGI script from the command-line, try something
       like this:

               &gt; perl -d my_cgi.pl -nodebug

       Of course CGI and perlfaq9 will tell you more.

<b>GUIs</b>
       The command line interface is tightly integrated with an <b>emacs</b> extension and there's a <b>vi</b> interface
       too.

       You don't have to do this all on the command line, though, there are a few GUI options out there.
       The nice thing about these is you can wave a mouse over a variable and a dump of its data will appear
       in an appropriate window, or in a popup balloon, no more tiresome typing of 'x $varname' :-)

       In particular have a hunt around for the following:

       <b>ptkdb</b> perlTK based wrapper for the built-in debugger

       <b>ddd</b> data display debugger

       <b>PerlDevKit</b> and <b>PerlBuilder</b> are NT specific

       NB. (more info on these and others would be appreciated).

<b>SUMMARY</b>
       We've seen how to encourage good coding practices with <b>use</b> <b>strict</b> and <b>-w</b>.  We can run the perl debug-<font color="#ffffff" class="whiteout">ger&nbsp;debugger</font>
       ger <b>perl</b> <b>-d</b> <b>scriptname</b> to inspect your data from within the perl debugger with the <b>p</b> and <b>x</b> commands.
       You can walk through your code, set breakpoints with <b>b</b> and step through that code with <b>s</b> or <b>n</b>, con-<font color="#ffffff" class="whiteout">tinue&nbsp;continue</font>
       tinue with <b>c</b> and return from a sub with <b>r</b>.  Fairly intuitive stuff when you get down to it.

       There is of course lots more to find out about, this has just scratched the surface.  The best way to
       learn more is to use perldoc to find out more about the language, to read the on-line help (perldebug
       is probably the next place to go), and of course, experiment.

<b>SEE</b> <b>ALSO</b>
       perldebug, perldebguts, perldiag, dprofpp, perlrun

<b>AUTHOR</b>
       Richard Foley &lt;richard@rfi.net&gt; Copyright (c) 2000

<b>CONTRIBUTORS</b>
       Various people have made helpful suggestions and contributions, in particular:

       Ronald J Kimball &lt;rjk@linguist.dartmouth.edu&gt;

       Hugo van der Sanden &lt;hv@crypt0.demon.co.uk&gt;

       Peter Scott &lt;Peter@PSDT.com&gt;



perl v5.8.8                                      2006-01-07                                    PERLDEBTUT(1)
</tt></pre>
</td></tr></table>
	<table width="100%" border="0" cellpadding="0" cellspacing="0">
		<tr>
			<td><div style="width: 100%; height: 1px; background-color: #919699; margin-top: 5px; margin-bottom: 15px"></div></td>
		</tr>
		<tr>
			<td align="center"><br/>
				<table border="0" cellpadding="0" cellspacing="0" class="graybox">
					<tr>
						<th>Did this document help you?</th>
					</tr>
					<tr>
						<td>
						    <div style="margin-bottom: 8px"><a href="http://developer.apple.com/feedback/?v=1&url=/documentation/Darwin/Reference/ManPages/man1/perldebtut.1.html%3Fid%3DTP40000894-7.0&media=dvd" target=_new>Yes</a>:  Tell us what works for you.</div>
							<div style="margin-bottom: 8px"><a href="http://developer.apple.com/feedback/?v=2&url=/documentation/Darwin/Reference/ManPages/man1/perldebtut.1.html%3Fid%3DTP40000894-7.0&media=dvd" target=_new>It&#8217;s good, but:</a> Report typos, inaccuracies, and so forth.</div>
							<div><a href="http://developer.apple.com/feedback/?v=3&url=/documentation/Darwin/Reference/ManPages/man1/perldebtut.1.html%3Fid%3DTP40000894-7.0&media=dvd" target=_new>It wasn&#8217;t helpful</a>: Tell us what would have helped.</div>
						</td>
					</tr>
				</table>
			</td>
		</tr>
	</table>

</body></html>
