<html><head><title>Mac OS X
 Manual Page For perlref(1)</title>
<style type="text/css"><!-- @import "../../../../adcstyle.css"; .char {background: #ffffff; color:#0000ff;}.comment {background:#ffffff; color:#236e25}.function {background:#ffffff; color:#000000;}.keyword {background:#ffffff; color:#761550;}.number {background: #ffffff; color:#0000ff;}.param {background:#ffffff; color:#000000;}.preprocessor {background:#ffffff; color:#236e25}.string {background: #ffffff; color:#891315;}.template {background:#ffffff; color:#761550;}.type {background:#ffffff; color:#761550;}.var {background:#ffffff; color:#000000;} pre.manpages {font-size: 10pt;} .graybox { border-top: 1px solid #919699; border-left: 1px solid #919699; margin-bottom: 10px; } .graybox th { padding: 4px 8px 4px 8px; background: #E2E2E2; font-size: 12px; font-weight: bold; border-bottom: 1px solid #919699; border-right: 1px solid #919699; } .graybox td { padding: 8px; font-size: 12px; text-align: left; vertical-align: top; border-bottom: 1px solid #919699; border-right: 1px solid #919699; } .whiteout { font-size: 1px; background: #ffffff; color: #ffffff; } --></style>
<style type="text/css" media="print, aural, braille, embossed, projection, tty"><!-- @import "../../../../adcstyle.css"; .char {background: #ffffff; color:#0000ff;}.comment {background:#ffffff; color:#236e25}.function {background:#ffffff; color:#000000;}.keyword {background:#ffffff; color:#761550;}.number {background: #ffffff; color:#0000ff;}.param {background:#ffffff; color:#000000;}.preprocessor {background:#ffffff; color:#236e25}.string {background: #ffffff; color:#891315;}.template {background:#ffffff; color:#761550;}.type {background:#ffffff; color:#761550;}.var {background:#ffffff; color:#000000;} pre.manpages {font-size: 10pt;} .graybox { border-top: 1px solid #919699; border-left: 1px solid #919699; margin-bottom: 10px; } .graybox th { padding: 4px 8px 4px 8px; background: #E2E2E2; font-size: 12px; font-weight: bold; border-bottom: 1px solid #919699; border-right: 1px solid #919699; } .graybox td { padding: 8px; font-size: 12px; text-align: left; vertical-align: top; border-bottom: 1px solid #919699; border-right: 1px solid #919699; } .whiteout { display: none; } --></style>
</head><body bgcolor="white">
<a name="//apple_ref/doc/man/1/perlref" title="Mac OS X
 Manual Page for perlref(1)"><!-- headerDoc=man; indexgroup=Section 1; uid="//apple_ref/doc/man/1/perlref"; name=perlref(1) --></a>
<!--#include virtual="/includes/framesetheader"-->
<table width=600><tr><td scope="row">
<!-- begin breadcrumb insert request --><font face="Geneva,Helvetica,Arial" size="1">
 <a target="_top" href="http://developer.apple.com">ADC Home</a> <b>&gt;</b> <a href="../../../../../referencelibrary/index.html#//apple_ref/doc/uid/TP30000943" target="_top">Reference Library</a> <b>&gt;</b> <a href="../../../../../reference/index.html#//apple_ref/doc/uid/TP30001281" target="_top">Reference</a> <b>&gt;</b> <!-- a href="" target="_top" -->Mac OS X<!-- /a --> <b>&gt;</b> <a href="../index.html#//apple_ref/doc/framework/manpages" target="_top">Mac OS X Man Pages</a></font>
<!-- end breadcrumb insert request -->
<p>&nbsp;</p>
</td></tr></table>
<!-- end of header -->
<table border="0"  cellpadding="2" cellspacing="2" width="630pt"><tr><td valign="top" height="12" colspan="5" scope="row">
<p style='text-align:justify;'>This document is a Mac OS X manual page.  Manual pages are a command-line technology
for providing documentation.  You can view these manual pages locally using the
<a href="man.1.html#//apple_ref/doc/man/1/man">man(1)</a> command.
These manual pages come from many different sources, and thus, have a variety of writing
styles.</p>
<p style='text-align:justify;'>For more information about the manual page format, see the manual page for 
<a href="../man5/manpages.5.html#//apple_ref/doc/man/5/manpages">manpages(5)</a>.</p>
</td></tr></table>
<hr />
<table border="0"  cellpadding="2" cellspacing="2" width="300"><tr><td valign="top" height="12" colspan="5" scope="row"><pre class="manpages"><tt>
PERLREF(1)                            Perl Programmers Reference Guide                            PERLREF(1)



<b>NAME</b>
       perlref - Perl references and nested data structures

<b>NOTE</b>
       This is complete documentation about all aspects of references.  For a shorter, tutorial introduction
       to just the essential features, see perlreftut.

<b>DESCRIPTION</b>
       Before release 5 of Perl it was difficult to represent complex data structures, because all refer-<font color="#ffffff" class="whiteout">ences&nbsp;references</font>
       ences had to be symbolic--and even then it was difficult to refer to a variable instead of a symbol
       table entry.  Perl now not only makes it easier to use symbolic references to variables, but also
       lets you have "hard" references to any piece of data or code.  Any scalar may hold a hard reference.
       Because arrays and hashes contain scalars, you can now easily build arrays of arrays, arrays of
       hashes, hashes of arrays, arrays of hashes of functions, and so on.

       Hard references are smart--they keep track of reference counts for you, automatically freeing the
       thing referred to when its reference count goes to zero.  (Reference counts for values in self-refer-<font color="#ffffff" class="whiteout">ential&nbsp;self-referential</font>
       ential or cyclic data structures may not go to zero without a little help; see "Two-Phased Garbage
       Collection" in perlobj for a detailed explanation.)  If that thing happens to be an object, the
       object is destructed.  See perlobj for more about objects.  (In a sense, everything in Perl is an
       object, but we usually reserve the word for references to objects that have been officially "blessed"
       into a class package.)

       Symbolic references are names of variables or other objects, just as a symbolic link in a Unix
       filesystem contains merely the name of a file.  The *glob notation is something of a symbolic refer-<font color="#ffffff" class="whiteout">ence.&nbsp;reference.</font>
       ence.  (Symbolic references are sometimes called "soft references", but please don't call them that;
       references are confusing enough without useless synonyms.)

       In contrast, hard references are more like hard links in a Unix file system: They are used to access
       an underlying object without concern for what its (other) name is.  When the word "reference" is used
       without an adjective, as in the following paragraph, it is usually talking about a hard reference.

       References are easy to use in Perl.  There is just one overriding principle: Perl does no implicit
       referencing or dereferencing.  When a scalar is holding a reference, it always behaves as a simple
       scalar.  It doesn't magically start being an array or hash or subroutine; you have to tell it explic-<font color="#ffffff" class="whiteout">itly&nbsp;explicitly</font>
       itly to do so, by dereferencing it.

       <b>Making</b> <b>References</b>

       References can be created in several ways.

       1.  By using the backslash operator on a variable, subroutine, or value.  (This works much like the &amp;
           (address-of) operator in C.)  This typically creates <u>another</u> reference to a variable, because
           there's already a reference to the variable in the symbol table.  But the symbol table reference
           might go away, and you'll still have the reference that the backslash returned.  Here are some
           examples:

               $scalarref = \$foo;
               $arrayref  = \@ARGV;
               $hashref   = \%ENV;
               $coderef   = \&amp;handler;
               $globref   = \*foo;

           It isn't possible to create a true reference to an IO handle (filehandle or dirhandle) using the
           backslash operator.  The most you can get is a reference to a typeglob, which is actually a com-<font color="#ffffff" class="whiteout">plete&nbsp;complete</font>
           plete symbol table entry.  But see the explanation of the *foo{THING} syntax below.  However, you
           can still use type globs and globrefs as though they were IO handles.

       2.  A reference to an anonymous array can be created using square brackets:

               $arrayref = [1, 2, ['a', 'b', 'c']];

           Here we've created a reference to an anonymous array of three elements whose final element is
           itself a reference to another anonymous array of three elements.  (The multidimensional syntax
           described later can be used to access this.  For example, after the above, "$arrayref-&gt;[2][1]"
           would have the value "b".)

           Taking a reference to an enumerated list is not the same as using square brackets--instead it's
           the same as creating a list of references!

               @list = (\$a, \@b, \%c);
               @list = \($a, @b, %c);      # same thing!

           As a special case, "\(@foo)" returns a list of references to the contents of @foo, not a refer-<font color="#ffffff" class="whiteout">ence&nbsp;reference</font>
           ence to @foo itself.  Likewise for %foo, except that the key references are to copies (since the
           keys are just strings rather than full-fledged scalars).

       3.  A reference to an anonymous hash can be created using curly brackets:

               $hashref = {
                   'Adam'  =&gt; 'Eve',
                   'Clyde' =&gt; 'Bonnie',
               };

           Anonymous hash and array composers like these can be intermixed freely to produce as complicated
           a structure as you want.  The multidimensional syntax described below works for these too.  The
           values above are literals, but variables and expressions would work just as well, because assign-<font color="#ffffff" class="whiteout">ment&nbsp;assignment</font>
           ment operators in Perl (even within <u>local()</u> or <u>my()</u>) are executable statements, not compile-time
           declarations.

           Because curly brackets (braces) are used for several other things including BLOCKs, you may occa-<font color="#ffffff" class="whiteout">sionally&nbsp;occasionally</font>
           sionally have to disambiguate braces at the beginning of a statement by putting a "+" or a
           "return" in front so that Perl realizes the opening brace isn't starting a BLOCK.  The economy
           and mnemonic value of using curlies is deemed worth this occasional extra hassle.

           For example, if you wanted a function to make a new hash and return a reference to it, you have
           these options:

               sub hashem {        { @_ } }   # silently wrong
               sub hashem {       +{ @_ } }   # ok
               sub hashem { return { @_ } }   # ok

           On the other hand, if you want the other meaning, you can do this:

               sub showem {        { @_ } }   # ambiguous (currently ok, but may change)
               sub showem {       {; @_ } }   # ok
               sub showem { { return @_ } }   # ok

           The leading "+{" and "{;" always serve to disambiguate the expression to mean either the HASH
           reference, or the BLOCK.

       4.  A reference to an anonymous subroutine can be created by using "sub" without a subname:

               $coderef = sub { print "Boink!\n" };

           Note the semicolon.  Except for the code inside not being immediately executed, a "sub {}" is not
           so much a declaration as it is an operator, like "do{}" or "eval{}".  (However, no matter how
           many times you execute that particular line (unless you're in an "eval("...")"), $coderef will
           still have a reference to the <u>same</u> anonymous subroutine.)

           Anonymous subroutines act as closures with respect to <u>my()</u> variables, that is, variables lexi-<font color="#ffffff" class="whiteout">cally&nbsp;lexically</font>
           cally visible within the current scope.  Closure is a notion out of the Lisp world that says if
           you define an anonymous function in a particular lexical context, it pretends to run in that con-<font color="#ffffff" class="whiteout">text&nbsp;context</font>
           text even when it's called outside the context.

           In human terms, it's a funny way of passing arguments to a subroutine when you define it as well
           as when you call it.  It's useful for setting up little bits of code to run later, such as call-<font color="#ffffff" class="whiteout">backs.&nbsp;callbacks.</font>
           backs.  You can even do object-oriented stuff with it, though Perl already provides a different
           mechanism to do that--see perlobj.

           You might also think of closure as a way to write a subroutine template without using <u>eval()</u>.
           Here's a small example of how closures work:

               sub newprint {
                   my $x = shift;
                   return sub { my $y = shift; print "$x, $y!\n"; };
               }
               $h = newprint("Howdy");
               $g = newprint("Greetings");

               # Time passes...

               &amp;$h("world");
               &amp;$g("earthlings");

           This prints

               Howdy, world!
               Greetings, earthlings!

           Note particularly that $x continues to refer to the value passed into <u>newprint()</u> <u>despite</u> "my $x"
           having gone out of scope by the time the anonymous subroutine runs.  That's what a closure is all
           about.

           This applies only to lexical variables, by the way.  Dynamic variables continue to work as they
           have always worked.  Closure is not something that most Perl programmers need trouble themselves
           about to begin with.

       5.  References are often returned by special subroutines called constructors.  Perl objects are just
           references to a special type of object that happens to know which package it's associated with.
           Constructors are just special subroutines that know how to create that association.  They do so
           by starting with an ordinary reference, and it remains an ordinary reference even while it's also
           being an object.  Constructors are often named <u>new()</u> and called indirectly:

               $objref = new Doggie (Tail =&gt; 'short', Ears =&gt; 'long');

           But don't have to be:

               $objref   = Doggie-&gt;new(Tail =&gt; 'short', Ears =&gt; 'long');

               use Term::Cap;
               $terminal = Term::Cap-&gt;Tgetent( { OSPEED =&gt; 9600 });

               use Tk;
               $main    = MainWindow-&gt;new();
               $menubar = $main-&gt;Frame(-relief              =&gt; "raised",
                                       -borderwidth         =&gt; 2)

       6.  References of the appropriate type can spring into existence if you dereference them in a context
           that assumes they exist.  Because we haven't talked about dereferencing yet, we can't show you
           any examples yet.

       7.  A reference can be created by using a special syntax, lovingly known as the *foo{THING} syntax.
           *foo{THING} returns a reference to the THING slot in *foo (which is the symbol table entry which
           holds everything known as foo).

               $scalarref = *foo{SCALAR};
               $arrayref  = *ARGV{ARRAY};
               $hashref   = *ENV{HASH};
               $coderef   = *handler{CODE};
               $ioref     = *STDIN{IO};
               $globref   = *foo{GLOB};
               $formatref = *foo{FORMAT};

           All of these are self-explanatory except for *foo{IO}.  It returns the IO handle, used for file
           handles ("open" in perlfunc), sockets ("socket" in perlfunc and "socketpair" in perlfunc), and
           directory handles ("opendir" in perlfunc).  For compatibility with previous versions of Perl,
           *foo{FILEHANDLE} is a synonym for *foo{IO}, though it is deprecated as of 5.8.0.  If deprecation
           warnings are in effect, it will warn of its use.

           *foo{THING} returns undef if that particular THING hasn't been used yet, except in the case of
           scalars.  *foo{SCALAR} returns a reference to an anonymous scalar if $foo hasn't been used yet.
           This might change in a future release.

           *foo{IO} is an alternative to the *HANDLE mechanism given in "Typeglobs and Filehandles" in perl-<font color="#ffffff" class="whiteout">data&nbsp;perldata</font>
           data for passing filehandles into or out of subroutines, or storing into larger data structures.
           Its disadvantage is that it won't create a new filehandle for you.  Its advantage is that you
           have less risk of clobbering more than you want to with a typeglob assignment.  (It still con-<font color="#ffffff" class="whiteout">flates&nbsp;conflates</font>
           flates file and directory handles, though.)  However, if you assign the incoming value to a
           scalar instead of a typeglob as we do in the examples below, there's no risk of that happening.

               splutter(*STDOUT);          # pass the whole glob
               splutter(*STDOUT{IO});      # pass both file and dir handles

               sub splutter {
                   my $fh = shift;
                   print $fh "her um well a hmmm\n";
               }

               $rec = get_rec(*STDIN);     # pass the whole glob
               $rec = get_rec(*STDIN{IO}); # pass both file and dir handles

               sub get_rec {
                   my $fh = shift;
                   return scalar &lt;$fh&gt;;
               }

       <b>Using</b> <b>References</b>

       That's it for creating references.  By now you're probably dying to know how to use references to get
       back to your long-lost data.  There are several basic methods.

       1.  Anywhere you'd put an identifier (or chain of identifiers) as part of a variable or subroutine
           name, you can replace the identifier with a simple scalar variable containing a reference of the
           correct type:

               $bar = $$scalarref;
               push(@$arrayref, $filename);
               $$arrayref[0] = "January";
               $$hashref{"KEY"} = "VALUE";
               &amp;$coderef(1,2,3);
               print $globref "output\n";

           It's important to understand that we are specifically <u>not</u> dereferencing $arrayref[0] or
           $hashref{"KEY"} there.  The dereference of the scalar variable happens <u>before</u> it does any key
           lookups.  Anything more complicated than a simple scalar variable must use methods 2 or 3 below.
           However, a "simple scalar" includes an identifier that itself uses method 1 recursively.  There-<font color="#ffffff" class="whiteout">fore,&nbsp;Therefore,</font>
           fore, the following prints "howdy".

               $refrefref = \\\"howdy";
               print $$$$refrefref;

       2.  Anywhere you'd put an identifier (or chain of identifiers) as part of a variable or subroutine
           name, you can replace the identifier with a BLOCK returning a reference of the correct type.  In
           other words, the previous examples could be written like this:

               $bar = ${$scalarref};
               push(@{$arrayref}, $filename);
               ${$arrayref}[0] = "January";
               ${$hashref}{"KEY"} = "VALUE";
               &amp;{$coderef}(1,2,3);
               $globref-&gt;print("output\n");  # iff IO::Handle is loaded

           Admittedly, it's a little silly to use the curlies in this case, but the BLOCK can contain any
           arbitrary expression, in particular, subscripted expressions:

               &amp;{ $dispatch{$index} }(1,2,3);      # call correct routine

           Because of being able to omit the curlies for the simple case of $$x, people often make the mis-<font color="#ffffff" class="whiteout">take&nbsp;mistake</font>
           take of viewing the dereferencing symbols as proper operators, and wonder about their precedence.
           If they were, though, you could use parentheses instead of braces.  That's not the case.  Con-<font color="#ffffff" class="whiteout">sider&nbsp;Consider</font>
           sider the difference below; case 0 is a short-hand version of case 1, <u>not</u> case 2:

               $$hashref{"KEY"}   = "VALUE";       # CASE 0
               ${$hashref}{"KEY"} = "VALUE";       # CASE 1
               ${$hashref{"KEY"}} = "VALUE";       # CASE 2
               ${$hashref-&gt;{"KEY"}} = "VALUE";     # CASE 3

           Case 2 is also deceptive in that you're accessing a variable called %hashref, not dereferencing
           through $hashref to the hash it's presumably referencing.  That would be case 3.

       3.  Subroutine calls and lookups of individual array elements arise often enough that it gets cumber-<font color="#ffffff" class="whiteout">some&nbsp;cumbersome</font>
           some to use method 2.  As a form of syntactic sugar, the examples for method 2 may be written:

               $arrayref-&gt;[0] = "January";   # Array element
               $hashref-&gt;{"KEY"} = "VALUE";  # Hash element
               $coderef-&gt;(1,2,3);            # Subroutine call

           The left side of the arrow can be any expression returning a reference, including a previous
           dereference.  Note that $array[$x] is <u>not</u> the same thing as "$array-&gt;[$x]" here:

               $array[$x]-&gt;{"foo"}-&gt;[0] = "January";

           This is one of the cases we mentioned earlier in which references could spring into existence
           when in an lvalue context.  Before this statement, $array[$x] may have been undefined.  If so,
           it's automatically defined with a hash reference so that we can look up "{"foo"}" in it.  Like-<font color="#ffffff" class="whiteout">wise&nbsp;Likewise</font>
           wise "$array[$x]-&gt;{"foo"}" will automatically get defined with an array reference so that we can
           look up "[0]" in it.  This process is called <u>autovivification</u>.

           One more thing here.  The arrow is optional <u>between</u> brackets subscripts, so you can shrink the
           above down to

               $array[$x]{"foo"}[0] = "January";

           Which, in the degenerate case of using only ordinary arrays, gives you multidimensional arrays
           just like C's:

               $score[$x][$y][$z] += 42;

           Well, okay, not entirely like C's arrays, actually.  C doesn't know how to grow its arrays on
           demand.  Perl does.

       4.  If a reference happens to be a reference to an object, then there are probably methods to access
           the things referred to, and you should probably stick to those methods unless you're in the class
           package that defines the object's methods.  In other words, be nice, and don't violate the
           object's encapsulation without a very good reason.  Perl does not enforce encapsulation.  We are
           not totalitarians here.  We do expect some basic civility though.

       Using a string or number as a reference produces a symbolic reference, as explained above.  Using a
       reference as a number produces an integer representing its storage location in memory.  The only use-<font color="#ffffff" class="whiteout">ful&nbsp;useful</font>
       ful thing to be done with this is to compare two references numerically to see whether they refer to
       the same location.

           if ($ref1 == $ref2) {  # cheap numeric compare of references
               print "refs 1 and 2 refer to the same thing\n";
           }

       Using a reference as a string produces both its referent's type, including any package blessing as
       described in perlobj, as well as the numeric address expressed in hex.  The <u>ref()</u> operator returns
       just the type of thing the reference is pointing to, without the address.  See "ref" in perlfunc for
       details and examples of its use.

       The <u>bless()</u> operator may be used to associate the object a reference points to with a package func-<font color="#ffffff" class="whiteout">tioning&nbsp;functioning</font>
       tioning as an object class.  See perlobj.

       A typeglob may be dereferenced the same way a reference can, because the dereference syntax always
       indicates the type of reference desired.  So "${*foo}" and "${\$foo}" both indicate the same scalar
       variable.

       Here's a trick for interpolating a subroutine call into a string:

           print "My sub returned @{[mysub(1,2,3)]} that time.\n";

       The way it works is that when the "@{...}" is seen in the double-quoted string, it's evaluated as a
       block.  The block creates a reference to an anonymous array containing the results of the call to
       "mysub(1,2,3)".  So the whole block returns a reference to an array, which is then dereferenced by
       "@{...}" and stuck into the double-quoted string. This chicanery is also useful for arbitrary expres-<font color="#ffffff" class="whiteout">sions:&nbsp;expressions:</font>
       sions:

           print "That yields @{[$n + 5]} widgets\n";

       <b>Symbolic</b> <b>references</b>

       We said that references spring into existence as necessary if they are undefined, but we didn't say
       what happens if a value used as a reference is already defined, but <u>isn't</u> a hard reference.  If you
       use it as a reference, it'll be treated as a symbolic reference.  That is, the value of the scalar is
       taken to be the <u>name</u> of a variable, rather than a direct link to a (possibly) anonymous value.

       People frequently expect it to work like this.  So it does.

           $name = "foo";
           $$name = 1;                 # Sets $foo
           ${$name} = 2;               # Sets $foo
           ${$name x 2} = 3;           # Sets $foofoo
           $name-&gt;[0] = 4;             # Sets $foo[0]
           @$name = ();                # Clears @foo
           &amp;$name();                   # Calls &amp;foo() (as in Perl 4)
           $pack = "THAT";
           ${"${pack}::$name"} = 5;    # Sets $THAT::foo without eval

       This is powerful, and slightly dangerous, in that it's possible to intend (with the utmost sincerity)
       to use a hard reference, and accidentally use a symbolic reference instead.  To protect against that,
       you can say

           use strict 'refs';

       and then only hard references will be allowed for the rest of the enclosing block.  An inner block
       may countermand that with

           no strict 'refs';

       Only package variables (globals, even if localized) are visible to symbolic references.  Lexical
       variables (declared with <u>my()</u>) aren't in a symbol table, and thus are invisible to this mechanism.
       For example:

           local $value = 10;
           $ref = "value";
           {
               my $value = 20;
               print $$ref;
           }

       This will still print 10, not 20.  Remember that <u>local()</u> affects package variables, which are all
       "global" to the package.

       <b>Not-so-symbolic</b> <b>references</b>

       A new feature contributing to readability in perl version 5.001 is that the brackets around a sym-<font color="#ffffff" class="whiteout">bolic&nbsp;symbolic</font>
       bolic reference behave more like quotes, just as they always have within a string.  That is,

           $push = "pop on ";
           print "${push}over";

       has always meant to print "pop on over", even though push is a reserved word.  This has been general-<font color="#ffffff" class="whiteout">ized&nbsp;generalized</font>
       ized to work the same outside of quotes, so that

           print ${push} . "over";

       and even

           print ${ push } . "over";

       will have the same effect.  (This would have been a syntax error in Perl 5.000, though Perl 4 allowed
       it in the spaceless form.)  This construct is <u>not</u> considered to be a symbolic reference when you're
       using strict refs:

           use strict 'refs';
           ${ bareword };      # Okay, means $bareword.
           ${ "bareword" };    # Error, symbolic reference.

       Similarly, because of all the subscripting that is done using single words, we've applied the same
       rule to any bareword that is used for subscripting a hash.  So now, instead of writing

           $array{ "aaa" }{ "bbb" }{ "ccc" }

       you can write just

           $array{ aaa }{ bbb }{ ccc }

       and not worry about whether the subscripts are reserved words.  In the rare event that you do wish to
       do something like

           $array{ shift }

       you can force interpretation as a reserved word by adding anything that makes it more than a bare-<font color="#ffffff" class="whiteout">word:&nbsp;bareword:</font>
       word:

           $array{ shift() }
           $array{ +shift }
           $array{ shift @_ }

       The "use warnings" pragma or the <b>-w</b> switch will warn you if it interprets a reserved word as a
       string.  But it will no longer warn you about using lowercase words, because the string is effec-<font color="#ffffff" class="whiteout">tively&nbsp;effectively</font>
       tively quoted.

       <b>Pseudo-hashes:</b> <b>Using</b> <b>an</b> <b>array</b> <b>as</b> <b>a</b> <b>hash</b>

       <b>WARNING</b>:  This section describes an experimental feature.  Details may change without notice in
       future versions.

       <b>NOTE</b>: The current user-visible implementation of pseudo-hashes (the weird use of the first array ele-<font color="#ffffff" class="whiteout">ment)&nbsp;element)</font>
       ment) is deprecated starting from Perl 5.8.0 and will be removed in Perl 5.10.0, and the feature will
       be implemented differently.  Not only is the current interface rather ugly, but the current implemen-<font color="#ffffff" class="whiteout">tation&nbsp;implementation</font>
       tation slows down normal array and hash use quite noticeably.  The 'fields' pragma interface will
       remain available.

       Beginning with release 5.005 of Perl, you may use an array reference in some contexts that would nor-<font color="#ffffff" class="whiteout">mally&nbsp;normally</font>
       mally require a hash reference.  This allows you to access array elements using symbolic names, as if
       they were fields in a structure.

       For this to work, the array must contain extra information.  The first element of the array has to be
       a hash reference that maps field names to array indices.  Here is an example:

           $struct = [{foo =&gt; 1, bar =&gt; 2}, "FOO", "BAR"];

           $struct-&gt;{foo};  # same as $struct-&gt;[1], i.e. "FOO"
           $struct-&gt;{bar};  # same as $struct-&gt;[2], i.e. "BAR"

           keys %$struct;   # will return ("foo", "bar") in some order
           values %$struct; # will return ("FOO", "BAR") in same some order

           while (my($k,$v) = each %$struct) {
              print "$k =&gt; $v\n";
           }

       Perl will raise an exception if you try to access nonexistent fields.  To avoid inconsistencies,
       always use the <u>fields::phash()</u> function provided by the "fields" pragma.

           use fields;
           $pseudohash = fields::phash(foo =&gt; "FOO", bar =&gt; "BAR");

       For better performance, Perl can also do the translation from field names to array indices at compile
       time for typed object references.  See fields.

       There are two ways to check for the existence of a key in a pseudo-hash.  The first is to use
       <u>exists()</u>.  This checks to see if the given field has ever been set.  It acts this way to match the
       behavior of a regular hash.  For instance:

           use fields;
           $phash = fields::phash([qw(foo bar pants)], ['FOO']);
           $phash-&gt;{pants} = undef;

           print exists $phash-&gt;{foo};    # true, 'foo' was set in the declaration
           print exists $phash-&gt;{bar};    # false, 'bar' has not been used.
           print exists $phash-&gt;{pants};  # true, your 'pants' have been touched

       The second is to use <u>exists()</u> on the hash reference sitting in the first array element.  This checks
       to see if the given key is a valid field in the pseudo-hash.

           print exists $phash-&gt;[0]{bar};      # true, 'bar' is a valid field
           print exists $phash-&gt;[0]{shoes};# false, 'shoes' can't be used

       <u>delete()</u> on a pseudo-hash element only deletes the value corresponding to the key, not the key
       itself.  To delete the key, you'll have to explicitly delete it from the first hash element.

           print delete $phash-&gt;{foo};     # prints $phash-&gt;[1], "FOO"
           print exists $phash-&gt;{foo};     # false
           print exists $phash-&gt;[0]{foo};  # true, key still exists
           print delete $phash-&gt;[0]{foo};  # now key is gone
           print $phash-&gt;{foo};            # runtime exception

       <b>Function</b> <b>Templates</b>

       As explained above, an anonymous function with access to the lexical variables visible when that
       function was compiled, creates a closure.  It retains access to those variables even though it
       doesn't get run until later, such as in a signal handler or a Tk callback.

       Using a closure as a function template allows us to generate many functions that act similarly.  Sup-<font color="#ffffff" class="whiteout">pose&nbsp;Suppose</font>
       pose you wanted functions named after the colors that generated HTML font changes for the various
       colors:

           print "Be ", red("careful"), "with that ", green("light");

       The <u>red()</u> and <u>green()</u> functions would be similar.  To create these, we'll assign a closure to a type-<font color="#ffffff" class="whiteout">glob&nbsp;typeglob</font>
       glob of the name of the function we're trying to build.

           @colors = qw(red blue green yellow orange purple violet);
           for my $name (@colors) {
               no strict 'refs';       # allow symbol table manipulation
               *$name = *{uc $name} = sub { "&lt;FONT COLOR='$name'&gt;@_&lt;/FONT&gt;" };
           }

       Now all those different functions appear to exist independently.  You can call <u>red()</u>, <u>RED()</u>, <u>blue()</u>,
       <u>BLUE()</u>, <u>green()</u>, etc.  This technique saves on both compile time and memory use, and is less error-<font color="#ffffff" class="whiteout">prone&nbsp;errorprone</font>
       prone as well, since syntax checks happen at compile time.  It's critical that any variables in the
       anonymous subroutine be lexicals in order to create a proper closure.  That's the reasons for the
       "my" on the loop iteration variable.

       This is one of the only places where giving a prototype to a closure makes much sense.  If you wanted
       to impose scalar context on the arguments of these functions (probably not a wise idea for this par-<font color="#ffffff" class="whiteout">ticular&nbsp;particular</font>
       ticular example), you could have written it this way instead:

           *$name = sub ($) { "&lt;FONT COLOR='$name'&gt;$_[0]&lt;/FONT&gt;" };

       However, since prototype checking happens at compile time, the assignment above happens too late to
       be of much use.  You could address this by putting the whole loop of assignments within a BEGIN
       block, forcing it to occur during compilation.

       Access to lexicals that change over type--like those in the "for" loop above--only works with clo-<font color="#ffffff" class="whiteout">sures,&nbsp;closures,</font>
       sures, not general subroutines.  In the general case, then, named subroutines do not nest properly,
       although anonymous ones do. Thus is because named subroutines are created (and capture any outer lex-<font color="#ffffff" class="whiteout">icals)&nbsp;lexicals)</font>
       icals) only once at compile time, whereas anonymous subroutines get to capture each time you execute
       the 'sub' operator.  If you are accustomed to using nested subroutines in other programming languages
       with their own private variables, you'll have to work at it a bit in Perl.  The intuitive coding of
       this type of thing incurs mysterious warnings about "will not stay shared".  For example, this won't
       work:

           sub outer {
               my $x = $_[0] + 35;
               sub inner { return $x * 19 }   # WRONG
               return $x + inner();
           }

       A work-around is the following:

           sub outer {
               my $x = $_[0] + 35;
               local *inner = sub { return $x * 19 };
               return $x + inner();
           }

       Now <u>inner()</u> can only be called from within <u>outer()</u>, because of the temporary assignments of the clo-<font color="#ffffff" class="whiteout">sure&nbsp;closure</font>
       sure (anonymous subroutine).  But when it does, it has normal access to the lexical variable $x from
       the scope of <u>outer()</u>.

       This has the interesting effect of creating a function local to another function, something not nor-<font color="#ffffff" class="whiteout">mally&nbsp;normally</font>
       mally supported in Perl.

<b>WARNING</b>
       You may not (usefully) use a reference as the key to a hash.  It will be converted into a string:

           $x{ \$a } = $a;

       If you try to dereference the key, it won't do a hard dereference, and you won't accomplish what
       you're attempting.  You might want to do something more like

           $r = \@a;
           $x{ $r } = $r;

       And then at least you can use the <u>values()</u>, which will be real refs, instead of the <u>keys()</u>, which
       won't.

       The standard Tie::RefHash module provides a convenient workaround to this.

<b>SEE</b> <b>ALSO</b>
       Besides the obvious documents, source code can be instructive.  Some pathological examples of the use
       of references can be found in the <u>t/op/ref.t</u> regression test in the Perl source directory.

       See also perldsc and perllol for how to use references to create complex data structures, and perl-<font color="#ffffff" class="whiteout">toot,&nbsp;perltoot,</font>
       toot, perlobj, and perlbot for how to use them to create objects.



perl v5.8.8                                      2006-01-07                                       PERLREF(1)
</tt></pre>
</td></tr></table>
	<table width="100%" border="0" cellpadding="0" cellspacing="0">
		<tr>
			<td><div style="width: 100%; height: 1px; background-color: #919699; margin-top: 5px; margin-bottom: 15px"></div></td>
		</tr>
		<tr>
			<td align="center"><br/>
				<table border="0" cellpadding="0" cellspacing="0" class="graybox">
					<tr>
						<th>Did this document help you?</th>
					</tr>
					<tr>
						<td>
						    <div style="margin-bottom: 8px"><a href="http://developer.apple.com/feedback/?v=1&url=/documentation/Darwin/Reference/ManPages/man1/perlref.1.html%3Fid%3DTP40000894-7.0&media=dvd" target=_new>Yes</a>:  Tell us what works for you.</div>
							<div style="margin-bottom: 8px"><a href="http://developer.apple.com/feedback/?v=2&url=/documentation/Darwin/Reference/ManPages/man1/perlref.1.html%3Fid%3DTP40000894-7.0&media=dvd" target=_new>It&#8217;s good, but:</a> Report typos, inaccuracies, and so forth.</div>
							<div><a href="http://developer.apple.com/feedback/?v=3&url=/documentation/Darwin/Reference/ManPages/man1/perlref.1.html%3Fid%3DTP40000894-7.0&media=dvd" target=_new>It wasn&#8217;t helpful</a>: Tell us what would have helped.</div>
						</td>
					</tr>
				</table>
			</td>
		</tr>
	</table>

</body></html>
