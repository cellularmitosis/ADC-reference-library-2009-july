<html><head><title>Mac OS X
 Manual Page For perlfaq5(1)</title>
<style type="text/css"><!-- @import "../../../../adcstyle.css"; .char {background: #ffffff; color:#0000ff;}.comment {background:#ffffff; color:#236e25}.function {background:#ffffff; color:#000000;}.keyword {background:#ffffff; color:#761550;}.number {background: #ffffff; color:#0000ff;}.param {background:#ffffff; color:#000000;}.preprocessor {background:#ffffff; color:#236e25}.string {background: #ffffff; color:#891315;}.template {background:#ffffff; color:#761550;}.type {background:#ffffff; color:#761550;}.var {background:#ffffff; color:#000000;} pre.manpages {font-size: 10pt;} .graybox { border-top: 1px solid #919699; border-left: 1px solid #919699; margin-bottom: 10px; } .graybox th { padding: 4px 8px 4px 8px; background: #E2E2E2; font-size: 12px; font-weight: bold; border-bottom: 1px solid #919699; border-right: 1px solid #919699; } .graybox td { padding: 8px; font-size: 12px; text-align: left; vertical-align: top; border-bottom: 1px solid #919699; border-right: 1px solid #919699; } .whiteout { font-size: 1px; background: #ffffff; color: #ffffff; } --></style>
<style type="text/css" media="print, aural, braille, embossed, projection, tty"><!-- @import "../../../../adcstyle.css"; .char {background: #ffffff; color:#0000ff;}.comment {background:#ffffff; color:#236e25}.function {background:#ffffff; color:#000000;}.keyword {background:#ffffff; color:#761550;}.number {background: #ffffff; color:#0000ff;}.param {background:#ffffff; color:#000000;}.preprocessor {background:#ffffff; color:#236e25}.string {background: #ffffff; color:#891315;}.template {background:#ffffff; color:#761550;}.type {background:#ffffff; color:#761550;}.var {background:#ffffff; color:#000000;} pre.manpages {font-size: 10pt;} .graybox { border-top: 1px solid #919699; border-left: 1px solid #919699; margin-bottom: 10px; } .graybox th { padding: 4px 8px 4px 8px; background: #E2E2E2; font-size: 12px; font-weight: bold; border-bottom: 1px solid #919699; border-right: 1px solid #919699; } .graybox td { padding: 8px; font-size: 12px; text-align: left; vertical-align: top; border-bottom: 1px solid #919699; border-right: 1px solid #919699; } .whiteout { display: none; } --></style>
</head><body bgcolor="white">
<a name="//apple_ref/doc/man/1/perlfaq5" title="Mac OS X
 Manual Page for perlfaq5(1)"><!-- headerDoc=man; indexgroup=Section 1; uid="//apple_ref/doc/man/1/perlfaq5"; name=perlfaq5(1) --></a>
<!--#include virtual="/includes/framesetheader"-->
<table width=600><tr><td scope="row">
<!-- begin breadcrumb insert request --><font face="Geneva,Helvetica,Arial" size="1">
 <a target="_top" href="http://developer.apple.com">ADC Home</a> <b>&gt;</b> <a href="../../../../../referencelibrary/index.html#//apple_ref/doc/uid/TP30000943" target="_top">Reference Library</a> <b>&gt;</b> <a href="../../../../../reference/index.html#//apple_ref/doc/uid/TP30001281" target="_top">Reference</a> <b>&gt;</b> <!-- a href="" target="_top" -->Mac OS X<!-- /a --> <b>&gt;</b> <a href="../index.html#//apple_ref/doc/framework/manpages" target="_top">Mac OS X Man Pages</a></font>
<!-- end breadcrumb insert request -->
<p>&nbsp;</p>
</td></tr></table>
<!-- end of header -->
<table border="0"  cellpadding="2" cellspacing="2" width="630pt"><tr><td valign="top" height="12" colspan="5" scope="row">
<p style='text-align:justify;'>This document is a Mac OS X manual page.  Manual pages are a command-line technology
for providing documentation.  You can view these manual pages locally using the
<a href="man.1.html#//apple_ref/doc/man/1/man">man(1)</a> command.
These manual pages come from many different sources, and thus, have a variety of writing
styles.</p>
<p style='text-align:justify;'>For more information about the manual page format, see the manual page for 
<a href="../man5/manpages.5.html#//apple_ref/doc/man/5/manpages">manpages(5)</a>.</p>
</td></tr></table>
<hr />
<table border="0"  cellpadding="2" cellspacing="2" width="300"><tr><td valign="top" height="12" colspan="5" scope="row"><pre class="manpages"><tt>
PERLFAQ5(1)                           Perl Programmers Reference Guide                           PERLFAQ5(1)



<b>NAME</b>
       perlfaq5 - Files and Formats ($Revision: 1.42 $, $Date: 2005/12/31 00:54:37 $)

<b>DESCRIPTION</b>
       This section deals with I/O and the "f" issues: filehandles, flushing, formats, and footers.

       <b>How</b> <b>do</b> <b>I</b> <b>flush/unbuffer</b> <b>an</b> <b>output</b> <b>filehandle?</b>  <b>Why</b> <b>must</b> <b>I</b> <b>do</b> <b>this?</b>

       Perl does not support truly unbuffered output (except insofar as you can "syswrite(OUT, $char, 1)"),
       although it does support is "command buffering", in which a physical write is performed after every
       output command.

       The C standard I/O library (stdio) normally buffers characters sent to devices so that there isn't a
       system call for each byte. In most stdio implementations, the type of output buffering and the size
       of the buffer varies according to the type of device. Perl's <u>print()</u> and <u>write()</u> functions normally
       buffer output, while <u>syswrite()</u> bypasses buffering all together.

       If you want your output to be sent immediately when you execute <u>print()</u> or <u>write()</u> (for instance, for
       some network protocols), you must set the handle's autoflush flag. This flag is the Perl variable $|
       and when it is set to a true value, Perl will flush the handle's buffer after each <u>print()</u> or
       <u>write()</u>. Setting $| affects buffering only for the currently selected default file handle. You choose
       this handle with the one argument <u>select()</u> call (see "$|" in perlvar and "select" in perlfunc).

       Use <u>select()</u> to choose the desired handle, then set its per-filehandle variables.

           $old_fh = select(OUTPUT_HANDLE);
           $| = 1;
           select($old_fh);

       Some idioms can handle this in a single statement:

           select((select(OUTPUT_HANDLE), $| = 1)[0]);

           $| = 1, select $_ for select OUTPUT_HANDLE;

       Some modules offer object-oriented access to handles and their variables, although they may be
       overkill if this is the only thing you do with them.  You can use IO::Handle:

           use IO::Handle;
           open(DEV, "&gt;/dev/printer");   # but is this?
           DEV-&gt;autoflush(1);

       or IO::Socket:

           use IO::Socket;               # this one is kinda a pipe?
               my $sock = IO::Socket::INET-&gt;new( 'www.example.com:80' );

           $sock-&gt;autoflush();

       <b>How</b> <b>do</b> <b>I</b> <b>change</b> <b>one</b> <b>line</b> <b>in</b> <b>a</b> <b>file/delete</b> <b>a</b> <b>line</b> <b>in</b> <b>a</b> <b>file/insert</b> <b>a</b> <b>line</b> <b>in</b> <b>the</b> <b>middle</b> <b>of</b> <b>a</b>
       <b>file/append</b> <b>to</b> <b>the</b> <b>beginning</b> <b>of</b> <b>a</b> <b>file?</b>

       Use the Tie::File module, which is included in the standard distribution since Perl 5.8.0.

       <b>How</b> <b>do</b> <b>I</b> <b>count</b> <b>the</b> <b>number</b> <b>of</b> <b>lines</b> <b>in</b> <b>a</b> <b>file?</b>

       One fairly efficient way is to count newlines in the file. The following program uses a feature of
       tr///, as documented in perlop.  If your text file doesn't end with a newline, then it's not really a
       proper text file, so this may report one fewer line than you expect.

           $lines = 0;
           open(FILE, $filename) or die "Can't open `$filename': $!";
           while (sysread FILE, $buffer, 4096) {
               $lines += ($buffer =~ tr/\n//);
           }
           close FILE;

       This assumes no funny games with newline translations.

       <b>How</b> <b>can</b> <b>I</b> <b>use</b> <b>Perl's</b> <b>"-i"</b> <b>option</b> <b>from</b> <b>within</b> <b>a</b> <b>program?</b>

       "-i" sets the value of Perl's $^I variable, which in turn affects the behavior of "&lt;&gt;"; see perlrun
       for more details.  By modifying the appropriate variables directly, you can get the same behavior
       within a larger program.  For example:

            # ...
            {
               local($^I, @ARGV) = ('.orig', glob("*.c"));
               while (&lt;&gt;) {
                  if ($. == 1) {
                      print "This line should appear at the top of each file\n";
                  }
                  s/\b(p)earl\b/${1}erl/i;        # Correct typos, preserving case
                  print;
                  close ARGV if eof;              # Reset $.
               }
            }
            # $^I and @ARGV return to their old values here

       This block modifies all the ".c" files in the current directory, leaving a backup of the original
       data from each file in a new ".c.orig" file.

       <b>How</b> <b>can</b> <b>I</b> <b>copy</b> <b>a</b> <b>file?</b>

       (contributed by brian d foy)

       Use the File::Copy module. It comes with Perl and can do a true copy across file systems, and it does
       its magic in a portable fashion.

               use File::Copy;

               copy( $original, $new_copy ) or die "Copy failed: $!";

       If you can't use File::Copy, you'll have to do the work yourself: open the original file, open the
       destination file, then print to the destination file as you read the original.

       <b>How</b> <b>do</b> <b>I</b> <b>make</b> <b>a</b> <b>temporary</b> <b>file</b> <b>name?</b>

       If you don't need to know the name of the file, you can use "open()" with "undef" in place of the
       file name.  The "open()" function creates an anonymous temporary file.

               open my $tmp, '+&gt;', undef or die $!;

       Otherwise, you can use the File::Temp module.

         use File::Temp qw/ tempfile tempdir /;

         $dir = tempdir( CLEANUP =&gt; 1 );
         ($fh, $filename) = tempfile( DIR =&gt; $dir );

         # or if you don't need to know the filename

         $fh = tempfile( DIR =&gt; $dir );

       The File::Temp has been a standard module since Perl 5.6.1.  If you don't have a modern enough Perl
       installed, use the "new_tmpfile" class method from the IO::File module to get a filehandle opened for
       reading and writing.  Use it if you don't need to know the file's name:

           use IO::File;
           $fh = IO::File-&gt;new_tmpfile()
               or die "Unable to make new temporary file: $!";

       If you're committed to creating a temporary file by hand, use the process ID and/or the current
       time-value.  If you need to have many temporary files in one process, use a counter:

           BEGIN {
               use Fcntl;
               my $temp_dir = -d '/tmp' ? '/tmp' : $ENV{TMPDIR} || $ENV{TEMP};
               my $base_name = sprintf("%s/%d-%d-0000", $temp_dir, $$, time());
               sub temp_file {
                   local *FH;
                   my $count = 0;
                   until (defined(fileno(FH)) || $count++ &gt; 100) {
                       $base_name =~ s/-(\d+)$/"-" . (1 + $1)/e;
                       # O_EXCL is required for security reasons.
                       sysopen(FH, $base_name, O_WRONLY|O_EXCL|O_CREAT);
                   }
                   if (defined(fileno(FH))
                       return (*FH, $base_name);
                   } else {
                       return ();
                   }
               }
           }

       <b>How</b> <b>can</b> <b>I</b> <b>manipulate</b> <b>fixed-record-length</b> <b>files?</b>

       The most efficient way is using <u>pack()</u> and <u>unpack()</u>.  This is faster than using <u>substr()</u> when taking
       many, many strings.  It is slower for just a few.

       Here is a sample chunk of code to break up and put back together again some fixed-format input lines,
       in this case from the output of a normal, Berkeley-style ps:

           # sample input line:
           #   15158 p5  T      0:00 perl /home/tchrist/scripts/now-what
           my $PS_T = 'A6 A4 A7 A5 A*';
           open my $ps, '-|', 'ps';
           print scalar &lt;$ps&gt;;
           my @fields = qw( pid tt stat time command );
           while (&lt;$ps&gt;) {
               my %process;
               @process{@fields} = unpack($PS_T, $_);
               for my $field ( @fields ) {
                   print "$field: &lt;$process{$field}&gt;\n";
               }
               print 'line=', pack($PS_T, @process{@fields} ), "\n";
           }

       We've used a hash slice in order to easily handle the fields of each row.  Storing the keys in an
       array means it's easy to operate on them as a group or loop over them with for. It also avoids pol-<font color="#ffffff" class="whiteout">luting&nbsp;polluting</font>
       luting the program with global variables and using symbolic references.

       <b>How</b> <b>can</b> <b>I</b> <b>make</b> <b>a</b> <b>filehandle</b> <b>local</b> <b>to</b> <b>a</b> <b>subroutine?</b>  <b>How</b> <b>do</b> <b>I</b> <b>pass</b> <b>filehandles</b> <b>between</b> <b>subroutines?</b>
       <b>How</b> <b>do</b> <b>I</b> <b>make</b> <b>an</b> <b>array</b> <b>of</b> <b>filehandles?</b>

       As of perl5.6, <u>open()</u> autovivifies file and directory handles as references if you pass it an unini-<font color="#ffffff" class="whiteout">tialized&nbsp;uninitialized</font>
       tialized scalar variable.  You can then pass these references just like any other scalar, and use
       them in the place of named handles.

               open my    $fh, $file_name;

               open local $fh, $file_name;

               print $fh "Hello World!\n";

               process_file( $fh );

       Before perl5.6, you had to deal with various typeglob idioms which you may see in older code.

               open FILE, "&gt; $filename";
               process_typeglob(   *FILE );
               process_reference( \*FILE );

               sub process_typeglob  { local *FH = shift; print FH  "Typeglob!" }
               sub process_reference { local $fh = shift; print $fh "Reference!" }

       If you want to create many anonymous handles, you should check out the Symbol or IO::Handle modules.

       <b>How</b> <b>can</b> <b>I</b> <b>use</b> <b>a</b> <b>filehandle</b> <b>indirectly?</b>

       An indirect filehandle is using something other than a symbol in a place that a filehandle is
       expected.  Here are ways to get indirect filehandles:

           $fh =   SOME_FH;       # bareword is strict-subs hostile
           $fh =  "SOME_FH";      # strict-refs hostile; same package only
           $fh =  *SOME_FH;       # typeglob
           $fh = \*SOME_FH;       # ref to typeglob (bless-able)
           $fh =  *SOME_FH{IO};   # blessed IO::Handle from *SOME_FH typeglob

       Or, you can use the "new" method from one of the IO::* modules to create an anonymous filehandle,
       store that in a scalar variable, and use it as though it were a normal filehandle.

           use IO::Handle;                     # 5.004 or higher
           $fh = IO::Handle-&gt;new();

       Then use any of those as you would a normal filehandle.  Anywhere that Perl is expecting a filehan-<font color="#ffffff" class="whiteout">dle,&nbsp;filehandle,</font>
       dle, an indirect filehandle may be used instead. An indirect filehandle is just a scalar variable
       that contains a filehandle.  Functions like "print", "open", "seek", or the "&lt;FH&gt;" diamond operator
       will accept either a named filehandle or a scalar variable containing one:

           ($ifh, $ofh, $efh) = (*STDIN, *STDOUT, *STDERR);
           print $ofh "Type it: ";
           $got = &lt;$ifh&gt;
           print $efh "What was that: $got";

       If you're passing a filehandle to a function, you can write the function in two ways:

           sub accept_fh {
               my $fh = shift;
               print $fh "Sending to indirect filehandle\n";
           }

       Or it can localize a typeglob and use the filehandle directly:

           sub accept_fh {
               local *FH = shift;
               print  FH "Sending to localized filehandle\n";
           }

       Both styles work with either objects or typeglobs of real filehandles.  (They might also work with
       strings under some circumstances, but this is risky.)

           accept_fh(*STDOUT);
           accept_fh($handle);

       In the examples above, we assigned the filehandle to a scalar variable before using it.  That is
       because only simple scalar variables, not expressions or subscripts of hashes or arrays, can be used
       with built-ins like "print", "printf", or the diamond operator.  Using something other than a simple
       scalar variable as a filehandle is illegal and won't even compile:

           @fd = (*STDIN, *STDOUT, *STDERR);
           print $fd[1] "Type it: ";                           # WRONG
           $got = &lt;$fd[0]&gt;                                     # WRONG
           print $fd[2] "What was that: $got";                 # WRONG

       With "print" and "printf", you get around this by using a block and an expression where you would
       place the filehandle:

           print  { $fd[1] } "funny stuff\n";
           printf { $fd[1] } "Pity the poor %x.\n", 3_735_928_559;
           # Pity the poor deadbeef.

       That block is a proper block like any other, so you can put more complicated code there.  This sends
       the message out to one of two places:

           $ok = -x "/bin/cat";
           print { $ok ? $fd[1] : $fd[2] } "cat stat $ok\n";
           print { $fd[ 1+ ($ok || 0) ]  } "cat stat $ok\n";

       This approach of treating "print" and "printf" like object methods calls doesn't work for the diamond
       operator.  That's because it's a real operator, not just a function with a comma-less argument.
       Assuming you've been storing typeglobs in your structure as we did above, you can use the built-in
       function named "readline" to read a record just as "&lt;&gt;" does.  Given the initialization shown above
       for @fd, this would work, but only because <u>readline()</u> requires a typeglob.  It doesn't work with
       objects or strings, which might be a bug we haven't fixed yet.

           $got = readline($fd[0]);

       Let it be noted that the flakiness of indirect filehandles is not related to whether they're strings,
       typeglobs, objects, or anything else.  It's the syntax of the fundamental operators.  Playing the
       object game doesn't help you at all here.

       <b>How</b> <b>can</b> <b>I</b> <b>set</b> <b>up</b> <b>a</b> <b>footer</b> <b>format</b> <b>to</b> <b>be</b> <b>used</b> <b>with</b> <u>write()</u><b>?</b>

       There's no builtin way to do this, but perlform has a couple of techniques to make it possible for
       the intrepid hacker.

       <b>How</b> <b>can</b> <b>I</b> <u>write()</u> <b>into</b> <b>a</b> <b>string?</b>

       See "Accessing Formatting Internals" in perlform for an <u>swrite()</u> function.

       <b>How</b> <b>can</b> <b>I</b> <b>output</b> <b>my</b> <b>numbers</b> <b>with</b> <b>commas</b> <b>added?</b>

       (contributed by brian d foy and Benjamin Goldberg)

       You can use Number::Format to separate places in a number.  It handles locale information for those
       of you who want to insert full stops instead (or anything else that they want to use, really).

       This subroutine will add commas to your number:

               sub commify {
                  local $_  = shift;
                  1 while s/^([-+]?\d+)(\d{3})/$1,$2/;
                  return $_;
                  }

       This regex from Benjamin Goldberg will add commas to numbers:

          s/(^[-+]?\d+?(?=(?&gt;(?:\d{3})+)(?!\d))|\G\d{3}(?=\d))/$1,/g;

       It is easier to see with comments:

          s/(
              ^[-+]?            # beginning of number.
              \d+?              # first digits before first comma
              (?=               # followed by, (but not included in the match) :
                 (?&gt;(?:\d{3})+) # some positive multiple of three digits.
                 (?!\d)         # an *exact* multiple, not x * 3 + 1 or whatever.
              )
             |                  # or:
              \G\d{3}           # after the last group, get three digits
              (?=\d)            # but they have to have more digits after them.
          )/$1,/xg;

       <b>How</b> <b>can</b> <b>I</b> <b>translate</b> <b>tildes</b> <b>(~)</b> <b>in</b> <b>a</b> <b>filename?</b>

       Use the &lt;&gt; (<u>glob()</u>) operator, documented in perlfunc.  Older versions of Perl require that you have a
       shell installed that groks tildes.  Recent perl versions have this feature built in. The File::KGlob
       module (available from CPAN) gives more portable glob functionality.

       Within Perl, you may use this directly:

               $filename =~ s{
                 ^ ~             # find a leading tilde
                 (               # save this in $1
                     [^/]        # a non-slash character
                           *     # repeated 0 or more times (0 means me)
                 )
               }{
                 $1
                     ? (getpwnam($1))[7]
                     : ( $ENV{HOME} || $ENV{LOGDIR} )
               }ex;

       <b>How</b> <b>come</b> <b>when</b> <b>I</b> <b>open</b> <b>a</b> <b>file</b> <b>read-write</b> <b>it</b> <b>wipes</b> <b>it</b> <b>out?</b>

       Because you're using something like this, which truncates the file and <u>then</u> gives you read-write
       access:

           open(FH, "+&gt; /path/name");          # WRONG (almost always)

       Whoops.  You should instead use this, which will fail if the file doesn't exist.

           open(FH, "+&lt; /path/name");          # open for update

       Using "&gt;" always clobbers or creates.  Using "&lt;" never does either.  The "+" doesn't change this.

       Here are examples of many kinds of file opens.  Those using <u>sysopen()</u> all assume

           use Fcntl;

       To open file for reading:

           open(FH, "&lt; $path")                                 || die $!;
           sysopen(FH, $path, O_RDONLY)                        || die $!;

       To open file for writing, create new file if needed or else truncate old file:

           open(FH, "&gt; $path") || die $!;
           sysopen(FH, $path, O_WRONLY|O_TRUNC|O_CREAT)        || die $!;
           sysopen(FH, $path, O_WRONLY|O_TRUNC|O_CREAT, 0666)  || die $!;

       To open file for writing, create new file, file must not exist:

           sysopen(FH, $path, O_WRONLY|O_EXCL|O_CREAT)         || die $!;
           sysopen(FH, $path, O_WRONLY|O_EXCL|O_CREAT, 0666)   || die $!;

       To open file for appending, create if necessary:

           open(FH, "&gt;&gt; $path") || die $!;
           sysopen(FH, $path, O_WRONLY|O_APPEND|O_CREAT)       || die $!;
           sysopen(FH, $path, O_WRONLY|O_APPEND|O_CREAT, 0666) || die $!;

       To open file for appending, file must exist:

           sysopen(FH, $path, O_WRONLY|O_APPEND)               || die $!;

       To open file for update, file must exist:

           open(FH, "+&lt; $path")                                || die $!;
           sysopen(FH, $path, O_RDWR)                          || die $!;

       To open file for update, create file if necessary:

           sysopen(FH, $path, O_RDWR|O_CREAT)                  || die $!;
           sysopen(FH, $path, O_RDWR|O_CREAT, 0666)            || die $!;

       To open file for update, file must not exist:

           sysopen(FH, $path, O_RDWR|O_EXCL|O_CREAT)           || die $!;
           sysopen(FH, $path, O_RDWR|O_EXCL|O_CREAT, 0666)     || die $!;

       To open a file without blocking, creating if necessary:

           sysopen(FH, "/foo/somefile", O_WRONLY|O_NDELAY|O_CREAT)
                   or die "can't open /foo/somefile: $!":

       Be warned that neither creation nor deletion of files is guaranteed to be an atomic operation over
       NFS.  That is, two processes might both successfully create or unlink the same file!  Therefore
       O_EXCL isn't as exclusive as you might wish.

       See also the new perlopentut if you have it (new for 5.6).

       <b>Why</b> <b>do</b> <b>I</b> <b>sometimes</b> <b>get</b> <b>an</b> <b>"Argument</b> <b>list</b> <b>too</b> <b>long"</b> <b>when</b> <b>I</b> <b>use</b> <b>&lt;*&gt;?</b>

       The "&lt;&gt;" operator performs a globbing operation (see above).  In Perl versions earlier than v5.6.0,
       the internal <u>glob()</u> operator forks <a href="csh.1.html#//apple_ref/doc/man/1/csh"><u>csh</u>(1)</a> to do the actual glob expansion, but csh can't handle more
       than 127 items and so gives the error message "Argument list too long".  People who installed tcsh as
       csh won't have this problem, but their users may be surprised by it.

       To get around this, either upgrade to Perl v5.6.0 or later, do the glob yourself with <u>readdir()</u> and
       patterns, or use a module like File::KGlob, one that doesn't use the shell to do globbing.

       <b>Is</b> <b>there</b> <b>a</b> <b>leak/bug</b> <b>in</b> <u>glob()</u><b>?</b>

       Due to the current implementation on some operating systems, when you use the <u>glob()</u> function or its
       angle-bracket alias in a scalar context, you may cause a memory leak and/or unpredictable behavior.
       It's best therefore to use <u>glob()</u> only in list context.

       <b>How</b> <b>can</b> <b>I</b> <b>open</b> <b>a</b> <b>file</b> <b>with</b> <b>a</b> <b>leading</b> <b>"&gt;"</b> <b>or</b> <b>trailing</b> <b>blanks?</b>

       (contributed by Brian McCauley)

       The special two argument form of Perl's <u>open()</u> function ignores trailing blanks in filenames and
       infers the mode from certain leading characters (or a trailing "|"). In older versions of Perl this
       was the only version of <u>open()</u> and so it is prevalent in old code and books.

       Unless you have a particular reason to use the two argument form you should use the three argument
       form of <u>open()</u> which does not treat any charcters in the filename as special.

               open FILE, "&lt;", "  file  ";  # filename is "   file   "
               open FILE, "&gt;", "&gt;file";     # filename is "&gt;file"

       <b>How</b> <b>can</b> <b>I</b> <b>reliably</b> <b>rename</b> <b>a</b> <b>file?</b>

       If your operating system supports a proper <a href="mv.1.html#//apple_ref/doc/man/1/mv"><u>mv</u>(1)</a> utility or its functional equivalent, this works:

           rename($old, $new) or system("mv", $old, $new);

       It may be more portable to use the File::Copy module instead.  You just copy to the new file to the
       new name (checking return values), then delete the old one.  This isn't really the same semantically
       as a <u>rename()</u>, which preserves meta-information like permissions, timestamps, inode info, etc.

       Newer versions of File::Copy export a <u>move()</u> function.

       <b>How</b> <b>can</b> <b>I</b> <b>lock</b> <b>a</b> <b>file?</b>

       Perl's builtin <u>flock()</u> function (see perlfunc for details) will call <a href="../man2/flock.2.html#//apple_ref/doc/man/2/flock"><u>flock</u>(2)</a> if that exists,
       <a href="../man2/fcntl.2.html#//apple_ref/doc/man/2/fcntl"><u>fcntl</u>(2)</a> if it doesn't (on perl version 5.004 and later), and <a href="../man3/lockf.3.html#//apple_ref/doc/man/3/lockf"><u>lockf</u>(3)</a> if neither of the two previous
       system calls exists.  On some systems, it may even use a different form of native locking.  Here are
       some gotchas with Perl's <u>flock()</u>:

       1   Produces a fatal error if none of the three system calls (or their close equivalent) exists.

       2   <a href="../man3/lockf.3.html#//apple_ref/doc/man/3/lockf"><u>lockf</u>(3)</a> does not provide shared locking, and requires that the filehandle be open for writing
           (or appending, or read/writing).

       3   Some versions of <u>flock()</u> can't lock files over a network (e.g. on NFS file systems), so you'd
           need to force the use of <a href="../man2/fcntl.2.html#//apple_ref/doc/man/2/fcntl"><u>fcntl</u>(2)</a> when you build Perl.  But even this is dubious at best.  See
           the flock entry of perlfunc and the <u>INSTALL</u> file in the source distribution for information on
           building Perl to do this.

           Two potentially non-obvious but traditional flock semantics are that it waits indefinitely until
           the lock is granted, and that its locks are <u>merely</u> <u>advisory</u>.  Such discretionary locks are more
           flexible, but offer fewer guarantees.  This means that files locked with <u>flock()</u> may be modified
           by programs that do not also use <u>flock()</u>.  Cars that stop for red lights get on well with each
           other, but not with cars that don't stop for red lights.  See the perlport manpage, your port's
           specific documentation, or your system-specific local manpages for details.  It's best to assume
           traditional behavior if you're writing portable programs.  (If you're not, you should as always
           feel perfectly free to write for your own system's idiosyncrasies (sometimes called "features").
           Slavish adherence to portability concerns shouldn't get in the way of your getting your job
           done.)

           For more information on file locking, see also "File Locking" in perlopentut if you have it (new
           for 5.6).

       <b>Why</b> <b>can't</b> <b>I</b> <b>just</b> <b>open(FH,</b> <b>"&gt;file.lock")?</b>

       A common bit of code <b>NOT</b> <b>TO</b> <b>USE</b> is this:

           <a href="../man3/sleep.3.html#//apple_ref/doc/man/3/sleep">sleep(3)</a> while -e "file.lock";      # PLEASE DO NOT USE
           open(LCK, "&gt; file.lock");           # THIS BROKEN CODE

       This is a classic race condition: you take two steps to do something which must be done in one.
       That's why computer hardware provides an atomic test-and-set instruction.   In theory, this "ought"
       to work:

           sysopen(FH, "file.lock", O_WRONLY|O_EXCL|O_CREAT)
                       or die "can't open  file.lock: $!";

       except that lamentably, file creation (and deletion) is not atomic over NFS, so this won't work (at
       least, not every time) over the net.  Various schemes involving <u>link()</u> have been suggested, but these
       tend to involve busy-wait, which is also subdesirable.

       <b>I</b> <b>still</b> <b>don't</b> <b>get</b> <b>locking.</b>  <b>I</b> <b>just</b> <b>want</b> <b>to</b> <b>increment</b> <b>the</b> <b>number</b> <b>in</b> <b>the</b> <b>file.</b>  <b>How</b> <b>can</b> <b>I</b> <b>do</b> <b>this?</b>

       Didn't anyone ever tell you web-page hit counters were useless?  They don't count number of hits,
       they're a waste of time, and they serve only to stroke the writer's vanity.  It's better to pick a
       random number; they're more realistic.

       Anyway, this is what you can do if you can't help yourself.

           use Fcntl qw(:DEFAULT :flock);
           sysopen(FH, "numfile", O_RDWR|O_CREAT)       or die "can't open numfile: $!";
           flock(FH, LOCK_EX)                           or die "can't flock numfile: $!";
           $num = &lt;FH&gt; || 0;
           seek(FH, 0, 0)                               or die "can't rewind numfile: $!";
           truncate(FH, 0)                              or die "can't truncate numfile: $!";
           (print FH $num+1, "\n")                      or die "can't write numfile: $!";
           close FH                                     or die "can't close numfile: $!";

       Here's a much better web-page hit counter:

           $hits = int( (time() - 850_000_000) / <a href="rand.1ssl.html#//apple_ref/doc/man/1/rand">rand(1_000)</a> );

       If the count doesn't impress your friends, then the code might.  :-)

       <b>All</b> <b>I</b> <b>want</b> <b>to</b> <b>do</b> <b>is</b> <b>append</b> <b>a</b> <b>small</b> <b>amount</b> <b>of</b> <b>text</b> <b>to</b> <b>the</b> <b>end</b> <b>of</b> <b>a</b> <b>file.</b>  <b>Do</b> <b>I</b> <b>still</b> <b>have</b> <b>to</b> <b>use</b> <b>lock</b>-<font color="#ffffff" class="whiteout">ing?&nbsp;locking?</font>
       <b>ing?</b>

       If you are on a system that correctly implements <u>flock()</u> and you use the example appending code from
       "perldoc -f flock" everything will be OK even if the OS you are on doesn't implement append mode cor-<font color="#ffffff" class="whiteout">rectly&nbsp;correctly</font>
       rectly (if such a system exists.) So if you are happy to restrict yourself to OSs that implement
       <u>flock()</u> (and that's not really much of a restriction) then that is what you should do.

       If you know you are only going to use a system that does correctly implement appending (i.e. not
       Win32) then you can omit the <u>seek()</u> from the above code.

       If you know you are only writing code to run on an OS and filesystem that does implement append mode
       correctly (a local filesystem on a modern Unix for example), and you keep the file in block-buffered
       mode and you write less than one buffer-full of output between each manual flushing of the buffer
       then each bufferload is almost guaranteed to be written to the end of the file in one chunk without
       getting intermingled with anyone else's output. You can also use the <u>syswrite()</u> function which is
       simply a wrapper around your systems <a href="../man2/write.2.html#//apple_ref/doc/man/2/write"><u>write</u>(2)</a> system call.

       There is still a small theoretical chance that a signal will interrupt the system level <u>write()</u> oper-<font color="#ffffff" class="whiteout">ation&nbsp;operation</font>
       ation before completion.  There is also a possibility that some STDIO implementations may call multi-<font color="#ffffff" class="whiteout">ple&nbsp;multiple</font>
       ple system level <u>write()</u>s even if the buffer was empty to start.  There may be some systems where
       this probability is reduced to zero.

       <b>How</b> <b>do</b> <b>I</b> <b>randomly</b> <b>update</b> <b>a</b> <b>binary</b> <b>file?</b>

       If you're just trying to patch a binary, in many cases something as simple as this works:

           perl -i -pe 's{window manager}{window mangler}g' /usr/bin/emacs

       However, if you have fixed sized records, then you might do something more like this:

           $RECSIZE = 220; # size of record, in bytes
           $recno   = 37;  # which record to update
           open(FH, "+&lt;somewhere") || die "can't update somewhere: $!";
           seek(FH, $recno * $RECSIZE, 0);
           read(FH, $record, $RECSIZE) == $RECSIZE || die "can't read record $recno: $!";
           # munge the record
           seek(FH, -$RECSIZE, 1);
           print FH $record;
           close FH;

       Locking and error checking are left as an exercise for the reader.  Don't forget them or you'll be
       quite sorry.

       <b>How</b> <b>do</b> <b>I</b> <b>get</b> <b>a</b> <b>file's</b> <b>timestamp</b> <b>in</b> <b>perl?</b>

       If you want to retrieve the time at which the file was last read, written, or had its meta-data
       (owner, etc) changed, you use the <b>-A</b>, <b>-M</b>, or <b>-C</b> file test operations as documented in perlfunc.
       These retrieve the age of the file (measured against the start-time of your program) in days as a
       floating point number. Some platforms may not have all of these times.  See perlport for details. To
       retrieve the "raw" time in seconds since the epoch, you would call the stat function, then use <u>local-</u>
       <u>time()</u>, <u>gmtime()</u>, or <u>POSIX::strftime()</u> to convert this into human-readable form.

       Here's an example:

           $write_secs = (stat($file))[9];
           printf "file %s updated at %s\n", $file,
               scalar localtime($write_secs);

       If you prefer something more legible, use the File::stat module (part of the standard distribution in
       version 5.004 and later):

           # error checking left as an exercise for reader.
           use File::stat;
           use Time::localtime;
           $date_string = ctime(stat($file)-&gt;mtime);
           print "file $file updated at $date_string\n";

       The <u>POSIX::strftime()</u> approach has the benefit of being, in theory, independent of the current
       locale.  See perllocale for details.

       <b>How</b> <b>do</b> <b>I</b> <b>set</b> <b>a</b> <b>file's</b> <b>timestamp</b> <b>in</b> <b>perl?</b>

       You use the <u>utime()</u> function documented in "utime" in perlfunc.  By way of example, here's a little
       program that copies the read and write times from its first argument to all the rest of them.

           if (@ARGV &lt; 2) {
               die "usage: cptimes timestamp_file other_files ...\n";
           }
           $timestamp = shift;
           ($atime, $mtime) = (stat($timestamp))[8,9];
           utime $atime, $mtime, @ARGV;

       Error checking is, as usual, left as an exercise for the reader.

       The perldoc for utime also has an example that has the same effect as <a href="touch.1.html#//apple_ref/doc/man/1/touch"><u>touch</u>(1)</a> on files that <u>already</u>
       <u>exist</u>.

       Certain file systems have a limited ability to store the times on a file at the expected level of
       precision.  For example, the FAT and HPFS filesystem are unable to create dates on files with a finer
       granularity than two seconds.  This is a limitation of the filesystems, not of <u>utime()</u>.

       <b>How</b> <b>do</b> <b>I</b> <b>print</b> <b>to</b> <b>more</b> <b>than</b> <b>one</b> <b>file</b> <b>at</b> <b>once?</b>

       To connect one filehandle to several output filehandles, you can use the IO::Tee or Tie::FileHan-
       dle::Multiplex modules.

       If you only have to do this once, you can print individually to each filehandle.

           for $fh (FH1, FH2, FH3) { print $fh "whatever\n" }

       <b>How</b> <b>can</b> <b>I</b> <b>read</b> <b>in</b> <b>an</b> <b>entire</b> <b>file</b> <b>all</b> <b>at</b> <b>once?</b>

       You can use the File::Slurp module to do it in one step.

               use File::Slurp;

               $all_of_it = read_file($filename); # entire file in scalar
           @all_lines = read_file($filename); # one line perl element

       The customary Perl approach for processing all the lines in a file is to do so one line at a time:

           open (INPUT, $file)         || die "can't open $file: $!";
           while (&lt;INPUT&gt;) {
               chomp;
               # do something with $_
           }
           close(INPUT)                || die "can't close $file: $!";

       This is tremendously more efficient than reading the entire file into memory as an array of lines and
       then processing it one element at a time, which is often--if not almost always--the wrong approach.
       Whenever you see someone do this:

           @lines = &lt;INPUT&gt;;

       you should think long and hard about why you need everything loaded at once.  It's just not a scal-<font color="#ffffff" class="whiteout">able&nbsp;scalable</font>
       able solution.  You might also find it more fun to use the standard Tie::File module, or the DB_File
       module's $DB_RECNO bindings, which allow you to tie an array to a file so that accessing an element
       the array actually accesses the corresponding line in the file.

       You can read the entire filehandle contents into a scalar.

           {
               local(*INPUT, $/);
               open (INPUT, $file)     || die "can't open $file: $!";
               $var = &lt;INPUT&gt;;
           }

       That temporarily undefs your record separator, and will automatically close the file at block exit.
       If the file is already open, just use this:

           $var = do { local $/; &lt;INPUT&gt; };

       For ordinary files you can also use the read function.

               read( INPUT, $var, -s INPUT );

       The third argument tests the byte size of the data on the INPUT filehandle and reads that many bytes
       into the buffer $var.

       <b>How</b> <b>can</b> <b>I</b> <b>read</b> <b>in</b> <b>a</b> <b>file</b> <b>by</b> <b>paragraphs?</b>

       Use the $/ variable (see perlvar for details).  You can either set it to "" to eliminate empty para-<font color="#ffffff" class="whiteout">graphs&nbsp;paragraphs</font>
       graphs ("abc\n\n\n\ndef", for instance, gets treated as two paragraphs and not three), or "\n\n" to
       accept empty paragraphs.

       Note that a blank line must have no blanks in it.  Thus "fred\n \nstuff\n\n" is one paragraph, but
       "fred\n\nstuff\n\n" is two.

       <b>How</b> <b>can</b> <b>I</b> <b>read</b> <b>a</b> <b>single</b> <b>character</b> <b>from</b> <b>a</b> <b>file?</b>  <b>From</b> <b>the</b> <b>keyboard?</b>

       You can use the builtin "getc()" function for most filehandles, but it won't (easily) work on a ter-<font color="#ffffff" class="whiteout">minal&nbsp;terminal</font>
       minal device.  For STDIN, either use the Term::ReadKey module from CPAN or use the sample code in
       "getc" in perlfunc.

       If your system supports the portable operating system programming interface (POSIX), you can use the
       following code, which you'll note turns off echo processing as well.

           #!/usr/bin/perl -w
           use strict;
           $| = 1;
           for (1..4) {
               my $got;
               print "gimme: ";
               $got = getone();
               print "--&gt; $got\n";
           }
           exit;

           BEGIN {
               use POSIX qw(:termios_h);

               my ($term, $oterm, $echo, $noecho, $fd_stdin);

               $fd_stdin = fileno(STDIN);

               $term     = POSIX::Termios-&gt;new();
               $term-&gt;getattr($fd_stdin);
               $oterm     = $term-&gt;getlflag();

               $echo     = ECHO | ECHOK | ICANON;
               $noecho   = $oterm &amp; ~$echo;

               sub cbreak {
                   $term-&gt;setlflag($noecho);
                   $term-&gt;setcc(VTIME, 1);
                   $term-&gt;setattr($fd_stdin, TCSANOW);
               }

               sub cooked {
                   $term-&gt;setlflag($oterm);
                   $term-&gt;setcc(VTIME, 0);
                   $term-&gt;setattr($fd_stdin, TCSANOW);
               }

               sub getone {
                   my $key = '';
                   cbreak();
                   sysread(STDIN, $key, 1);
                   cooked();
                   return $key;
               }

           }

           END { cooked() }

       The Term::ReadKey module from CPAN may be easier to use.  Recent versions include also support for
       non-portable systems as well.

           use Term::ReadKey;
           open(TTY, "&lt;/dev/tty");
           print "Gimme a char: ";
           ReadMode "raw";
           $key = ReadKey 0, *TTY;
           ReadMode "normal";
           printf "\nYou said %s, char number %03d\n",
               $key, ord $key;

       <b>How</b> <b>can</b> <b>I</b> <b>tell</b> <b>whether</b> <b>there's</b> <b>a</b> <b>character</b> <b>waiting</b> <b>on</b> <b>a</b> <b>filehandle?</b>

       The very first thing you should do is look into getting the Term::ReadKey extension from CPAN.  As we
       mentioned earlier, it now even has limited support for non-portable (read: not open systems, closed,
       proprietary, not POSIX, not Unix, etc) systems.

       You should also check out the Frequently Asked Questions list in comp.unix.* for things like this:
       the answer is essentially the same.  It's very system dependent.  Here's one solution that works on
       BSD systems:

           sub key_ready {
               my($rin, $nfd);
               vec($rin, fileno(STDIN), 1) = 1;
               return $nfd = select($rin,undef,undef,0);
           }

       If you want to find out how many characters are waiting, there's also the FIONREAD ioctl call to be
       looked at.  The <u>h2ph</u> tool that comes with Perl tries to convert C include files to Perl code, which
       can be "require"d.  FIONREAD ends up defined as a function in the <u>sys/ioctl.ph</u> file:

           require 'sys/ioctl.ph';

           $size = pack("L", 0);
           ioctl(FH, FIONREAD(), $size)    or die "Couldn't call ioctl: $!\n";
           $size = unpack("L", $size);

       If <u>h2ph</u> wasn't installed or doesn't work for you, you can <u>grep</u> the include files by hand:

           % grep FIONREAD /usr/include/*/*
           /usr/include/asm/ioctls.h:#define FIONREAD      0x541B

       Or write a small C program using the editor of champions:

           % cat &gt; fionread.c
           #include &lt;sys/ioctl.h&gt;
           main() {
               printf("%#08x\n", FIONREAD);
           }
           ^D
           % cc -o fionread fionread.c
           % ./fionread
           0x4004667f

       And then hard code it, leaving porting as an exercise to your successor.

           $FIONREAD = 0x4004667f;         # XXX: opsys dependent

           $size = pack("L", 0);
           ioctl(FH, $FIONREAD, $size)     or die "Couldn't call ioctl: $!\n";
           $size = unpack("L", $size);

       FIONREAD requires a filehandle connected to a stream, meaning that sockets, pipes, and tty devices
       work, but <u>not</u> files.

       <b>How</b> <b>do</b> <b>I</b> <b>do</b> <b>a</b> <b>"tail</b> <b>-f"</b> <b>in</b> <b>perl?</b>

       First try

           seek(GWFILE, 0, 1);

       The statement "seek(GWFILE, 0, 1)" doesn't change the current position, but it does clear the end-of-<font color="#ffffff" class="whiteout">file&nbsp;end-offile</font>
       file condition on the handle, so that the next &lt;GWFILE&gt; makes Perl try again to read something.

       If that doesn't work (it relies on features of your stdio implementation), then you need something
       more like this:

               for (;;) {
                 for ($curpos = tell(GWFILE); &lt;GWFILE&gt;; $curpos = tell(GWFILE)) {
                   # search for some stuff and put it into files
                 }
                 # sleep for a while
                 seek(GWFILE, $curpos, 0);  # seek to where we had been
               }

       If this still doesn't work, look into the POSIX module.  POSIX defines the <u>clearerr()</u> method, which
       can remove the end of file condition on a filehandle.  The method: read until end of file, <u>clear</u>-<font color="#ffffff" class="whiteout">err(),&nbsp;clearerr(),</font>
       <u>err()</u>, read some more.  Lather, rinse, repeat.

       There's also a File::Tail module from CPAN.

       <b>How</b> <b>do</b> <b>I</b> <u>dup()</u> <b>a</b> <b>filehandle</b> <b>in</b> <b>Perl?</b>

       If you check "open" in perlfunc, you'll see that several of the ways to call <u>open()</u> should do the
       trick.  For example:

           open(LOG, "&gt;&gt;/foo/logfile");
           open(STDERR, "&gt;&amp;LOG");

       Or even with a literal numeric descriptor:

          $fd = $ENV{MHCONTEXTFD};
          open(MHCONTEXT, "&lt;&amp;=$fd");   # like <a href="../man3/fdopen.3.html#//apple_ref/doc/man/3/fdopen">fdopen(3S)</a>

       Note that "&lt;&amp;STDIN" makes a copy, but "&lt;&amp;=STDIN" make an alias.  That means if you close an aliased
       handle, all aliases become inaccessible.  This is not true with a copied one.

       Error checking, as always, has been left as an exercise for the reader.

       <b>How</b> <b>do</b> <b>I</b> <b>close</b> <b>a</b> <b>file</b> <b>descriptor</b> <b>by</b> <b>number?</b>

       This should rarely be necessary, as the Perl <u>close()</u> function is to be used for things that Perl
       opened itself, even if it was a dup of a numeric descriptor as with MHCONTEXT above.  But if you
       really have to, you may be able to do this:

           require 'sys/syscall.ph';
           $rc = syscall(&amp;SYS_close, $fd + 0);  # must force numeric
           die "can't sysclose $fd: $!" unless $rc == -1;

       Or, just use the <a href="../man3/fdopen.3.html#//apple_ref/doc/man/3/fdopen">fdopen(3S)</a> feature of <u>open()</u>:

           {
               local *F;
               open F, "&lt;&amp;=$fd" or die "Cannot reopen fd=$fd: $!";
               close F;
           }

       <b>Why</b> <b>can't</b> <b>I</b> <b>use</b> <b>"C:\temp\foo"</b> <b>in</b> <b>DOS</b> <b>paths?</b>  <b>Why</b> <b>doesn't</b> <b>`C:\temp\foo.exe`</b> <b>work?</b>

       Whoops!  You just put a tab and a formfeed into that filename!  Remember that within double quoted
       strings ("like\this"), the backslash is an escape character.  The full list of these is in "Quote and
       Quote-like Operators" in perlop.  Unsurprisingly, you don't have a file called "c:(tab)emp(form-
       feed)oo" or "c:(tab)emp(formfeed)oo.exe" on your legacy DOS filesystem.

       Either single-quote your strings, or (preferably) use forward slashes.  Since all DOS and Windows
       versions since something like MS-DOS 2.0 or so have treated "/" and "\" the same in a path, you might
       as well use the one that doesn't clash with Perl--or the POSIX shell, ANSI C and C++, awk, Tcl, Java,
       or Python, just to mention a few.  POSIX paths are more portable, too.

       <b>Why</b> <b>doesn't</b> <b>glob("*.*")</b> <b>get</b> <b>all</b> <b>the</b> <b>files?</b>

       Because even on non-Unix ports, Perl's glob function follows standard Unix globbing semantics.
       You'll need "glob("*")" to get all (non-hidden) files.  This makes <u>glob()</u> portable even to legacy
       systems.  Your port may include proprietary globbing functions as well.  Check its documentation for
       details.

       <b>Why</b> <b>does</b> <b>Perl</b> <b>let</b> <b>me</b> <b>delete</b> <b>read-only</b> <b>files?</b>  <b>Why</b> <b>does</b> <b>"-i"</b> <b>clobber</b> <b>protected</b> <b>files?</b>  <b>Isn't</b> <b>this</b> <b>a</b>
       <b>bug</b> <b>in</b> <b>Perl?</b>

       This is elaborately and painstakingly described in the <u>file-dir-perms</u> article in the "Far More Than
       You Ever Wanted To Know" collection in <a href="http://www.cpan.org/misc/olddoc/FMTEYEWTK.tgz">http://www.cpan.org/misc/olddoc/FMTEYEWTK.tgz</a> .

       The executive summary: learn how your filesystem works.  The permissions on a file say what can hap-
       pen to the data in that file.  The permissions on a directory say what can happen to the list of
       files in that directory.  If you delete a file, you're removing its name from the directory (so the
       operation depends on the permissions of the directory, not of the file).  If you try to write to the
       file, the permissions of the file govern whether you're allowed to.

       <b>How</b> <b>do</b> <b>I</b> <b>select</b> <b>a</b> <b>random</b> <b>line</b> <b>from</b> <b>a</b> <b>file?</b>

       Here's an algorithm from the Camel Book:

           srand;
           rand($.) &lt; 1 &amp;&amp; ($line = $_) while &lt;&gt;;

       This has a significant advantage in space over reading the whole file in.  You can find a proof of
       this method in <u>The</u> <u>Art</u> <u>of</u> <u>Computer</u> <u>Programming</u>, Volume 2, Section 3.4.2, by Donald E. Knuth.

       You can use the File::Random module which provides a function for that algorithm:

               use File::Random qw/random_line/;
               my $line = random_line($filename);

       Another way is to use the Tie::File module, which treats the entire file as an array.  Simply access
       a random array element.

       <b>Why</b> <b>do</b> <b>I</b> <b>get</b> <b>weird</b> <b>spaces</b> <b>when</b> <b>I</b> <b>print</b> <b>an</b> <b>array</b> <b>of</b> <b>lines?</b>

       Saying

           print "@lines\n";

       joins together the elements of @lines with a space between them.  If @lines were "("little",
       "fluffy", "clouds")" then the above statement would print

           little fluffy clouds

       but if each element of @lines was a line of text, ending a newline character "("little\n",
       "fluffy\n", "clouds\n")" then it would print:

           little
            fluffy
            clouds

       If your array contains lines, just print them:

           print @lines;

<b>AUTHOR</b> <b>AND</b> <b>COPYRIGHT</b>
       Copyright (c) 1997-2006 Tom Christiansen, Nathan Torkington, and other authors as noted. All rights
       reserved.

       This documentation is free; you can redistribute it and/or modify it under the same terms as Perl
       itself.

       Irrespective of its distribution, all code examples here are in the public domain.  You are permitted
       and encouraged to use this code and any derivatives thereof in your own programs for fun or for
       profit as you see fit.  A simple comment in the code giving credit to the FAQ would be courteous but
       is not required.



perl v5.8.8                                      2006-01-07                                      PERLFAQ5(1)
</tt></pre>
</td></tr></table>
	<table width="100%" border="0" cellpadding="0" cellspacing="0">
		<tr>
			<td><div style="width: 100%; height: 1px; background-color: #919699; margin-top: 5px; margin-bottom: 15px"></div></td>
		</tr>
		<tr>
			<td align="center"><br/>
				<table border="0" cellpadding="0" cellspacing="0" class="graybox">
					<tr>
						<th>Did this document help you?</th>
					</tr>
					<tr>
						<td>
						    <div style="margin-bottom: 8px"><a href="http://developer.apple.com/feedback/?v=1&url=/documentation/Darwin/Reference/ManPages/man1/perlfaq5.1.html%3Fid%3DTP40000894-7.0&media=dvd" target=_new>Yes</a>:  Tell us what works for you.</div>
							<div style="margin-bottom: 8px"><a href="http://developer.apple.com/feedback/?v=2&url=/documentation/Darwin/Reference/ManPages/man1/perlfaq5.1.html%3Fid%3DTP40000894-7.0&media=dvd" target=_new>It&#8217;s good, but:</a> Report typos, inaccuracies, and so forth.</div>
							<div><a href="http://developer.apple.com/feedback/?v=3&url=/documentation/Darwin/Reference/ManPages/man1/perlfaq5.1.html%3Fid%3DTP40000894-7.0&media=dvd" target=_new>It wasn&#8217;t helpful</a>: Tell us what would have helped.</div>
						</td>
					</tr>
				</table>
			</td>
		</tr>
	</table>

</body></html>
